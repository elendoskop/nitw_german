title: EnterLevel_A1D1
tags:
colorID: 2
position: -13929,1776
---
<<sit>>
<<irisIn 1 wait>>
<<wait 4>>
Mae: So... #line:9cb8af
Mae: Arbeitest du im Ol Pickaxe? #line:f8cd3d
Bea: Yep. #line:71f90f
Mae: Bilden sie dich aus, um das Familienunternehmen zu uebernehmen? #line:a64802
Bea: "Sie?" #line:55943e
Mae: Aehm... deine Eltern? #line:b94bd4
Bea: ... #line:052193
<<close>>
<<wait 1>>
<<stopCarAbruptly>>
<<wait .5>>
Bea: Hey schau wir sind da. #line:c38b04
Mae: Oh, mein Haus ist eigentlich erst ein paar Blocks entfernt. #line:61d7c8
Bea: Hau ab. #line:b01963
Mae: Ok! #line:ce94a4
Mae: Aeh... danke fuers Mitnehmen. #line:16f0f9
Bea: RAUS. #line:669296
[[End_A1D1]]

===
title: EnterLevel_A1D3
tags:
colorID: 2
position: -13088,1780
---
<<sit>>
<<if $after_party is 0>>
	[[ToParty_A1D3]]
<<elseif $after_party>>
	[[BackFromParty_A1D3]]
<<endif>>

===
title: End_A1D1
tags:
colorID: 2
position: -13930,2123
---
<<close>>
<<set $night to 1>>
<<fadeOut 1 0 0 0>>
<<wait 1>>
<<tell CarMode GetOut>>
<<wait 2>>
<<scene MaeHouse left>>
===
title: ToParty_A1D3
tags:
colorID: 2
position: -13364,2173
---
<<irisIn 1 wait>>
<<wait 3>>
<<rotateHead Mae 10 .5>>
<<lookLeft Gregg>>
Gregg: Hey Mae, ich glaube, Cole koennte heute Abend da sein! #line:feda1f
<<if $knows_cole_will_be_there is 1>>
	[[ToParty_Knows]]
<<else>>
	[[ToParty_DoesntKnow]]
<<endif>>

===
title: BackFromParty_A1D3
tags:
colorID: 2
position: -12747,2161
---
<<wait 1>>
<<sit>>
<<rotateHead Mae -10 .5>>
<<lookOut Mae>>
<<dilate Mae 1.2 1>>
Mae: =_= #line:f4e428
<<wait 1>>
<<irisIn 1 wait>>
Bea: Wenn du in dieses Auto kotzt, dann hilf mir... #line:45c451
[[(Ich muss mich bei dir entschuldigen, Bea.)|BackFromParty_Apology]] #line:6478b6
[[(Das ist mir wirklich sehr peinlich.)|BackFromParty_Embarrassed]] #line:b37820
===
title: ToParty_A1D3_End
tags:
colorID: 2
position: -13397,2818
---
<<set $night to 1>>
<<close>>
<<fadeOut 1>>
<<wait 1>>
<<tell CarMode GetOut>>
<<wait 2>>
<<scene PartyClearing>>
===
title: BackFromParty_A1D3_End
tags:
colorID: 2
position: -12658,5079
---
<<close>>
<<fadeOut 1>>
<<wait 1>>
<<tell CarMode GetOut>>
<<wait 2>>
<<scene MaeRoom>>
===
title: ToParty_Knows
tags:
colorID: 2
position: -13561,2496
---
Mae: JA ICH WEISS. =_= #line:e8effa
Gregg: Oje! Entschuldigung. #line:97a093
Mae: Es ist mir egal, ob er da ist. #line:3aa33d
Angus: Vielleicht ist er nicht da. #line:3a0e84
Bea: Nun, hoffen wir es oder nicht. Ich weiss nicht einmal, worueber wir reden. #line:9198c3
Gregg: Mae und Cole waren im ersten Jahr zusammen. #line:a40c06
Mae: Ja, und es hat nicht geklappt. #line:58f955
Mae: Also lass uns aufhoeren, darueber zu reden. =_= #line:fc86ba
Bea: Jepp. #line:0b7c0f
[[ToParty_A1D3_End]]
===
title: ToParty_DoesntKnow
tags:
colorID: 2
position: -13210,2494
---
<<dilate Mae .75 1>>
<<lookOut Mae>>
<<rotateHead Mae 0 .2>>
Mae: WAS?! :o / \ #line:6b1cce
Mae: Oh mein Gott. \ / #line:17e0f7
<<dilate Mae .9 1>>
Bea: Wer ist Cole? #line:bae07a
Angus: AEhm erinnerst du dich an ihn? Er war mit uns im Jahrbuch. #line:d5e677
Bea: Oh COLE. Ja, ich erinnere mich an ihn. #line:44b8db
Gregg: Mae und er waren eine Zeit lang ein Paar. #line:9c1122
Bea: Echt jetzt? Wahnsinn. #line:c6b4b3
Bea: Er war wirklich schlau. #line:6f8552
Mae: =_= #line:6ec756
<<lookLeft Mae>>
<<dilate Mae 1 1>>
<<lookLeft Gregg>>
Mae: Ich bin wirklich schlau! #line:07c288
Gregg: Ja, das bist du, Mann :) #line:317d61
Bea: Na dann ist das ja geklaert. #line:a7f756
[[ToParty_A1D3_End]]
===
title: BackFromParty_Apology
tags:
colorID: 2
position: -12902,2492
---
Mae: [wave]Tudmialeid I weissnimmer was pass iertis[/wave] #line:dc2f90
[[BackFromParty_2]]
===
title: BackFromParty_Embarrassed
tags:
colorID: 2
position: -12535,2500
---
Mae:[wave]BEE Dasis mir so PEINLICH[/wave] #line:c18c63
[[BackFromParty_2]]
===
title: BackFromParty_2
tags:
colorID: 2
position: -12692,2814
---
Bea: Nach was hast du dich so betrunken #line:22fb7e
Bea: Drei Becher verwaessertes Bier? #line:821639
[[(Weisst du noch, als wir mal beste Freunde waren?)|BackFromParty_BestFriends]] #line:4dede7
[[(Ich erinnere mich, als wir als Kinder zusammen abhingen...)|BackFromParty_HungOutAsKids]] #line:65e763
===
title: BackFromParty_BestFriends
tags:
colorID: 2
position: -12904,3076
---
Mae: [wave]wir warnmaa bess de freun dinn en b eea unnich liep dichimmer noch b ee[/wave] #line:c055c5
Bea: ich habe kein Wort davon verstanden #line:1cbab4
[[BackFromParty_3]]
===
title: BackFromParty_3
tags:
colorID: 2
position: -12698,3371
---
Mae: [wave]weissdunoch dassdu michimmer [speed=.5]maaaydaaaaay[/speed] genannthaast?[/wave] #line:5f6ce6
Mae: [wave]und ich nannte dich [speed=.5]beeeeebeeeee[/speed][/wave] #line:468aa8
Bea: Oh du meinst als wir 10 waren? #line:dcb29f
Bea: Nein, ich weiss es nicht. #line:13d77d
Mae: [wave]weissdunoch als wir zusammen Pfadfinder waren[/wave] #line:56b057
Mae: [wave]und wir haben die schiiildkroete gefangen?[/wave] #line:8187c8
Bea: [winkt] Ja. Boxy die Schildkroete. Er ist gestorben. #line:473688
[[(Wieso bist du so gemein zu mir??)|BackFromParty_WhySoMean]] #line:b8b64d
[[(Ich habe das Gefuehl, dass du die ganze Zeit sauer auf mich bist. Warum?)|BackFromParty_WhySoMad]] #line:62aa2a
===
title: BackFromParty_HungOutAsKids
tags:
colorID: 2
position: -12503,3089
---
Mae:[wave]i ch wei ssno ch allsss wirr bess de freun dinnen warrnn b ee e[/wave] #line:67347a
[[BackFromParty_3]]
===
title: BackFromParty_WhySoMean
tags:
colorID: 2
position: -12886,3682
---
Mae: [wave][shake=.03]WIESOBISSU SO GEMEINZZUMIA????[/all]o_o #line:cded09
[[BackFromParty_4]]
===
title: BackFromParty_WhySoMad
tags:
colorID: 2
position: -12474,3679
---
Mae:[wave][shake=.03]WIESOBISSU DIEGANNSEZEIT SO SAUERAUFMICH????[/all]
[[BackFromParty_4]]
===
title: BackFromParty_4
tags:
colorID: 2
position: -12674,3982
---
Mae: *hurp!* #line:590d83
Mae: [wave]Ahhmann wenn ich hier rein kotze, werden deine Eltern so sauer auf mich sein[/wave] =_= #line:adcfc4
Mae: [wave]Sagdeiner mom es tut mir leid okay sie is soo neeeeeeeeeeeeeeett[/wave] #line:9a04c5
Bea: GOTTVERDAMMT MAE #line:abaa0d
Mae: WHAA? o_o #line:a6f74e
Bea: MEINE MUTTER IST TOT. #line:da32ec
Mae: WHAAAAA? :( #line:89dbc9
Bea: SIE STARB. #line:7adfd6
Bea: AN KREBS. #line:9fdde2
Bea: ABSCHLUSSJAHR. #line:8f54cd
Mae: [wave]OHneeeeeein sie war soo neeeeeeeeeeeett![/wave] #line:861cd2
Mae: [wave]Beebee gehzdir guuuht?[/wave] #line:3e01c9
Bea: [WAVE]WIE KANNST DU DAS NICHT WISSEN? #line:32fb54
Bea: HAST DU MEINE TOTE MUTTER VERGESSEN? #line:0b80c9
[[Das war eine schreckliche Gedaechtnisluecke, Bea. Es tut mir so leid.)|BackFromParty_LapseOfMemory]] #line:0cc069
[[(Ich bin beschaemt ueber mein Verhalten. Es tut mir leid.)|BackFromParty_Mortified]] #line:d6142f
===
title: BackFromParty_LapseOfMemory
tags:
colorID: 2
position: -12873,4249
---
Mae: [wave]ich mag nicht, rebmember ANYTHING manchmal[/wave] #line:17a7bf
Mae: [wave]nimm es nicht persoenlich[/wave] #line:b7f84b
[[BackFromParty_5]]
===
title: BackFromParty_Mortified
tags:
colorID: 2
position: -12458,4254
---
Mae: [wave]awe Bee, ich verhalte mich wie'n IDIOT[/wave] #line:43aadc
Mae: [wave]bist du sauer auf mich???[/wave] #line:3e24a2
[[BackFromParty_5]]
===
title: BackFromParty_5
tags:
colorID: 2
position: -12657,4580
---
Bea: WAS IST MIT DIR PASSIERT? #line:98a390
Bea: DU WARST MAL SCHLAU!!! #line:dfcc06
Bea: DU WARST MAL COOL! #line:ac0f28
Bea: DU WARST ES MAL WERT, DASS MAN MIT DIR REDET! #line:249225
Mae: *schnief* :o #line:3b2e58
Bea: Warum bist du ueberhaupt zurueckgekommen? #line:a708fc
Bea: Oh, hat es mit dem College nicht geklappt bei dir? #line:7f361f
Bea: War es UNZULAESSIG? #line:c33112
Bea: Warst du nicht in der MODE? #line:54da99
Mae: *schnueffel* :( #line:0dfe66
Bea: Dafuer haette ich getoetet. #line:0cdb27
Bea: Ich wuerde es immer noch tun. #line:0792dc
Bea: Ich wuerde dich auf der Stelle aus diesem fahrenden Auto schmeissen #line:39d57f
Bea: wenn ich dadurch aufs College gehen koennte. #line:e42eb4
<<setAnimInt Mae crying 1>>
Mae: [shake=.1]WAAHAAAHAAA[/shake] #line:4ee9d2
<<close>>
<<stopCarAbruptly>>
Bea: Wir sind da. Raus aus meinem Auto. #line:2e5fcb
Mae: [size=1][shake=.2]AAAAHHHAA HAAAA HAAAA HAAAA AAAAHAA[/all] #line:27879e
Bea: Oh Gott, lass mich dir rein helfen. #line:775171
[[BackFromParty_A1D3_End]]
===
title: InitLevel_A1D3
tags:
colorID: 2
position: -13082,1535
---
<<if $after_party is 0>>
    <<setSpriteLayer Gregg TownsfolkBG>>
<<else>>
    <<hide Gregg>>
    <<hide Angus>>
<<endif>>

<<setLookAtTalkingCharacters Mae 0>>
<<setLookAtTalkingCharacters Bea 0>>
<<setLookAtTalkingCharacters Gregg 0>>
<<setLookAtTalkingCharacters Angus 0>>
===
title: InitLevel_A1D1
tags:
colorID: 2
position: -13943,1528
---
<<hide Gregg>>
<<hide Angus>>
===
title: InitLevel_A2D3
tags:
colorID: 3
position: -11838,1615
---
<<if $bea_friendship_quest is 3>>
    <<hide Gregg>>
    <<hide Angus>>
    <<setLookAtTalkingCharacters Mae 0>>
    <<setLookAtTalkingCharacters Bea 0>>
    <<setLookAtTalkingCharacters Gregg 0>>
    <<setLookAtTalkingCharacters Angus 0>>
<<endif>>
===
title: EnterLevel_A2D3
tags:
colorID: 3
position: -11869,1909
---
<<sit>>
<<if $bea_friendship_quest is 3>>
    [[BFQ3]]
<<endif>>
===
title: BFQ3
tags:
colorID: 3
position: -11863,2280
---
<<irisIn 1 wait>>
Mae: Das hat Spass gemacht! #line:db1eea
Bea: ja, Lebensmittel. #line:73bafb
Bea: da lernt man das Leben richtig zu schaetzen. #line:dc0b9a
Mae: dein Sarkasmus ist ungueltig. #line:4575ee
Mae: Einkaufen ist fuuuuuuun! #line:7cdcd1
<<wait 3>>
Bea: woohoo. #line:d01819
<<close>>
<<wait 1>>
<<irisOut 1 wait>>
<<tell CarMode GetOut>>
<<wait 2>>
<<scene BeaApartment>>
===
title: EnterLevel_A3D3
tags:
colorID: 4
position: -11001,1894
---
<<sit>>
<<if $bea_friendship_quest is 4>>
    [[BeaFQ4]]
<<elseif $gregg_friendship_quest is 4>>
    [[GreggFQ4]]
<<endif>>
===
title: BeaFQ4
tags:
colorID: 4
position: -11106,2248
---
<<musicFadeOut 1>>
<<irisIn 1 wait>>
Mae: Was fuer eine Party ist das denn? <line:c06dd6
Bea: AEhm. #line:9eb0d5
Bea: Eine Jackie Party? #line:e0dc29
Mae: Was ist eine Jackie Party? #line:4b2f8d
Bea: Erinnerst du dich an Jackie? #line:74c75f
    ->yeah, sie hasst mich. #line:712259
        Bea: Oh, das war vor Jahren. #line:aab1df
        Mae: uh huh. #line:23899d
    ->yeah, sie ist heftig. #line:a10e8e
        Bea: sie ist ernst, wenn es um ernste Dinge geht. #line:8fc866
        Bea: sonst ist sie sehr lustig. #line:b4ab1c
Mae: hatte Jackie nicht so etwas wie eine Ein-Frau-Band? #line:79417d
Bea: ja. #line:01c09c
Bea: "Trans World Order Give Them Hell" #line:75729f
Mae: T.W.O.G.T.H.? #line:707bb4
Bea: Ja. Das ist eigentlich ganz toll. #line:ffee51
Bea: sie hat einen tollen Schrei #line:5afccc
Mae: Ich bin begeistert. #line:c6aab7
<<wait 2>>
Mae: "Twogth" #line:26053d
Mae: Hey Bea sag es. #line:91df7c
Mae: Sag Twogth. #line:8279a5
Bea: Twogth. #line:7e89f6
<<lookOut Mae>>
Mae: Twogth. #line:042c49
<<dilate Mae .75 1>>
Mae: T w o o o o g t h. #line:4ecc9f
<<wait 2>>
Bea: egal #line:bc96f6
<<dilate Mae 1 .25>>
<<lookNearest Mae>>
<<wait 1>>
Bea: das ist eine Party #line:de609d
Bea: und Jackie hat mich eingeladen. #line:ae8c90
Mae: schau dich an! #line:a0564d
Mae: alle gehen auf geheime Partys und so. #line:dcd960
Bea: die sind nicht geheim, Mae. #line:cfefe1
Mae: nun *ich* war nicht eingeladen. #line:621908
Bea: ok. ich lade dich ein. du bist jetzt eingeladen. #line:532da2
Mae: woohoo! #line:62ef35
Bea: ok wir haben ungefaehr 90 Minuten Fahrt vor uns, also... #line:dbf70f
Mae: Mensch, das wird hoffentlich eine tolle Party. #line:64244f
Bea: Jackie Partys sind die Reise wert. #line:9c7d82
<<close>>
<<fadeOut 1 wait>>
<<scene BeaTown>>
===
title: EnterLevel_A2D1
tags: demo2
colorID: 3
position: -12185,1943
---
<<sit>>
<<if $bea_friendship_quest is 1>>
    <<lookDown Mae>>
    <<lookOut Bea>>
    <<dilate Bea .75 .1>>
    <<wait 1>>
    <<irisIn 1 wait>>
    Mae: Bin froh, dass ich hier nicht gekotzt habe. #line:0ffeaf
    Bea: Ich auch. #line:977681
    Mae: vielleicht trinke ich einfach nie wieder. #line:9b111f
    Bea: oderrrr vielleicht nimmst du es das naechste Mal einfach leichter. #line:310058
    <<lookAt Mae Bea>>
    Mae: ich weiss nicht, ob ich mich gut maessigen kann. #line:f64e6b
    Bea: das koennte ich sehen. #line:9b7307
    Mae: ich glaube, der Begriff ist "risikofreudiges Verhalten". #line:4ed73b
    <<dilate Bea 1 .1>>
    Bea: -_- #line:645f29
    <<lookLeft Bea>>
    Bea: ...triffst du dich mit jemandem wegen dieser Sache? #line:09539f
    Mae: Mit dir zusammen zu sein, funktioniert heute Abend ziemlich gut. #line:e1f502
    Bea: ... #line:c544fa
    -> ... #line:a3d1bc
    -> Kein Druck. #line:6bda1e
    Bea: Ich sollte stundenweise bezahlt werden. #line:3c920e
    Mae: Ich habe Kaugummi. #line:c5ae58
    Bea: Ich werde jetzt das Radio einschalten. #line:2f8028
    <<close>>
    <<playMusic bea_dance_in_car>>
    <<close>>
    <<wait 1>>
    <<tell CarMode GetOut>>
    <<wait 2>>
    <<irisOut 1 wait>>
    <<sectionTitle BeaFQ1Intro FortLucenneMall>>
<<endif>>

===
title: InitLevel_A2D1
tags:
colorID: 3
position: -12150,1628
---
<<if $bea_friendship_quest is 1>>
    <<hide Gregg>>
    <<hide Angus>>
    <<setLookAtTalkingCharacters Mae 0>>
    <<setLookAtTalkingCharacters Bea 0>>
    <<setLookAtTalkingCharacters Gregg 0>>
    <<setLookAtTalkingCharacters Angus 0>>
<<endif>>
===
title: InitLevel_A3D3
tags:
colorID: 4
position: -10967,1595
---
<<if $gregg_friendship_quest is 4>>
    <<hide Bea>>
    <<setLookAtTalkingCharacters Mae 0>>
    <<setLookAtTalkingCharacters Bea 0>>
    <<setLookAtTalkingCharacters Gregg 0>>
    <<setLookAtTalkingCharacters Angus 0>>
    <<warp Angus AngusDrive>>
    <<warp Mae MaeBackSeat>>
    <<warp Gregg GreggPassengerSeat>>
    <<sit Gregg>>
    <<setSpriteLayer Gregg FG1>>
    <<setSpriteLayer Angus FG1>>
    <<setSortingLayerName MainCast1>>
<<elseif $bea_friendship_quest is 4>>
    <<hide Gregg>>
    <<hide Angus>>
    <<setLookAtTalkingCharacters Mae 0>>
    <<setLookAtTalkingCharacters Bea 0>>
    <<setLookAtTalkingCharacters Gregg 0>>
    <<setLookAtTalkingCharacters Angus 0>>
<<endif>>
===
title: GreggFQ4
tags:
colorID: 4
position: -10842,2267
---
<<lookAt Mae Gregg>>
<<musicFadeOut 1>>
<<irisIn 1 wait>>
Mae: Und das sind alle Leute, die ich gekuesst habe. #line:c62cd3
Gregg: das sind 2 Leute #line:340691
Gregg: Irgendwie #line:4e69b3
Gregg: eineinhalb #line:e3b83f
Mae: wie auch immer. Als ob dein Durchschnitt besser waere. #line:6cefd7
<<close>>
<<wait 1>>
<<lookAt Mae Angus>>
<<wait 1>>
<<lookNearest Mae>>
Mae: Angus du bist wirklich still gewesen. #line:622948
Angus: ... #line:e4093e
Angus: Entschuldigung. #line:aa0d19
Mae: bist du okay? #line:2f8398
Angus: ... #line:d5a855
<<lookAt Mae Gregg>>
Gregg: Wir werden Pfannkuchen bekommen!!! #line:2c9f2f
<<lookAt Mae Angus>>
Angus: ... #line:b214ff
<<close>>
<<wait 2>>
Mae: Hey Angus #line:06d5bc
Mae: Angus #line:db3098
Mae: Hey #line:18be2e
Mae: ich hole die Pfannkuchen #line:1560e2
Mae: Angus #line:1f70b0
<<close>>
<<wait 2>>
Mae: *crrrrsch!* #line:eafd54
Mae: "Aufruf fuer Doktor Jackson..." #line:4d9e74
<<close>>
<<wait 1>>
Mae: "...Doktor Flap Jackson..." #line:41e3e0
Mae: "...bitte melden Sie sich in meinem Bauch..." #line:9a848c
Angus: OK JA PFANNKUCHEN #line:920f89
Gregg: Alter. #line:8e9896
Mae: Alter. #line:9666c1
<<close>>
<<wait 2>>
<<tell CarMode FlatTire>>
<<wait 2>>
Angus: Gott #line:c295c9
Angus: Verdammt #line:00b841
Angus: Es #line:cdd87e
Gregg: Whoa, haben wir einen Platten? #line:5399bb
Angus: Ich fahre rechts ran. #line:2daf5e
Mae: Nein! Donut Wolf! #line:617d7a
Angus: Mae. #line:165248
<<close>>
<<tell CarMode StopWithFlatTire>>
<<wait 2>>
Gregg: Wir werden es schaffen. Wir werden das Auto schieben. #line:25c98d
Angus: Ok, ihr bleibt alle hier drin. #line:114e50
<<close>>
<<irisOut 1 wait>>
<<scene Roadside>>
===
title: PreInitLevel
tags:
colorID: 0
position: -14388,1188
---
<<fadeIn 100000>>
===
title: InitLevel_A3D2
tags:
colorID: 4
position: -10401,1607
---
<<if $angus_investigation_quest>>
    [[InitAngusIQ]]
<<endif>>
===
title: InitAngusIQ
tags:
colorID: 4
position: -10130,1966
---
<<warp Angus AngusDrive>>
<<hide Bea>>
<<hide Gregg>>
===
title: InitLevel_A3D4
tags:
colorID: 4
position: -9791,1602
---
<<if $angus_investigation_quest>>
    [[InitAngusIQ]]
<<endif>>
===
title: EnterAngusIQ
tags:
colorID: 4
position: -10318,2699
---
<<sit>>
<<irisIn 1 wait>>
===
title: EnterLevel_A3D2
tags:
colorID: 4
position: -10340,2302
---
<<if $angus_investigation_quest>>
    <<if $did_angus_constellations>>
        [[EnterAngusIQEnding]]
    <<else>>
        [[EnterAngusIQ]]
    <<endif>>
<<endif>>
===
title: EnterLevel_A3D4
tags:
colorID: 4
position: -9875,2311
---
<<if $angus_investigation_quest>>
    <<if $did_angus_constellations>>
        [[EnterAngusIQEnding]]
    <<else>>
        [[EnterAngusIQ]]
    <<endif>>
<<endif>>
===
title: EnterAngusIQEnding
tags:
colorID: 4
position: -9928,2693
---
<<sit>>
<<irisIn 1 wait>>
<<set $night to 1>>
<<set $did_angus_investigation_quest to 1>>
<<if $act is 3 and $day is 2>>
    <<set $did_angus_investigation_quest_on_a3d2 to 1>>
<<endif>>
<<set $completed_investigation_quests += 1>>
<<set $angus_investigation_quest to 0>>
<<unlockDoodle AngusIQ>>
<<unlockDoodle AngusPsychic>>
Angus: *roeoeoeoechel* #line:d3fe32
Angus: *roeoeoeoeoeoeoeoeoeoeoeoechel* #line:39c17e
Mae: Bist du okay, Grosser? #line:26f63a
Angus: Ja, in ein paar Minuten geht's mir wieder gut. #line:784104
Angus: Ich bin unverwuestlich. #line:424cfd
Mae: Das war's also! Das war der Geist! #line:24efa4
Angus: Oder *roechel* es war *roechel* irgendein Spinner, der da draussen lebt. #line:20505c
Angus: da hinten feiern wahrscheinlich viele Kinder Partys #line:9a80b7
Angus: Ich meine die Party, zu der wir gegangen sind, wo du dich uebergeben hast #line:5a1f30
Mae: ugh #line:1f4907
Angus: das war nicht *SO* weit weg von dort. #line:e6eea1
<<if $act is 3 and $day is 4>>
    Mae: WARUM NOB- #line:b1c16d
    <<close>>
    //freakout animation and sound plays//
    <<enableAnimator BeaCar>>
    <<playAnimation Mae MaeFreakoutHeadRight>>
    <<trigger BeaCar Freakout>>
    <<wait 12>>
    <<playAmbientSFX _AmbientSFX:mae_head_hurt 3>>
    <<wait 3>>
    Mae:{width=4} AAAAAAAAAAAAAAAAAGH!!! #line:fc7790
    Angus: Mae? #line:998090
    Mae:{width=4} AAAAAUUGHHHGH. #line:01a8c7
    Angus: Was ist los?!?! #line:b731b2
    Angus: Soll ich rechts ranfahren? #line:4cb825
    Mae: Igitt. Mein Kopf... igitt... ugh #line:33bcf1
    Angus: Ok, dann lass uns dich nach Hause bringen... #line:77f1e0
    Mae: NEIN! #line:8e589a
    Mae: Muss mit... allen... heute Abend reden... #line:22406f
    Angus: Mit wem? #line:01f8f6
    Mae: Alle von uns. #line:341d49
    Mae: Ich brauche uns alle. #line:a56dfb
    <<fadeOut 1 wait>>
    <<scene GreggAngusApartment>>
<<else>>
    //if A3D2//
	Mae:{width=4} uuuuuuuuuuuuuuuuuuuuuug #line:b802f1
	Mae: WARUM GLAUBT MIR NIEMAND? #line:212276
    Angus: Mae es ist wie #line:6c3796
    Angus: jede andere Erklaerung ist wahrscheinlicher #line:9450c9
    Angus: als "Geist". #line:906f82
    Mae: ok gut, was auch immer. #line:7c5dbb
    <<wait 1>>
    Mae: Entschuldigung. #line:ce061d
    Mae: muerrisch. #line:bf560c
	Mae: Kopfschmerzen. #line:06d5a2
    Mae: das war gruselig. #line:a0c6af
    Angus: das ist cool. #line:32d78a
    Angus: das war es. #line:a16f1a
	<<fadeOut 1 wait>>
	<<scene MaeHouse left>>
<<endif>>
===
