title: Bill_A1D3
tags: 
colorID: 2
position: -677,9063
---
Bea: Ja Bill. Schreib es auf wie auf der Uhr. #line:fc0976
Bill: Das wird ihr nicht gefallen. #line:99274c
Bea: Es ist mir egal, wenn es ihr nicht gefaellt. #line:5c6640
Bea: Es war ihre Schuld. Schlicht und einfach. #line:9aef14
Bea: Das ist zusaetzliche Zeit. Das stellen wir in Rechnung. #line:619372
Bill: Also gut! #line:fdf482
<<close>>
<<flip Bill -1>>
<<walk Bill BillExit>>
<<walk Player CentreTalkFront wait>>
[[Hey, Bea!|Bea_A1D3_Hey]] #line:605875
[[Was sollte das denn?|Bea_A1D3_WhatWasThat]] #line:0c5e3e
===
title: Bea_A1D3_WhatWasThat
tags: 
colorID: 2
position: -557,9351
---
<<flip Bea 1>>
<<walk Bea RegisterMed wait>>
<<lookAt Bea RegisterLook>>
Bea: Ugh. Frag gar nicht erst. #line:521916
Bea: Frau Miranda hat einen unserer Jungs schon zweimal in ihrem Keller eingesperrt. #line:2159d4
Mae: Und warum? #line:274165
<<lookAt Bea Player>>
Bea: Weil sie alt ist und vergisst. #line:03187b
Bea: Vielleicht erinnert sie sich das naechste Mal, wenn sie eine Rechnung dafuer bekommt. #line:4a6424
[[Bea_A1D3_2]]
===
title: Bea_A1D3_Hey
tags: 
colorID: 2
position: -781,9342
---
<<lookAt Bea Player>>
Bea: Hi. #line:5a5ec0
Mae: Was macht die Arbeit? #line:4a1bd9
<<flip Bea 1>>
<<walk Bea RegisterMed>>
<<lookAt Bea RegisterLook>>
Bea: Stressig. Das Ende des Monats steht bevor. #line:92acf5
Bea: Und ich muss anfangen, Wintersachen zu bestellen. #line:e9a364
Bea: Was bedeutet, dass ich einen oder hoffentlich zwei der Jungs bestechen muss #line:242f9f
Bea: Um die Lastwagen zu entladen. #line:1ee1b8
Bea: Weil ich nicht alleine einen Lastwagen voller Steinsalz reinbringen werde. #line:4dbb59
[[Bea_A1D3_2]]
===
title: Bea_A1D3_2
tags: 
colorID: 2
position: -672,9646
---
Mae: Huh. #line:9302a5
<<lookAt Bea Player>>
Bea: Was willst du denn? #line:8d8e87
Mae: Nun, Gregg hat mich gebeten, dich zu fragen, ob du uns heute Abend zu der Party fahren wuerdest? #line:4ee84e
Mae: Du weisst schon, die oben im State Park? #line:00a839
Bea: Danke fuer die Klarstellung. #line:d1fa3e
Bea: Sonst waere ich vielleicht verwirrt gewesen #line:5fb535
Bea: Welche der Dutzenden von Partys #line:493d92
Bea: die in jeder beliebigen Nacht in Possum Springs abgehen #line:1b453a
Bea: auf die du dich bezogen hast. #line:1b70d5
Mae: ...bist du sarkastisch? -_- #line:ac1f4d
<<close>>
<<flip Bea -1>>
<<walk Bea BeaMid wait>>
<<lookAt Bea Player>>
Bea: Ich werde fahren. #line:ccb61e
Mae: o_o :) Super! Danke! #line:ff37e4
Bea: Wir sind um 8:30 Uhr bei dir zu Hause. #line:ae9f9c
Bea: Sei bereit, sonst gehen wir ohne dich. #line:2c8c55
Mae: Ok! :| #line:d06ae4
Bea: Verirrt euch nicht auf dem Heimweg. #line:879b1b
Mae: Ich weiss, wo ich wohne. #line:209627
Bea: Nein, im Ernst. Geh nach Hause und mach dich fertig. #line:e1ef42
Mae: Schon gut, schon gut! Oje. #line:d6487f
<<unlockDoodle Bea1>>
<<set $mom_done to 0>>
<<set $party_prep to 1>>
<<set $bea_can_drive to 1>>
<<set $bill_done to 1>>
<<set $trigger_molly to 1>>
<<set $bea_done to 1>>
===
title: InitLevel
tags: 
colorID: 0
position: -1456,8201
---
<<setSpriteLayer Bea BG1>>
<<flip Bea -1>>

<<if $act is 2 and $day is 3>>
    <<show Germ>>
    <<setSpriteLayer Germ BG6>>
<<else>>
    <<hide Germ>>
<<endif>>

<<if $act is 1 and $day is 3>>
    <<if $bill_done is 0>>
        <<show Bill>>
    <<endif>>
<<endif>>
===
title: A2D3_00
tags: 
colorID: 3
position: -1252,9222
---
<<if $bea_day_intro is 0>>
    <<set $bea_day_intro to 1>>
    Bea: Hey Mae. #line:efe594
    Mae: Wie geht's der Spitzhacke? #line:671a2e
    Bea: versucht, mich zu toeten #line:7f41ab
    Bea: Erfolglos #line:9dff80
    Bea: bis jetzt zumindest. #line:e54726
    Mae: was ist so anstrengend? #line:dabdf8
    Bea: naja, zusaetzlich zu allem anderen #line:d996fd
    Bea: wir veranstalten morgen dieses bloede Harfestspiel. #line:ad9e10
    Mae:{width=8} oh woooooooooooooooooow. #line:d18b5e
    Bea: Ja. Ich werde nie wieder anbieten, das zu tun. #line:91c6c8
    Mae: was hast du heute Abend vor? #line:ab4ab6
    Bea: aeh. nichts. #line:ec34c4
    Bea: ich bin super muede. #line:bfd5df
    Bea: weil alles in meinem Leben anstrengend ist #line:eb3906
    Bea: und ich mache die Arbeit von etwa 3 Angestellten, plus einem Ladenbesitzer #line:e3f64a
    Bea: auch wenn ich nur eine Angestellte bin und mir der Laden nicht gehoert. #line:ee4092
    Mae: Oh seltsam. Ich dachte, aus irgendeinem Grund gehoert der Laden jetzt dir. #line:6e7aef
    Bea: das koennte man meinen. #line:4a3efd
    Bea: aber auch nein. ich wuerde lieber sterben. #line:5bbe06
    Bea: mach mir buchstaeblich einen Sarg #line:c8f4d6
    Bea: mit Materialien und Werkzeugen aus diesem Laden #line:01de3d
    Bea: und begrabe mich irgendwo anders. #line:b4b6f0
    Mae: Mensch, das war aber heftig. #line:45133a
    Bea: aaaaaaaaaaaaaaagh. #line:f0ed69
    Bea: wie auch immer. ich werde heute Abend nichts tun. #line:a5f9c9
    Mae: ich kann auch nichts tun! #line:54a3b5
    Bea: ... #line:280981
    Bea: gut. #line:5478df
<<endif>>

Bea: Also machen wir das? #line:1dd7ce
-> Nicht sicher! Wir muessen noch ein paar andere Stopps einlegen. #line:89f3dc
    Bea: OK. #line:7216ce
-> Lass uns abhaengen! #line:c280bd
    [[GermShowsUp]]
===
title: GermShowsUp
tags: 
colorID: 3
position: -1313,9484
---
<<close>>
<<set $bea_friendship_quest to 3>>
<<walk Germ GermComeOut wait>>
Germ: Hi Bea. #line:07b422
Germ: Hi Mae. #line:4de71f
<<close>>
<<flip Bea -1>>
Bea: Hi Germ! #line:2efc23
Mae: Hey Kumpel. #line:e6a12b
Bea: Germ hilft mir, Sachen fuer Geld unter dem Tisch zu transportieren. #line:13c0fa
<<close>>
<<flip Germ -1>>
<<walk Germ GermExitBack wait>>
Bea: Naja. #line:ef8f06
Bea: Ich mache frueher Feierabend, um ein paar Lebensmittel zu besorgen. Willst du mitkommen? #line:69a127
Mae: Klar! #line:25ea41
<<close>>
<<flip Bea 1>>
<<walk Bea BeaOffRightBack wait>>
<<warp Bea BeaOffRight>>
<<flip Bea -1>>
<<setSpriteLayer Bea BeaFQ>>
<<walk Bea BeaBeforeExitLeft wait>>
Bea: Bereit zu gehen? #line:f86340
<<close>>
<<trackAllSpeechBubblesToCharacters 1>>
<<flip Germ 1>>
<<walk Germ GermRightBack>>
<<wait .5>>
Germ:{width=3} Du hast eine Menge Haemmer! #line:b21725
Germ: heilige Scheisse, es gibt so viele Arten von Naegeln! #line:77b234
<<close>>
Mae: Na gut. #line:897e71
Bea: Er ist wie etwas aus einem Kinderbuch #line:70261e
Bea: ueber was auch immer er ist. #line:bfea88
<<close>>
<<irisOut 1 wait>>
<<sectionTitle BeaFQ3Intro HamPanther>>
===
title: A2D1_00
tags: 
colorID: 3
position: -1851,9120
---
Mae: Hey Bea. #line:0ce03e
Bea: Yyyyo. #line:13fede
Mae: Tut mir leid wegen gestern Abend. #line:caa951
Mae: Mir wird von Erkaeltungsmedizin schwummrig. #line:3e4bc3
Mae: Also ich weiss nicht wirklich, was ich erwartet habe, was passieren wuerde. #line:61eff4
Bea: Tut mir leid, was ich gesagt habe. #line:3d5c1e
Mae: Weisst du, ich erinnere mich nicht wirklich an viel. #line:c4d820
Mae: Na ja, was soll's. #line:18fa6b
Bea: Ein Hoch auf den Alkohol. #line:d46a42
Mae: Neues Stadtmotto. #line:3b2ea4
===
title: A2D1_01
tags: 
colorID: 3
position: -2125,9323
---
Mae: Bandprobe spaeter? #line:2206f4
Bea: Ha ha. Jepp. #line:8e5442
Mae: Ich bin erstaunt, dass es immer noch so ist. #line:f805b7
Mae: Wir waren in der Highschool nicht mal eine Band. #line:90a371
Bea: Eh. Es ist lustig. Man kann mit Angus rumhaengen. #line:b2b477
Mae: Wie bist du ueberhaupt dazu gekommen, das zu tun? #line:851e1e
Bea: Als Casey die Stadt verlassen hat, hat Angus mir erzaehlt, dass Gregg sehr traurig war. #line:8fc642
Bea: Also meldete ich mich freiwillig als neuer Schlagzeuger. #line:479781
Mae: Nun, "Schlagzeuger". #line:cf460a
Mae: Das ist ein Laptop, Bea. #line:3933e8
Bea: Das ist er, Mae. Das ist es wirklich. #line:e480a2
===
title: A2D1_02
tags: 
colorID: 3
position: -2392,9531
---
Bea: Ich seh dich spaeter. Ich habe noch etwas zu tun. #line:71eec0
<<set $bea_done to 1>>
===
title: Bea_Act2
tags: 
colorID: 3
position: -1533,8878
---
<<if $day is 1>>
    <<if $bea is 0>>
        <<set $bea += 1>>
        [[A2D1_00]]
    <<elseif $bea is 1>>
        <<set $bea += 1>>
        [[A2D1_01]]
    <<elseif $bea is 2>>
        <<set $bea += 1>>
        [[A2D1_02]]
    <<endif>>
<<elseif $day is 2>>
    <<if $bea is 0>>
        [[A2D2_00]]
    <<endif>>
<<elseif $day is 3>>
    <<if $bea is 0>>
        [[A2D3_00]]
    <<endif>>
<<elseif $day is 4>>
    <<if $bea is 0>>
        [[A2D4_00]]
    <<endif>>
<<endif>>
===
title: Bea
tags: 
colorID: 3
position: -630,8416
---
<<if $act is 2>>
    [[Bea_Act2]]
<<elseif $act is 3>>
    [[Bea_Act3]]
<<endif>>
===
title: A2D2_00
tags: 
colorID: 3
position: -1556,9158
---
<<if $bea_day_intro is 0>>
    <<set $bea_day_intro to 1>>
    Mae: heyyyyy beabea. #line:bc3ce2
    Bea: hey Mae. #line:fd551f
    Mae: wie geht's? #line:da7f98
    Bea: habe eine Ladung Steinsalz zu transportieren. #line:7eebe8
    Bea: kam dieses Jahr frueh rein. #line:e09dd5
    Bea: und eine *Menge* von Schneeschaufeln... #line:35d3c5
    Bea: normalerweise baut der Haendler nicht so einen Mist. #line:d09195
    Mae: vielleicht wird er von Wetterzauberern geleitet? #line:4fc718
    Bea: Ja, das ist keine schlechte Theorie. #line:3d04bb
    Bea: also was ist los? #line:bb94c8
    Mae: wollen wir heute Abend abhaengen???? #line:80c74a
    Bea: aeh. ich arbeite. #line:e60f40
    Bea: wenn du Lust hast #line:47c376
    Bea: komm mit. #line:140aa2
    Mae: oh ich dachte wir koennten in Hunwick Putt-Putt spielen gehen. #line:89cf8d
    Bea: Mae das ist... #line:df8c0b
    Bea: eigentlich keine schlechte Idee... #line:2803e7
    Bea: aber sorry, ich muss arbeiten. #line:85249f
    Mae: awww geez. was ist ueberhaupt der Punkt? #line:ad3ead
    Bea: Gehaltsschecks, Miete, Essen, Medizin, solche Sachen. #line:eeb9b6
<<endif>>
Bea: Willst du nun mitkommen oder nicht? #line:797cdd
-> Hab heute viel zu tun. #line:f3cd7f
    Bea: Okay! #line:856cb7
-> Lass uns abhaengen! #line:c49a37
    <<set $bea_friendship_quest to 2>>
    <<irisOut 1 wait>>
    <<sectionTitle BeaFQ2Intro MirandaYard>>
===
title: EnterLevel_A2D4
tags: 
colorID: 3
position: -209,8829
---
<<if $harfest_costume is 1>>
    [[EnterHarfest]]
<<endif>>
===
title: EnterHarfest
tags: 
colorID: 3
position: -179,9092
---
<<playAmbientSFX _AmbientSFX:play_pre_audience>>
Mae: Hey hey. #line:f7894e
Bea: ... #line:d8fdb6
Mae: Was hast du vor? #line:7335c9
<<wait 2>>
Bea: ernsthaft? #line:734522
Mae: :) #line:2250e3
<<wait 2>>
Bea: Wir veranstalten dieses Jahr den Harfest-Wettbewerb. #line:301ab3
Mae: oh richtig ok #line:ba3866
Mae: Live von der Ol' Pickaxe! #line:36f236
Mae: :| #line:db69e6
Mae: Wann soll das Ding denn losgehen? #line:9af17e
Bea: Aeh... in 10 Minuten... glaube ich? #line:57c1fc
Mae: Denkst du? #line:cd5432
Bea: Ugh, das ist ein Alptraum. #line:63cc2a
Bea: Warum habe ich jemals zugestimmt, das zu tun? #line:e3040b
Bea: Die Handelskammer schuldet mir viel dafuer. #line:5cbe48
Mae: Oje. Ich hoere dich. #line:8f045a
Bea: ... #line:4868d6
Bea: ... #line:84a32a
Mae: Was? #line:fe29e8
Bea: ok, also, die Sache ist die - du wirst nicht viele Zeilen haben... #line:66722b
Mae: Was?! #line:0d8cf7
Bea: Ich bin schon 3 mal nicht erschienen! #line:11f677
Mae: Warum? #line:48dffa
Bea: WEIL DIE GANZE SACHE BLOeD IST UND SICH NIEMAND DAFUeR INTERESSIERT!!! #line:9ea9d4
Mae: Bea, #line:fb4e90
Bea: Bitte? Das ist ein lustiges Teil! #line:b03a36
Mae: Bea. #line:1f9399
Bea: Ja? #line:4a216b
Mae: Ich werde dir auf jeden Fall den Arsch retten. #line:8bf53a
Mae: Ich habe das vielleicht noch auswendig gelernt, als ich ein Kind war. #line:f5c6eb
Bea: Wahrscheinlich nicht. Sie haben es in den letzten 2 Jahren etwa 6 mal umgeschrieben #line:f871e3
Bea: um gruseliger zu sein #line:c86f62
Bea: also sind es jetzt etwa 20% tatsaechliche Geschichte #line:38bae7
Bea: und etwa 80% Spuk. #line:5512e0
->Also ist es jetzt gefaelschte Geschichte? #line:876299
    Bea: Es ist wie... das schlechte Remake #line:c29dfb
    Bea: die das Ausgangsmaterial nicht respektiert. #line:5d4c72
->Ok seltsam. #line:07a01e
Bea: es faengt normal an, aber wird... trotzdem. #line:2d7635
Bea: Du hast etwa 5 Minuten Zeit, dir deine Zeilen anzusehen. #line:0cea08
Mae: Pssh. Ich kann es improvisieren. #line:015c62
Bea: Mae, bitte befluegeln Sie es nicht. #line:49d514
Mae: ok. #line:0aa559
<<fadeOut 1 wait>>
<<stopAmbientSFX _AmbientSFX:play_pre_audience>>
<<show Bill>>
<<setSpriteLayer Bill BG2>>
<<set $bill_hat to 1>>
<<playerUpdateFallDuringInteract 1>>
[[PlayScene1]]
===
title: PlayScene1
tags: 
colorID: 3
position: 161,9023
---
<<tell _AmbientSFX:amb_play Play1>>
<<tell HarfestPlayWatchers Show>>
<<cameraLookAt HarfestCamHigh 0>>
<<warp Mae CounterOffRight>>
<<warp Dan CounterOffLeft2>>
<<warp Bill CounterOffLeft>>
<<warp Bea BeaCounter>>
<<show Dan>>
<<setAnimInt Dan harfestIdle 1>>
<<fadeIn 1 wait>>
<<cameraLookAt HarfestCamLow 6>>
//organ fanfare as camera descends from ceiling//
<<playOneShot event:/music/harfest_organ_00>>
<<wait 7>>
<<walk Dan DanCounter wait>>
<<walk Bill BillCounter wait>>
//will do actual stage blocking after dialogue is in//
<<wait 1>>
<<dilate Bea .75 10>>
<<lookOut Bea>>
Bea: Oh! Ich habe euch gar nicht gesehen! #line:acd1f8
Bea: Seid ihr Geister? #line:bc48a6
Bea: Seid ihr hier, um zu hoeren #line:bc7b5b
Bea: die Geschichte, wie Possum Springs entstanden ist? #line:7bbfe0
<<tell HarfestPlayWatchers Happy>>
<<wait 3>>
<<dilate Bea 1 10>>
<<lookLeft Bea>>
Bea: Durch die Nebel der Zeit sehe ich #line:44ffdb
Bea: Muede Pelztierjaeger, von 1793. #line:286657
//dan enters//
Dan: Bruder John! #line:1e3a53
//bill enters//
Bill: Ja, Bruder Steven! #line:a650e0
Dan: Von diesem Aussichtspunkt aus erspaehe ich eine tiefe Hoehle #line:f4d9b3
Dan: und darin einen grossen toten Baum #line:f5a396
Dan: und ausserdem mit... aeh, welcher, einer Quelle! #line:5e280c
Bill: Gesegnet sind wir, Bruder Steven, #line:77dfc0
Bill: denn wahrlich, meine Kehle ist ausgedoerrt #line:e592d7
Bill: von diesen langen und beschwerlichen Reisen #line:901dbc
Bill: und dem Tragen dieser 300 Biberfelle. #line:d48bf5
Dan: Lass uns eilen, oh ausgezeichneter Bruder! #line:77f692
<<close>>
<<walk Mae MaeCounter wait>>
<<shuffleNextOptions>>
-> Ho! Reisende! Habt ihr eine Brotkruste fuer eine beduerftige Frau uebrig? #line:7216ac
    <<tell HarfestPlayWatchers Happy>>
    <<set $play_correct_responses += 1>>
-> Ho! Reisende! Hilfst du einer sehr alten Frau? #line:108719
    Mae: aeh. bitte helfen. #line:7016fb
    Bea: Wa-? #line:aafcfb
    <<lookForward Mae>>
    <<playOneShot event:/music/harfest_organ_mistake>>
    <<tell HarfestPlayWatchers Grumble>>
    <<shuffleNextOptions>>
    -> Oh nein, ich bin aus Spinnweben gemacht, weil ich so alt bin! #line:e5a2fb
        Bea: Aeh. Die Reisenden hatten keine Ahnung, was das bedeutete. #line:8f5299
    -> Ich muss Nahrung haben, um meine jugendliche Form wiederherzustellen, denn einst war ich ein huebsches Maedchen. #line:a27f9b
        Bea: ... #line:94994a
    <<tell HarfestPlayWatchers Grumble>>
<<lookForward Mae>>
//witchy organ riff//
<<playOneShot event:/music/harfest_organ_01>>
<<wait 5>>
<<lookNearest Mae>>
Bill: Hagere Hexe! #line:0eb94a
Dan: Schrecklich anzuschauen! #line:5ba171
Mae: Grausame junge Maenner, #line:551e29
Mae: Ihr habt nicht einmal einen eurer 300 feinen Biberfelle angeboten, #line:cc665e
Mae: um meine alten Gebeine zu waermen! #line:eb1c63
<<shuffleNextOptions>>
-> Ich werde euch verfluchen und dich, #line:572cf4
    Mae: und noch in dieser Nacht werdet ihr umkommen. #line:6515c9
    <<tell HarfestPlayWatchers Happy>>
    <<set $play_correct_responses += 1>>
    Dan: Ich bin furchtsam! #line:c3b7e2
    Bill: Ruhig, Bruder Steven #line:07511d
    Bill: Ich habe mich nicht in diese Huegel hinausgewagt #line:9c0e02
    Bill: um mit einer Hoellenhexe um Worte zu ringen! #line:e2612e
-> Du bist ein ungehobelter und frecher Junge, #line:f13fe0
    <<playOneShot event:/music/harfest_organ_mistake>>
    <<tell HarfestPlayWatchers Grumble>>
    Bill: ... #line:3ed334
    Mae: Deine Ungezogenheit muss bestraft werden! #line:347a56
    Dan: Ich... bin... ungemuetlich! #line:da93be
    Bea: Aehm. #line:21bb14
    <<lookForward Mae>>
    <<shuffleNextOptions>>
    -> Deine Strafe ist ein Fluch, #line:6dbae7
        <<tell HarfestPlayWatchers Happy>>
        <<set $play_correct_responses += 1>>
        Mae: der dich und dich nehmen wird #line:039830
        Mae: noch in dieser Nacht! #line:d2cbcc
        Bea: *whew* #line:da92db
    -> Deine Strafe ist eine Aufgabe, schrecklich! #line:4f760a
        <<tell HarfestPlayWatchers Grumble>>
        <<playOneShot event:/music/harfest_organ_mistake>>
        Bea: wha- #line:dd3320
        <<lookForward Mae>>
        Mae: Du sollst in deine Stadt zurueckkehren... #line:a1cfc2
        Bill: um #line:778262
        Mae: Grabe das aelteste Grab auf dem Kirchhof aus... #line:ee1206
        Dan: aeh #line:e555e0
        Mae: ...und kuesse sie! #line:bb5f89
        Bill: Was? #line:fc62ef
        Bea: Aber die Hexe hat doch nur herumgealbert #line:914566
        Bea: um die Reisenden zu erschrecken! #line:9ed818
        Bea: und stattdessen verfluchte sie sie, noch in dieser Nacht zu sterben! #line:25025d
<<lookForward Mae>>
Mae: YEE HEE HEE HEEEEEE! #line:0a525c
<<lookNearest Mae>>
Mae: Ich wurde unter diesen Baeumen geboren #line:2cf60d
Mae: 'neath a harvest moon #line:27e210
Mae: und ihr werdet unter diesem Mond sterben! #line:6d0696
Dan: Bruder John, ich bin erschrocken! #line:e95e69
Bill: Glaube, Bruder Steven! #line:d3482d
Bill: Bist du so leicht zu erschuettern durch das Geschwaetz eines alten Weibes? #line:51d3f4
Bill: Komm, hilf mir, diese 300 Biberfelle zu tragen. #line:bb347c
Bea: ...aber kaum jemand wusste, #line:9dacbe
Bea: dass die Hexe nicht geflucht hat... #line:0463b6
Bea: IN VAIN. #line:f91243
<<close>>
//spooky organ end of act theme//
<<playOneShot event:/music/harfest_organ_02>>
<<tell HarfestPlayWatchers Happy>>
<<playOneShot event:/harfest/play_applause>>
<<cameraLookAt HarfestCamHigh 11>>
<<wait 11.5>>
<<fadeOut 1 wait>>
[[PlayScene2]]
===
title: PlayScene2
tags: 
colorID: 3
position: 439,9034
---
<<warp Mae CounterOffRight>>
<<warp Gregg CounterOffLeft>>
<<warp Dan CounterOffLeft>>
<<warp Bill CounterOffLeft>>
<<fadeIn 1 wait>>
//OH NO! Organ theme//
<<cameraLookAt HarfestCamLow 6>>
<<playOneShot event:/music/harfest_organ_03>>
<<wait 4>>
<<walk Dan DanCounter wait>>
Dan: Hilfe! Hilfe! #line:b427dd
Dan: Wer wird einem armen Reisenden helfen?! #line:de41ba
<<close>>
<<walk Gregg GreggCounter wait>>
<<lookLeft Gregg>>
Gregg: Aeh... aehm #line:f40fba
Gregg: aeh, bei dem Mond und den Sternen ueber uns, Herr #line:f22ac6
Gregg: Ich beschwoere dich, was ist dein Problem? #line:39235d
<<close>>
<<walk Mae MaeCounter wait>>
Mae: Du machst da mit? #line:3d9c55
<<lookRight Gregg>>
Gregg: ja #line:35f35b
<<lookLeft Gregg>>
Dan: AAAGGGH!! WIIIITCH!!! #line:4e5be7
Mae: ja #line:3d4214
Dan: Ich schwoere dir, Freund, dass noch heute #line:962cf0
Dan: dieses entsetzliche Weib #line:9ee76b
Dan: meinen Bruder und mich zum Tode verflucht hat! #line:bc86cd
Gregg: 'Zwounds!  #line:ba066e
Dan: Wir lagerten genau an dieser Quelle unter dem toten Baum #line:318629
Dan: und selbst jetzt liegt sein Koerper auf der Erde, #line:b9d1af
Dan: leblos wie ein Haufen von 300 Biberfellen! #line:dd9bc7
Gregg: Oh! Es kuehlt das Herz!!! #line:de5458
<<lookRight Gregg>>
Mae: EE HEE HEE HEEEEEEEEE!!! #line:05c683
<<shuffleNextOptions>>
-> Ich habe diesen Fruehling verflucht! #line:9bcd6b
    Mae: dass jeder Mann, der daraus trinkt, umkommen soll! #line:d3d062
    <<lookLeft Gregg>>
    <<tell HarfestPlayWatchers Happy>>
    <<set $play_correct_responses += 1>>
    Dan: AAAGH! ICH BIN SLAIN! #line:24dfc9
    Dan: Ich schliesse mich dir jetzt an, Bruder Steven!!! #line:e24ebf
-> Ein Fluch ueber ungehobelte junge Maenner! #line:f4dcf1
    <<tell HarfestPlayWatchers Grumble>>
    <<playOneShot event:/music/harfest_organ_mistake>>
    Gregg: das ist nicht- #line:91bba5
    Dan: aehm #line:b56b21
    Bea: ... #line:16f123
    <<lookLeft Gregg>>
    Dan: Sie hat die Quelle verflucht, und jetzt soll ich sterben!!! #line:c773c0
//dan falls behind counter//
<<setAnimInt Dan die 1>>
<<wait 2>>
Mae: wow #line:f13554
<<lookRight Gregg>>
Gregg: Ich bin auch verloren, denn ich habe von dem Wasser getrunken! #line:4e72e6
Mae: Lass mich hier nicht allein Gregg. #line:1be6f2
Gregg: Byeeeeeeeeee. #line:94418d
//gregg falls behind counter//
<<setAnimInt Gregg die 1>>
<<wait 2>>
Bea: und doch, selbst dort #line:65eb71
Bea: die Gier des Fluches nach Blut... #line:ce6815
Bea: ...WUeRDE NICHT GESCHUeTZT WERDEN!!! #line:e18098
//spooky organ end of act theme//
<<close>>
<<playOneShot event:/music/harfest_organ_04>>
<<tell HarfestPlayWatchers Happy>>
<<playOneShot event:/harfest/play_applause>>
<<cameraLookAt HarfestCamHigh 7.5>>
<<wait 7.5>>
<<fadeOut 1 wait>>
<<warp Gregg CounterOffLeft>>
[[PlayScene3]]
===
title: PlayScene3
tags: 
colorID: 3
position: 689,9037
---
<<warp Mae CounterOffRight>>
<<warp Bea BeaCounter>>
<<warp Dan DanCounter>>
<<warp Bill BillCounter>>
<<show Bill>>
<<set $bill_hat to 1>>
<<setAnimInt Dan die 0>>
<<fadeIn 1 wait>>
//THINGS ARE SPOOKY ORGAN RIFF//
<<cameraLookAt HarfestCamLow 8.5>>
<<playOneShot event:/music/harfest_organ_05>>
<<wait 8.5>>
Bea: Starrt sie an! Starrt auf diese Geister! #line:290b42
Bill: Bruder, wir sind jetzt Geister! #line:c38e08
Bill: aah! ich werde von dem anklagenden Blick gequaelt #line:8372a7
Bill: von 300 geisterhaften Bibern! #line:fbe73f
Dan: Es scheint, dass alle, die hier sterben #line:ac3282
Dan: verflucht sind, niemals zu gehen! #line:7dc333
Bill: Weiss diese Hexe, was sie getan hat? #line:c66f93
<<close>>
<<walk Dan CounterOffLeft>>
<<walk Bill CounterOffLeft>>
<<walk Bea CounterOffLeft>>
<<wait 4>>
<<walk Mae MaeCounter2 wait>>
<<wait 3>>
<<warp Janitor CounterOffRight>>
<<shuffleNextOptions>>
Mae: aehh... aehm... #line:42e97f
Mae: ...scheisse... #line:619604
<<wait 3>>
Bea:{locator=LeftSide} "Foooooor, #line:8ed56d
-> Denn was ist ein Geist, #line:6b6af0
    Mae: als das, was den leeren Raum heimsucht #line:6ddba2
    Mae: der einst voll war? #line:3b806c
    Janitor: Und kann ein Ort, der einmal heimgesucht wurde, wieder frei werden? #line:2f8cb9
-> Denn jeder Mensch ist auf seinem Weg bestimmt #line:706f3f
    Mae: und jeder Weg fuehrt nur zu seinem Ende. #line:f5ac65
    Bea:{locator=LeftSide} ok...? #line:737f9c
    Janitor: Und doch kann jeder Weg an unbekannte Orte fuehren. #line:b56df9
<<close>>
<<walk Janitor JanitorCounter wait>>
Mae: Du! #line:01df52
Mae: vom Busbahnhof! #line:0f86c5
Janitor: Junge Hexe! #line:2541e0
Janitor: Ich bin es, der Gott des Waldes! #line:8f4813
Mae: aeh... haette nur nicht erwartet, dass du... #line:fbf2fa
Janitor: Ist das nicht die Art und Weise. #line:625414
Mae: So... #line:3cbfa5
Janitor: Hexe, du hast zu lange in dieser Welt verweilt #line:223239
Janitor: Ich verbanne dich, um in der Nacht zu wandern #line:762895
Janitor: durch die fremden Orte! #line:44be06
<<shuffleNextOptions>>
-> Oh Gott... woher wusstest du... #line:ffe900
-> Ich... was? #line:215b22
Janitor: Junge Hexe, lass mich Weisheit zu dir sprechen: #line:01eb62
Janitor: Wir beginnen und wir enden, #line:38fbf9
Janitor: bei Nacht, im Wald. #line:ee1eb7
Janitor: aber das ist nicht die ganze Geschichte. #line:d885d5
<<close>>
<<wait 2>>
Mae: ... #line:087ae7
Mae: ... #line:f643a7
<<close>>
<<lookRight Bea>>
<<warp Bea BeaBehindCounter>>
<<walk Bea BeaAboveCounter wait>>
Bea: *das ist deine Zeile* #line:5a291f
<<close>>
<<walk Bea BeaBehindCounter wait>>
<<shuffleNextOptions>>
<<lookAt Mae Janitor>>
-> Waldgott, du hast hier keine Macht! #line:e26d18
    <<tell HarfestPlayWatchers Happy>>
    <<set $play_correct_responses += 1>>
    Janitor: selbst jetzt beginnt dein Ende. #line:1e0a38
    Mae: AAAACK!!! Meine hoellischen Kraefte! #line:2e6d5d
    Janitor: Sssh, junge Hexe. #line:90e6e3
    Janitor: selbst jetzt #line:c349c5
    Janitor: die Welt, die du kennst, endet #line:537952
    Janitor: und wer kann sagen was liegt #line:2650b4
    Janitor: in der kommenden Welt? #line:44fbe1
-> Woher weisst du so viel ueber mich? #line:599400
    <<tell HarfestPlayWatchers Grumble>>
    <<playOneShot event:/music/harfest_organ_mistake>>
    Janitor: Junge Hexe, ich weiss mehr, als du je wissen koenntest #line:a91581
    Janitor: denn ich bin so alt wie diese Baeume. #line:8bf353
Mae: Wow. #line:c1a89a
Janitor: Nimm dich in Acht, wenn du gehst #line:58c681
Janitor: denn es gibt Geister. #line:a86755
<<lookNearest Mae>>
//bill and dan both say WOOOOOO!!!//
<<runNextLinesTogether 2>>
Bill:{locator=LeftSide} [wave]Wooooo!!![/wave] #line:ded394
Dan:{locator=LeftSide2} [wave]Wooooo!!![/wave] #line:640c1f
Janitor: Pass auf dich auf. #line:bee150
<<close>>
<<walk Janitor CounterOffLeft>>
<<wait 3>>
//walk janitor off//
//walk bill and dan off//
//spooky organ end of act theme//
<<cameraLookAt HarfestCamHigh 6>>
<<playOneShot event:/music/harfest_organ_06>>
<<tell HarfestPlayWatchers Happy>>
<<playOneShot event:/harfest/play_applause>>
<<wait 5>>
<<fadeOut 1 wait>>
[[PlayScene4]]
===
title: PlayScene4
tags: 
colorID: 3
position: 964,9003
---
<<warp Mae MaeCounter>>
<<warp Dan DanCounter>>
<<warp Gregg GreggCounter>>
<<warp Bill BillCounter>>
<<warp Janitor CounterOffRight>>
<<hide Janitor>>
<<warp Bea BeaCounter>>
<<setAnimInt Gregg die 0>>
<<setAnimInt Dan harfestIdle 2>>
<<cameraLookAt HarfestCamLow 0>>
<<lookLeft Bea>>
<<fadeIn 1 wait>>
Bea: Und so wurde dann dieses Fest gegruendet, #line:bc3eb8
Bea: an Halloween werden wir feiern #line:99c32e
Bea: das Sterben des Jahres #line:c79b0e
Bea: und die Gruendung von Possum Springs! #line:211e93
Dan: Ich, Buergermeister Geist, verkuende es! #line:bf2799
Bill: Die Biberpopulation ist zurueckgegangen #line:8a0049
Bill: und jetzt sind unsere groessten Exporte Mais und Kuerbisse! #line:079a9d
Gregg: Wir waren mal die Kreisstadt! #line:81b18c
<<shuffleNextOptions>>
->YEE HEE HEE HEEEEEEE!!! #line:43007f
    Mae: Eigentlich ist der Mythos der boesen Hexe #line:8cea0a
    Mae: ist das Produkt von jahrhundertelanger Verfolgung durch- #line:326ef7
->Nun geht, bevor ich euch alle verfluche!!!! #line:403ae1
    Bea: Oh nein, Publikum!!! #line:825088
Bea: Und das ist unsere Show! #line:9555cc
<<lookOut Mae>>
<<lookOut Gregg>>
<<dilate Bea .75 10>>
<<lookOut Bea>>
Bea: Sei vorsichtig, wenn du hier weggehst, #line:330585
Bea: denn wer weiss, was in der Dunkelheit lauern mag? #line:394912
Bea: und nimm dir auf dem Weg nach draussen ein Flugblatt mit #line:9b8345
Bea: von der Handelskammer von Possum Springs! #line:911284
Bea: Unterstuetzen Sie Ihre gruseligen lokalen Geschaefte #ssssssss!!!! #line:5bd9f9
<<close>>
<<tell HarfestPlayWatchers HappyForever>>
<<cameraLookAt HarfestCamHigh 11>>
//organ fanfare as camera ascends toceiling//
<<playOneShot event:/music/harfest_organ_07>>
<<playOneShot event:/harfest/play_applause>>
<<wait 11>>
<<set $did_harfest_play to 1>>
<<fadeOut 1 wait>>
<<set $bill_hat to 0>>
<<scene TowneCentreEast_Harfest>>
===
title: Bea_Act3
tags: 
colorID: 4
position: 1661,8712
---
<<if $day is 1>>
    <<if $bea is 0>>
        [[A3D1_00]]
    <<endif>>
<<elseif $day is 2>>
    <<if $bea is 0>>
        [[A3D2_00]]
    <<endif>>
<<elseif $day is 3>>
    <<if $bea is 0>>
        [[A3D3_00]]
    <<endif>>
<<elseif $day is 4>>
    <<if $bea is 0>>
        [[A3D4_00]]
    <<endif>>
<<endif>>
===
title: A3D1_00
tags: 
colorID: 4
position: 1227,9070
---
Mae: heyyyyyyy Bea. #line:e3d519
Bea: hey uh #line:e11968
Bea: danke nochmal fuer letzte Nacht. #line:945afd
Bea: ich schulde dir was. #line:cbfe4d
Mae: schon gut. es hat Spass gemacht. #line:a879a7
Mae: meine Nacht wurde danach wirklich seltsam und schlecht. #line:3c035f
Bea: wie das? #line:f7c3c4
Mae: erzaehl ich dir spaeter, schaetze ich. #line:791cb7
Mae: Ich mag es immer noch, es zusammenzusetzen. #line:ea3542
Bea: ok gut. lass mich wissen wie das laeuft. #line:83156c
Mae: werde ich. #line:a7227e
Bea: ich glaube, es ist Tag der Bandprobe? #line:78f3cc
Mae: ja ich werde Gregg jagen gehen. #line:2317ce
Bea: sollte nicht zu schwierig sein. #line:d4efda
Mae: ja nein. er ist im Snack Falcon. #line:2f3d67
Bea: ich weiss nicht, wie der Typ seinen Job behalten kann. #line:1c471d
Bea: er klaut staendig, macht zum Spass Vorraete kaputt, #line:6eb7b8
Bea: und verlaesst die Arbeit wann immer er will. #line:2ea899
Mae: Gregg spielt nach seinen eigenen Regeln. #line:390a8c
Bea: es fuehlt sich eher so an, als wuerde die Welt nach seinen Regeln spielen. #line:23457e
Mae: whoa. #line:cf93b6
Bea: ich weiss nicht einmal, was das bedeutet. #line:166f9a
Mae: immer noch. wow. #line:acbbd0
<<set $bea_done to 1>>
===
title: InvestigationQuest
tags: 
colorID: 4
position: 1542,9662
---
<<close>>
<<irisOut 1 wait>>
<<sectionTitle BeaIQ BeaIQGraveyard>>
===
title: A3D3_00
tags: 
colorID: 4
position: 1763,9091
---
<<if $bea_day_intro is 0>>
    <<set $bea_day_intro to 1>>
    Mae: hey bea. #line:4de664
    Bea: hey. #line:830ee3
    Mae: bist du in Ordnung? du scheinst niedergeschlagen zu sein. #line:c68f48
    Bea: oh, es ist nichts. #line:f3e1bd
    Bea: Zeug. #line:c2ab59
    Mae: willst du darueber reden? #line:13bfb4
    Bea: nein. #line:58edce
    Mae: ok. #line:885f88
<<endif>>

Bea: oof. was hast du heute vor? #line:bd433b
-> Wandert durch die Stadt. #line:1754d1
    Bea: viel Spass! #line:803705
-> Was auch immer ihr tun wollt! #line:82f99a
    [[FriendshipQuest]]
===
title: A3D4_00
tags: 
colorID: 4
position: 2155,9119
---
<<if $bea_day_intro is 0>>
    <<set $bea_day_intro to 1>>
    Bea: whoa! #line:ddc7e7
    Mae: whoa. #line:27e3bb
    Bea: Kumpel, bist du okay? #line:cdc4e1
    Mae: ja. ich denke schon. sicher. #line:1f2166
    Mae: muede, hatte seit Tagen Kopfschmerzen. #line:e51b33
    Bea: du solltest nach Hause gehen und dich ausruhen. #line:c763d5
    Bea: ich habe das Gefuehl, du koenntest ganz gut dahinvegetieren. #line:10e391
    Mae: Nein. Mir geht's gut. #line:1d655b
    Mae: Sachen machen hilft. #line:fe9c20
    Bea: wie geht es dir mit der ganzen "Geister"-Sache? #line:9bde00
        ->"Geist"? #line:c74715
            Bea: Entschuldigung, Geist. #line:5d7f1c
            Mae: danke. #line:531320
            Mae: und ich fuehle mich schlecht #line:790eaf
        -> schlecht #line:69d23b
    Mae: ich meine, seit es passiert ist, fuehle ich mich morgens nicht ausgeruht. #line:c44dcc
    Mae: und ich fuehle mich als ob jemand anderes in meinem Kopf ist. #line:0aa078
    Mae: und mein Schaedel droht zu explodieren. #line:6278b9
    Bea: also ja dann nicht gut. #line:6dcd05
    Mae: Noe. #line:8c4642
    Bea: hrm. #line:cd657f
<<endif>>

<<if $did_bea_investigation_quest is 0>>
    Bea: Wolltest du dir den Friedhof trotzdem ansehen? #line:345d97
    -> Nee...heute nicht. #line:467499
        Bea: Okay, sag mir Bescheid! #line:e1ea5e
    -> Eigentlich ja. Lass es uns tun. #line:48ef6a
        [[InvestigationQuest]]
<<else>>
    <<set $bea_done to 1>>
<<endif>>
===
title: A3D2_00
tags: 
colorID: 4
position: 1470,9089
---
<<if $bea_day_intro is 0>>
    <<set $bea_day_intro to 1>>
    Mae: Hey Bea! #line:4821a2
    Bea: Hey Mae. #line:35186b
    Bea: Ich glaube ich werde diesen Ort niederbrennen. #line:655d02
    Mae: oh cool! #line:914773
    Mae: aber vielleicht hat das auch Konsequenzen? #line:fab5e4
    Bea: wir koennten das Geld der Versicherung nehmen. #line:e9df43
    Bea: und wie #line:619efa
    Bea: das nicht tun. #line:a01e34
    Mae: Was ist, wenn du erwischt wirst? #line:54b8d5
    Mae: ich habe eine Sendung gesehen, wo sie jemanden dabei erwischt haben. #line:51757f
    Bea: ... #line:f46362
    Bea: Du wirst diesen Ort fuer mich niederbrennen. #line:a0f2d7
    Mae: oh cool! #line:0f8733
    Mae: aber vielleicht wuerde ich auch in den Knast kommen? #line:53ef58
    Bea: nein, du hast kein Motiv! #line:5be1a9
    Bea: niemand wuerde Verdacht schoepfen! #line:beb025
    Mae: also laeuft die Arbeit nicht gut? #line:f90a3f
    Bea: aaaaaaaaaagh. #line:ae1faf
    <<wait 2>>
    Mae: ok ich werde es tatsaechlich tun. #line:b20864
    Bea: bitte tu es nicht wirklich. #line:bbc7ce
    Mae: ok gut. #line:5cbd0d
<<endif>>

Bea: Wolltest du dir den Friedhof ansehen? #line:9662a0
    -> Nee... heute nicht. #line:6f9838
        Bea: Okay, sag mir Bescheid! #line:94a001
    -> Eigentlich ja. Lass es uns tun. #line:9ab105
        [[InvestigationQuest]]
===
title: FriendshipQuest
tags: 
colorID: 4
position: 1854,9650
---
<<if $dom_friend_bea is 0>>
    Bea: Ist schon gut. Ich bin beschaeftigt. #line:7dbf0f
    Bea: und Gregg und Angus haben heute Abend mein Auto #line:5aca82
    Bea: also sitze ich sowieso zu Hause fest. #line:a987cc
    Mae: oh. ok. #line:f6b357
    Bea: vielleicht sehen, was sie vorhaben? #line:b2d186
    Bea: sie gehen irgendwo hin oder so. #line:a96a50
    <<wait 2>>
    Bea: ich bin nicht. #line:e16f21
<<else>>
    <<close>>
    <<set $bea_friendship_quest to 4>>
    <<irisOut 1 wait>>
    <<sectionTitle BeaFQ4Intro BeaCar>>
<<endif>>
===
