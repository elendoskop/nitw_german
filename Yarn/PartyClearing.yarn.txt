title: Bea
tags: 
colorID: 0
position: -8156,1534
---
<<if $beer_phase is 0>>
	<<if $bea is 0>>
		Bea: Was? #line:3a9965
		Mae: Nichts. #line:a402a5
		<<set $bea += 1>>
	<<elseif $bea is 1>>
		Mae: Coole Party! #line:a32de1
		Bea: Jepp. #line:346938
		<<set $bea += 1>>
	<<elseif $bea is 2>>
		Mae: Mit wem sprichst du? #line:56b0a4
		Bea: Nicht mit dir. #line:00e569
		<<set $bea_done to 1>>
	<<endif>>
<<elseif $beer_phase is 1>>
	<<if $bea is 0>>
        Mae: =_= #line:144859
		Mae: Yooooo. #line:f5e99f
		Bea: Was? #line:776ec6
		Mae: Du bist irgendwie muerrisch, wusstest du das? #line:f57914
		Bea: Nein. Wusste ich nicht. #line:11b87e
		Jackie: Unhoeflich. #line:147744
        Mae: o_o :| _o_ #line:23d6be
		<<set $bea += 1>>
	<<elseif $bea is 1>>
        Mae: =_= #line:7b1bdf
		Mae: Wie lange hast du vor zu bleiben? #line:91c72b
		Bea: Mae, wir sind erst vor 10 Minuten hier angekommen. #line:42f444
		Mae: Oh. #line:b94b09
		Bea: Bist du... wie viele Biere hast du getrunken? #line:7d7256
		Mae: ZWEI! #line:fc4658
		Bea: Lass es langsam angehen, Mae. Du bist winzig. #line:f74988
        Mae: o_o :| _o_ #line:323674
		<<set $bea_done to 1>>
	<<endif>>
<<elseif $beer_phase is 2>>
	<<if $bea is 0>>
        Mae: =_= :) #line:39e9fb
		Mae: HEY BEA #line:33a46c
		Bea: Ja? #line:de76da
		Bea: Oh Gott. #line:29b05a
		Bea: Du bist betrunken. #line:bd61a1
		Bea: Jetzt schon. #line:656ff2
		Mae: Deshalb sind wir doch hierher gekommen, oder? #line:bd2ebf
		Bea: Eigentlich nicht. Ich muss noch fahren. #line:97dbd0
		Bea: Und selbst wenn ich es nicht taete... #line:00d815
		Mae: Also ich habe meinen Fuehrerschein nicht!!! \o/ #line:182f92
		Mae: So wooohooo fuer micheeee!!!! #line:4fb043
        Mae: o_o :| _o_ #line:17daa5
		<<set $bea += 1>>
	<<elseif $bea is 1>>
		Mae: HEY BEA #line:4c016c
		Bea: Was? #line:daf1b1
		Jackie: Wer ist das? #line:6c779e
		Bea: Erinnerst du dich an Mae Borowski? #line:a10767
		Jackie: Oh Gott. #line:2b7435
		<<set $bea += 1>>
	<<elseif $bea is 2>>
		Bea: Mae, du bist betrunken. #line:c58a41
		Bea: Du musst dich am Feuer ausruhen. #line:f8fbc9
        Mae: :) #line:cb8d92
		Mae: Ich werde nackt in den Wooooods herumlaufen!!!!! \o/ #line:b6caea
		Bea: GO. SITZ. JETZT. #line:bcfc63
        Mae: o_o :| _o_ #line:938ed9
        <<set $should_sit_log to 1>>
		<<set $bea_done to 1>>
	<<endif>>
<<elseif $beer_phase is 3>>
<<endif>>


===
title: EnterLevel
tags: 
colorID: 0
position: -8465,1136
---
<<set $night to 1>>
[[SceneStart]]
===
title: Beer
tags: 
colorID: 0
position: -8811,1242
---
<<if $can_drink_beer is 1>>
	<<set $beer_phase += 1>>
	<<fadeOut 1 wait>>
	<<wait 1>>
    <<playAmbientSFX AmbientSFX:beer_drink>>
    <<wait 3>>
	<<fadeIn 1 wait>>
	[[SceneStart]]
<<else>>
	Mae: Ich weiss nicht, ob ich jetzt noch einen Drink haben sollte. #line:097e53
<<endif>>

===
title: SceneStart
tags: 
colorID: 0
position: -8573,1604
---
<<set $bea to 0>>
<<set $gregg_angus to 0>>
<<set $cole to 0>>

<<set $angus_done to 0>>
<<set $bea_done to 0>>
<<set $gregg_done to 0>>
<<set $cole_done to 0>>

<<set $can_drink_beer to 0>>

<<if $beer_phase is 0>>
    Mae: =_= :\ #line:6233f0
	Mae: Ugh. #line:6ce028
	Mae: Das war furchtbar. #line:ec4077
	Mae: Ich hasse Bier. #line:77b8fa
	Mae: Ich kann mich nicht erinnern, wann ich das letzte Mal Bier getrunken habe. #line:6788a4
	Mae: Es ist ueberhaupt kein leckeres Getraenk. #line:2f5ea0
    Mae: o_o :| _o_ #line:bcee4c
<<elseif $beer_phase is 1>>
    Mae: =_= #line:53e5b9
	Mae: *seufz* #line:e841f6
	Mae: Ich schaetze, Bier ist nur eines dieser schrecklichen Dinge, die Erwachsene tun. #line:c20c59
	Mae: Wie Steuern machen und Babys bekommen. #line:00af69
    Mae: o_o :| _o_ #line:705e38
<<elseif $beer_phase is 2>>
    Mae: O_O #line:c4b706
	Mae: Scheiss drauf! Ich trinke zwei Bier! #line:0ab8de
    Mae: :) #line:994f03
	Mae: Ich trinke Bier! #line:9b7577
	<<if $ate_tacos>>
		Mae: Ich schmecke immer noch Tacos! #line:571999
	<<endif>>
	Mae: OK?! #line:5e0dce
	Bea: Was? #line:7696b0
	Mae: NOTHING! #line:dd4982
    Mae: o_o :| _o_ #line:c2083a
<<endif>>

===
title: End
tags: 
colorID: 0
position: -9034,1731
---
<<fadeOut .5 wait>>
<<scene BeaCar>>
===
title: GreggAngus_BP0
tags: 
colorID: 0
position: -7769,1470
---
<<if $gregg_angus is 0>>
	Mae: Hey Leute. #line:fc9c9f
	Gregg: Yo, hast du gesehen, dass Cole hier ist? #line:d7d552
    <<lookOut Mae>>
    <<dilate Mae .75 .5>>
	Mae: OH GOTT. #line:a02a2e
    <<wait 1>>
	Angus: Bist du ok? #line:254f8d
    <<lookNearest Mae>>
    <<dilate Mae 1 .5>>
	Mae: Ja. Vollkommen in Ordnung. #line:a1ecdd
	Gregg: Wirst du mit ihm reden? #line:8dd6d3
	[[NEIN.|GreggAngus_No]] #line:b7e814
	[[Ja sicher.|GreggAngus_YeahSure]] #line:034a8e
<<elseif $gregg_angus is 1>>
    Mae: =_= #line:2bbbf6
	Mae: Urg. #line:7b6b89
    Mae: \o/ #line:7ced98
	Mae: Er steht einfach nur da! Als ob es keine grosse Sache waere! #line:a01592
	Gregg: Dieser Trottel. #line:d4f887
	Angus: Totally. #line:c77c6a
    Mae: _o_ o_o :| #line:508179
	Mae: Weisst du was? Ich werde nicht daneben stehen #line:792f7b
	Mae: und ihn danebenstehen lassen #line:4575c5
	Mae: waehrend mein Abend ruiniert wird! #line:30532b
	Gregg: Ja! #line:d17974
	Mae: Ich gehe da rueber. Ich werde mit Cole reden. #line:5b4146
	Angus: Ruiniere deinen eigenen Abend! #line:fbca8a
	Gregg: Wenn jemand deinen Abend ruinieren kann, Mae, dann solltest du das sein. #line:2f98ae
    Mae: :) #line:89f513
	Mae: Danke Leute. #line:da4247
    Mae: :| #line:fc1965
	<<set $talking_to_cole to 1>>
    <<set $cole_done to 0>>
	<<set $gregg_angus += 1>>
<<elseif $gregg_angus is 2>>
	Mae: Koennt ihr glauben, dass dieser Kerl Gitarre spielt? #line:dc5060
	Gregg: Jep. Er spielt die Deppen-Hits. #line:20bb57
	Angus: Ich mag dieses Lied! #line:2c3b3d
	Angus: Ist das ein Trottel-Lied? #line:5d8652
	<<set $douchebag_song to 1>>
	Mae: Nee, keine Sorge, Grosser. #line:7473f8
	Angus: ... #line:58bd1c
	<<set $gregg_angus += 1>>
<<elseif $gregg_angus is 3>>
	Gregg: So einen Trottel gibt es auf jeder Party. #line:4665b3
	Mae: Du hast keine Ahnung, Gregg. #line:b64557
	Mae: Damals in der Schule hattest du immer einen Verbindungsstudenten #line:477618
	Mae: auf dem Rasen rumhaengen #line:659750
	Mae: versucht, ganz sensibel auszusehen. #line:950ad6
	<<set $try_to_look_sensitive to 1>>
	Angus: Warum ist das ein Idiotensong? #line:4c1648
	<<set $angus_done to 1>>
	<<set $gregg_done to 1>>
<<endif>>

===
title: GreggAngus_YeahSure
tags: 
colorID: 0
position: -7618,1797
---
<<set $talking_to_cole to 1>>
<<set $gregg_angus += 1>>
<<set $cole_done to 0>>
Gregg: Ja Mann! #line:52696d
Angus: Bist du sicher, Mae? #line:c59bc6
Mae: Ja! Ich meine, warum sollte ich deswegen komisch sein? #line:a187cc
Gregg: Nun, ich meine, du hast das Lied danach geschrieben... #line:050637
[[GreggAngus_Song]]

===
title: GreggAngus_No
tags: 
colorID: 0
position: -7885,1806
---
Angus: Wahrscheinlich eine gute Idee. #line:509b2a
Gregg: Ich meine, du hast den Song doch geschrieben... #line:bc8600
[[GreggAngus_Song]]
===
title: GreggAngus_Song
tags: 
colorID: 0
position: -7778,2157
---
Gregg: Wie hiess er denn? #line:46a0cf
Angus: ''Go Get Dead, Angel Face''? #line:83c1d1
Mae: Uuuuuuuuuuuuugh. #line:d048a5
Mae: Wir reden nicht ueber Go Get Dead, Angel Face. #line:efbe7f
<<set $gregg_angus += 1>>
===
title: Cole
tags: 
colorID: 0
position: -6862,1423
---
<<if $beer_phase is 0>>
	Mae: Oh Gott. Es ist... #line:433c74
	<<if $talking_to_cole is 1>>
		Mae: Vielleicht brauche ich noch einen Drink. #line:48cd8a
		<<set $can_drink_beer to 1>>
	<<else>>
        <<set $cole_done to 1>>
    <<endif>>
<<elseif $beer_phase is 1>>
	<<if $cole is 0>>
		<<if $whats_your_name is 1>>
			Mae: Oh hey, ich bin Mae! Wie ist dein Name? #line:2245fb
			Cole: Bist du das, Mae? #line:11a3d4
			Mae: Oh, ich glaube, ich kenne dich von irgendwoher... #line:2dbaa2
			Cole: Aeh ja. Von der Highschool. #line:a51d3b
			Mae: Wahrscheinlich als wir ein Liebespaar waren #line:82083b
			Cole: Ha ha. Ja, ich denke schon. #line:c63db0
			Mae: und wir haben diese Strassengang verpruegelt #line:cd03f1
			Cole: Oh ja! #line:4de03e
			Mae: mit Ketten und Muelleimern. #line:c5e7e6
			Cole: Gute Zeiten, Mae. #line:293628
			Cole: Das Spiel habe ich uebrigens immer noch. #line:c59c7d
			Cole: Meine Freundin damals in der Schule - #line:6d1b7c
            Mae: \o/ :( #line:8bacd6
			Mae: Was zum Teufel ist dein Problem? #line:a5c43c
			Cole: Was? #line:b747fd
            Mae: o_o :| _o_ #line:22ae6d
		<<else>>
			Mae: Heyyyyy. #line:3687e4
			Cole: Mae? Bist du das? #line:e9d03b
			Cole: Wow. Wie geht es dir? #line:8d826e
			<<if $dropout_week is 1>>
				Mae: Nun, Anfang dieser Woche habe ich die Schule abgebrochen #line:61e519
				Mae: und jetzt trinke ich in den Waeldern. #line:bb76a3
				Cole: Wow ok! #line:2d1933
				Mae: Was zum Teufel ist dein Problem? #line:f30c57
				Cole: Was? #line:6ab765
			<<elseif $hows_it_going is 1>>
				Mae: Hi, ich bin Mae! Wie geht's denn so? #line:2ff1a8
				Cole: Das habe ich dich gerade gefragt? #line:563b0e
				Cole: Ich meine, mir geht's gut. #line:ab2f5f
				Cole: Bin nur uebers Wochenende zu Besuch. #line:7312ad
				Mae: Tatsaechlich? Das ist ja faszinierend! #line:435443
				Cole: Ich denke schon. #line:568076
				Cole: Ich glaube, das ist das erste Mal seit einer Weile #line:2bd0bf
				Cole: jemand mein Leben faszinierend genannt hat. #line:ec91e0
				Mae: Das ist so wahr! Also dann, bis spaeter!  #line:dd7ee3
				Cole: Ok... #line:e36db3
				Mae: Was zum Teufel ist dein Problem? #line:3b4056
				Cole: Was? #line:62d023
			<<endif>>
		<<endif>>
        <<set $cole += 1>>
		<<set $has_talked_to_cole_1 to 1>>
	<<elseif $cole is 1>>
		Mae: Bleib cool, Mae. #line:2b0279
		Mae: Hol dir was zu trinken und sei cool. #line:a14877
		<<set $can_drink_beer to 1>>
	<<endif>>
<<elseif $beer_phase is 2>>
    Mae: Igitt, ich weiss nicht, ob ich das schaffe. #line:343d1c
<<endif>>
===
title: GreggAngus
tags: 
colorID: 0
position: -7517,1023
---
<<if $beer_phase is 0>>
	[[GreggAngus_BP0]]
<<elseif $beer_phase is 1>>
	[[GreggAngus_BP1]]
<<elseif $beer_phase is 2>>
	[[GreggAngus_BP2]]
<<endif>>
===
title: GreggAngus_BP1
tags: 
colorID: 0
position: -7417,1439
---
<<if $gregg_angus is 0>>
	<<if $has_talked_to_cole_1 is 1>>
		Mae: AAAAAAAGH #line:813868
		Gregg: Wie ist es gelaufen? #line:98935e
		Mae: AAAAAAAAGH #line:472a97
		<<set $can_drink_beer to 1>>
	<<endif>>

	<<if $people_dont_like is 1>>
        Mae: =_= :\ #line:862e61
		Mae: Ugh #line:293345
		Angus: Bist du ok? #line:ffff48
		Mae: Manchmal denke ich, dass die Leute mich einfach nicht moegen. #line:346c50
		Gregg: Ah, Alter. #line:795961
		Gregg: Das ist scheisse. #line:aad6ee
		Angus: Ja, Mae, du solltest dich nicht so runtermachen lassen. #line:053e02
		Gregg: Ja, wen kuemmert es, wenn die Leute dich nicht moegen? #line:23f261
		Angus: Ok. Das ist nicht das, was ich- #line:4d39ec
		Gregg: SCREW EVERYONE MAE'S THE BEST!!! D: \o/ #line:e6786a
		<<set $maes_the_best to 1>>
		<<lookAt Gregg DoucheBag>>
		<<lookAt Mae DoucheBag>>
		DoucheBag: Ja Kumpel! #line:623c11
		Gregg: Haltet die Klappe DOUCHEBAG! _o_ :| #line:e1575a
		DoucheBag: ...was? #line:8d56e7
		<<lookAt Gregg Mae>>
		<<lookAt Mae Gregg>>
        Mae: :) #line:f52cc8
		Mae: Danke Leute. #line:1e977d
        Mae: o_o :| #line:c3033d
		<<lookNearest Gregg>>
		<<lookNearest Mae>>
	<<elseif $you_dont_like is 1>>
        Mae: =_= #line:752b93
		Mae: Ugh. #line:2bcd2d
		Angus: Problem? #line:1b0e50
		Mae: Manchmal denke ich, dass ich die Menschen einfach nicht mag. #line:59f143
		Gregg: Ja, Mann, scheiss auf die Welt. #line:0a8bfa
		Angus: Ja, scheiss drauf. #line:6f6770
		Mae: Gott, irgendjemand sollte wirklich die Welt einfach schon abschaffen. #line:fd6568
		Gregg: Die Welt ist schon viel zu lange uneffed. #line:d90a9d
		Angus: Verdammt richtig. #line:8f9c99
        Mae: o_o :| #line:a83f23
		<<set $eff_the_world to 1>>
	<<endif>>

	<<set $gregg_angus += 1>>

<<elseif $gregg_angus is 1>>
    Mae: O_o #line:6a83e5
	Mae: Habt ihr was gehoert? #line:5477d9
	Angus: Und was? #line:f7abf4
	Mae: Irgendwas im Wald da drueben... #line:953f6d
	Gregg: Oh ja, zwei Leute knutschen auf dem Felsen hinter uns. #line:b5931f
	Mae: Das kann nicht sehr bequem sein. #line:0c4a05
	Gregg: Wer braucht schon Komfort, wenn man rummachen kann? #line:c951bb
	Angus: Aber vorzugsweise hat man beides. #line:744c0b
	Gregg: Es ist keine perfekte Welt, Angus. #line:a6ead1
    Mae: o_o #line:c9d912
	<<set $rock_makeouts to 1>>
	<<set $gregg_angus += 1>>
<<elseif $gregg_angus is 2>>
	Mae: Die gehen da oben wirklich ran. #line:6ab81c
	Gregg: Das tun sie ganz sicher. #line:fb8558
	Mae: Wollt ihr Jungs... ich weiss nicht... woanders stehen? #line:dce1ad
	Gregg: Nee, Alter! #line:005871
	Angus: Warum sollten wir umziehen? #line:6dd4aa
	Gregg: Sie sind die Unanstaendigen. #line:1f61e2
	Angus: Wir sind Gentlemen. #line:094689
	<<set $gregg_angus += 1>>
<<elseif $gregg_angus is 3>>
    Mae: =_= #line:455855
	Mae: Ich fange an, diese beiden Biere zu spueren. #line:8a55cf
	Angus: Ja, ich trinke nicht wirklich viel. #line:fc2788
	Mae: Ich hatte vorher nur ein Bier. #line:5616cb
	Gregg: Ha ha ha ja und du hast es ausgespuckt! #line:46138d
	Angus: Mae, wenn du nicht trinkst, solltest du wirklich vorsichtig sein. #line:4438a6
	Mae: Nee Mann, ich bin cool. #line:67ff3b
	Angus: Ok, wir werden ein Auge auf dich haben. #line:2c1ce4
	Gregg: Wir werden dich wie Falken beobachten. #line:086fee
	Angus: Anstandswauwau. #line:662a17
    Mae: :) #line:3ea21a
	Mae: Danke, Leute. #line:3c9e9c
    Mae: o_o :| #line:16c6f1
	<<set $chaperone_hawks to 1>>
	<<set $gregg_angus += 1>>
<<elseif $gregg_angus is 4>>
	Gregg: Ja, sie knutschen immer noch. #line:2bcddd
	<<set $gregg_done to 1>>
	<<set $angus_done to 1>>
<<endif>>

===
title: GreggAngus_BP2
tags: 
colorID: 0
position: -7184,1435
---
<<if $gregg_angus is 0>>
	<<if $porn_log is 1 and $rock_makeouts is 1>>
        Mae: =_= :) #line:26f4ab
		Mae: HEY GREGGGG! #line:a4cfb6
		Gregg: Wow, Kumpel. #line:d11a3d
		Angus: Bist du okay, Mae? #line:405308
		Mae: glaubst du, dass die Leute, die sich auf dem Felsen #line:b0c628
		Mae: auf diesem Felsen #line:2c466d
		Mae: trafen sich dort, weil #line:b7fa7f
		Mae: ES EINEN PORNOLOGG GIBT??? #line:30e54b
		Gregg: OH MEIN GOTT ICH WETTE, DASS ES DAS GIBT. #line:55f20b
		Angus: Ein was? #line:18059f
		<<lookAt Gregg Angus>>
		Gregg: Siehst du, es ist ein Baumstamm #line:190adb
		<<lookAt Gregg Mae>>
		Mae: wo die Leute von frueher #line:4e41f7
		Mae: WUeRDE POOOORN setzen #line:54be6a
		<<lookAt Gregg Angus>>
		Angus: Von welchem Alter reden wir? #line:584b21
		Angus: Wie die Zeit des Revolutionskrieges? #line:79a63b
		Angus: Ich glaube, hier hinten gibt es irgendwo einen alten Friedhof von damals... #line:4eeb7f
		<<lookAt Gregg Mae>>
		Mae: Diese Waelder sind voll von alten Geistern und Pornobloecken und Leuten, die rummachen!!! #line:1ab5df
	    Mae: o_o :| #line:ec1c99
		<<lookNearest Gregg>>
    <<else>>
        Mae: =_= :) #line:eb2a18
		Mae: HEY GREGGGG! #line:6620e6
		Gregg: Whoa, Kumpel. Bist du ok? #line:b80d12
		Mae: Hey Gregg wusstest du das ich denke #line:4eb4ce
		Mae: du bist ein Angyyyy #line:937511
		Mae: ... #line:522857
		Angus: "Angy?" #line:247058
		Mae: SIND SO CUUUUTE!!! #line:103f33
		Gregg: Das wusste ich doch, Alter. #line:8facb8
		Mae: \o/ #line:692289
        Mae: gut GUT #line:ad2330
        Mae: o_o :| _o_ #line:a61fed
	<<endif>>
	<<set $gregg_angus += 1>>
<<elseif $gregg_angus is 1>>
	<<if $dad_knows_angus is 1>>
        Mae: =_= :) #line:8aafa0
		Mae: Hey Angyyyy... mein Vater kennt deinen Vater! #line:37ad6f
		Mae: Sie haben in den 80ern zusammen gearbeitet. #line:9023e0
		Angus: Ok. #line:9e0b35
		Mae: Er hat immer mit deinem Dadddy in der Bar rumgehangen. #line:43c371
		Gregg: Kumpel. #line:56b5a1
		Gregg: Sag nie wieder "Daddy"! #line:0bc76f
		<<if $stop_saying_daddy is 1>>
			Gregg: Du sagst "Vati"! #line:c00569
			Gregg: Der Trottel spielt ein Lied ueber den "Dad" von jemandem! #line:bd0e82
			Gregg: Ich kann damit nicht umgehen! #line:f31b75
            Gregg: Meine Haut krabbelt von meinem Skelett! #line:514c0a
		<<endif>>
		Mae: pssst! #line:14730a
		Angus: What? #line:3463c3
		Mae: Mein Vater musste aufhoeren zu trinken, weil #line:e421e9
		Mae: er eine Gefahr fuer mich und meine Mutter war. #line:3d0bef
		Mae: HA HA HA HA HA HA HA HA HA HA!!! #line:f7d196
		Gregg: Kumpel. #line:ef7a3b
		Angus: Ok Mae du darfst nicht mehr trinken. #line:0fb41a
		Angus: Nie mehr. #line:b276a8
        Mae: o_o :| _o_ #line:b9846f
		<<set $gregg_angus += 1>>
	<<else>>
		<<set $gregg_angus += 1>>
		[[GreggAngus_BP2]]
	<<endif>>
<<elseif $gregg_angus is 2>>
	Angus: Ok Mae ich mache mir ein wenig Sorgen um dich. #line:4b7438
	<<if $chaperone_hawks is 1>>
		Gregg: Wir haben als Anstandswauwau versagt. #line:528581
		Angus: Eines Tages, Bug. #line:517484
        Mae: :) #line:b0a60a
		Mae: Awwwww. Ich habe vergessen, dass du ihn Buuuuug genannt hast. #line:73c0ca
		<<set $knows_gregg_is_bug to 1>>
		Gregg: ... #line:d83fdc
        Angus: ... #line:115aa5
	<<endif>>
    Mae: =_= #line:7ef1e6
	Mae: Nun, ich habe eine tolle Zeit hier in den Waeldern, ok. #line:1be2b0
	Angus: Gregg, denkst du nicht, Mae sollte sich vielleicht hinsetzen? #line:a086f5
	Gregg: Ja, Kumpel. Du solltest dich setzen. #line:9c99b4
    Mae: o_o :| _o_ #line:4ed7f6
    <<set $should_sit_log to 1>>
	<<set $gregg_angus += 1>>
<<elseif $gregg_angus is 3>>
	Gregg: Kumpel, setz dich ans Feuer und chille, ok? #line:83c21b
    Mae: o_o :| _o_ #line:afd21d
    <<set $should_sit_log to 1>>
	<<set $gregg_done to 1>>
	<<set $angus_done to 1>>
<<endif>>

===
title: Gregg
tags: 
colorID: 0
position: -7854,873
---
[[GreggAngus]]
===
title: ColeByFireEnding
tags: 
colorID: 0
position: -6553,1431
---
<<cameraLookAt CameraEnd 3>>
<<wait 6>>
<<flip Cole -1>>
<<wait 1>>
<<walk Cole ColeByFire wait>>
Cole: Hey Mae. #line:8d55fe
Mae: =_= #line:e15d6c
Mae: Hey Cooole-io. #line:be1e17
Cole: Wow. Das habe ich schon ewig nicht mehr gehoert.  #line:b6b335
Mae: Es ist schon DREI JAHRE her! #line:1487b0
Mae: Das ist gar nicht so lange! #line:22f099
Cole: ...nun, es war schoen, dich zu sehen. #line:432410
Cole: Ich bin nur fuer ein paar Tage von der Schule zurueck, und- #line:5ab076
Mae: HALT ENDLICH DIE KLAPPE. #line:2f0e17
Cole: Was? #line:f80ea3
Mae: o_o #line:14d4dc
<<if $no_smooches is 1>>
	Mae: Wir haben uns nicht einmal gekuesst! #line:a39a08
	Cole: Haben wir nicht? #line:1a510a
	Mae: NEIN! NICHT ERFOLGREICH!!!  #line:af5327
	Cole: Es... tut mir leid? #line:151c68
	Mae: Das ist mir sogar egal! #line:2c9ec2
<<endif>>
<<if $whatever_god is 1>>
	Mae: Ich habe mir solche Sorgen gemacht, dass du hier sein wuerdest. #line:20c4f7
	Cole: Oh. #line:7ff89e
	Mae: Ich habe zu GOTT gebetet, was auch immer!!! #line:b6abe0
	Cole: was? #line:f39ec1
	Mae: ICH HATTE GEWONNEN, dass jemand zuhoeren wuerde! #line:5aabb4
	Mae: ABER NEIN! #line:382a1f
	Mae: WEIL DU HIER BIST! #line:e638ea
<<endif>>
<<if $not_cute_ones is 1>>
	Mae: Das Maedchen in der Videothek ist ganz wie #line:df7dd5
	Mae: Woowoo! SUeSSE JUNGS!!! #line:2a4a6a
	Mae: Und du bist ein suesser Junge, Cole #line:6b8394
    Cole: Oh! Danke Mae, ich- #line:455afc
	Mae: ABER ICH HASSE NUeSSE MENSCHEN!!! #line:8cd589
	Mae: NIEDLICH IST DAS SCHLIMMSTE!!! #line:a422b8
<<endif>>
Cole: Ok, also ich werde gehen... #line:1c878c
<<close>>
<<playerJump>>
<<lookAt Mae Cole>>
<<wait 1>>
<<lookOut Mae>>
Mae: o_o #line:5681c0
<<dilate Mae .75 .25>>
Mae: Und ich kann nichts dafuer, dass ich ein totales Dreckschwein bin! #line:027414
<<flip Bea -1>>
<<clearBGChatPartner Bea>>
<<walk Bea BeaByFire>>
<<walk Gregg GreggByFire>>
<<walk Angus AngusByFire>>
<<set $nightmare_eyes to 1>>
<<fadeOutAmbientSFX AmbientSFX:DoucheBagGuitar 1>>
<<fadeOutAmbientSFX AmbientSFX:Walla 1>>
<<wait 1>>
<<setAnimInt DoucheBag state 0>>
<<if $nightmare_eyes>>
    Mae: ICH HABE NACHTRAUM-AUGEN!!!!! O_O #line:90b0ef
    Mae: Und das Einzige, was du siehst, sind meine Augen vor dem Fenster o_o #line:8491a5
    Mae: Und dann steche ich mit meinem Messer auf dich ein!!! :) \o/ #line:56cbea
    Mae: STAB STAB STAAAAABBB!!!! :/o\ #line:5bae82
    Mae: Bumm! Tot! _o_ #line:39b37e
    Mae: Wenn die Polizei eintrifft, seid ihr alle #line:38c706
    Mae: {width=4}[color=aaa][shake=.01][wave]N I G H T M A R E E Y E S.[/all] #line:fb2c69
    Gregg: NEUER NICKNAME! #line:8f860e
<<elseif $dog_bite_dead>>
    Mae: Du haettest den Hund sehen sollen, der mir ins Ohr gebissen hat. \o/ #line:76605e
    Cole: Was? #line:f0c3c2
    Mae: Ja, er hat sich festgebissen und wollte nicht mehr loslassen! #line:23776a
    Mae: Und er war wie RAWARWARRAWARARARARR #line:5965e0
    Mae: Und ich dachte ich wuerde sterben!!! #line:9bd12e
    Mae: Aber dann habe ich mir einen STICK geschnappt! #line:d026c1
    Mae: Und ich habe ihm damit den Arsch versohlt! #line:bdeed1
    Mae: Und ich werde das Gleiche mit jedem tun!!!! #line:62077d
    Mae: _o_ #line:69d57d
<<endif>>
Mae: ALSO VERARSCH MICH NICHT, JUNGE! #line:ef43ee
<<if $eff_the_world>>
	Gregg: EFF THE WORLD! #line:7fc1fe
<<endif>>
Bea: Ok Mae, du bedrohst jetzt Leute. #line:1ca115
<<if $body_posi>>
	Mae: Nein Bea, ich bin hier BODY POSITIVE. #line:982c15
	Mae: LIES DAS INTERNET!!!!	 #line:902359
<<elseif $eat_everything_i_see>>
	Mae: Ich werde in Form kommen und allen in den Arsch treten! #line:552668
    Mae: Ich werde jeden essen! Jeden den ich sehe! #line:552bdf
<<endif>>
Bea: Mae, ich glaube du musst... #line:fb2482
<<if $maes_the_best>>
	Gregg: EFF EVERYONE! MAE IST DIE BESTE! #line:dea041
	Angus: Ich stimme zu! Aber vielleicht solltest du dich etwas zurueckhalten! #line:8c68cb
    Bea: Ich werde nie wieder einen von euch irgendwohin fahren. #line:a9041c
<<endif>>
Mae: EFF COLLEGE!!! #line:dd959d
Gregg: Ja! #line:b64e86
Mae: ALLE DIESE REICHEN KINDER UND HEISSEN LEUTE #line:b68dbd
<<dilate Mae 1 .5>>
<<lookRight Mae>>
Mae: ALLE SCHREIBEN SEX UND HABEN PAPIERE MIT EINANDER! #line:12868a
Angus: Das war knapp! #line:a18ce2
Mae: Und diese verdammte Statue... die jeden Tag auf mich zeigt #line:1260c7
Mae: sollte der Gruender sein #line:12e919
Mae: :( #line:95bf65
Mae: aber nur Formen... #line:58873c
Mae: Ich... #line:b96de5
Mae: :| #line:f2eb43
<<lookUp Mae>>
Mae: ICH BIN EIN TOTALES MUeLLMAeULCHEN! #line:e7fdf1
Mae: UND NICHTS WIRD JEMALS WIEDER GUT WERDEN! #line:84cc34
Mae: wir hatten hier nie eine Chance #line:2725d1
Mae: aber ich kann nirgendwo anders hingehen #line:7d3e62
Mae: ... #line:69e7f2
Mae: =_= #line:14b2b4
<<lookDown Mae>>
Mae: ich bin wieder zu Hause #line:8cd309
Mae: und ich kann etwas anderes tun als schlafen #line:43aca7
Mae: und alleine weinen #line:5a23a8
Mae: *urp* #line:ad94c2
Mae: weil die Dinge hier nicht nur Dinge sind, weisst du? #line:e3537e
Mae: oh Gott #line:23000b
Mae: ich bin so #line:b4deca
Mae: -_- #line:9cc58e
Mae: :( #line:39e0ab
Mae: krank #line:6a47ff
Mae: zu #line:008d95
Mae: Tod #line:2f6b3d
<<lookRight Mae>>
Mae: o_o #line:01a00b
Mae: :| #line:608a00
Mae: Ergibt das einen Sinn? Cole-io? #line:5b133d
Cole: Mae, es tut mir so leid. Ich wollte dich nicht veraergern. #line:ac5e3a
Cole: Ich finde, du bist eine wirklich tolle... #line:52284d
<<lookUp Mae>>
Mae: HOME AGAIN!!!! \o/ :) #line:b1f071
Gregg: WOOOO! #line:c011d7
Mae: WAAAAAAAAAAUGH :| #line:3a3605
<<close>>
<<swapRig Player MaePuke>>
<<trigger MaePuke Puke>>
<<wait 5>>
<<if $ate_tacos>>
	Mae: Tacos. #line:10f0df
<<endif>>
<<set $after_party to 1>>
<<fadeOutAmbientSFX AmbientSFX 1>>
<<fadeOutAmbientSFX AmbientSFX:Campfire 1>>
<<fadeOutAmbientSFX AmbientSFX:Walla 1>>
<<fadeOut 1>>
<<wait 1>>
<<scene BeaCar>>
===
title: Log
tags: 
colorID: 0
position: -6553,1064
---
<<sit>>
<<if $should_sit_log is 1>>
    [[ColeByFireEnding]]
<<else>>
    <<waitForInteractOrJump>>
<<endif>>
===
title: TestEnding
tags: 
colorID: 0
position: -6277,1111
---
<<close>>
<<playerJump>>
<<lookAt Mae Cole>>
<<wait 2>>
<<swapRig Player MaePuke>>
<<trigger MaePuke Puke>>
<<wait 5>>
===
