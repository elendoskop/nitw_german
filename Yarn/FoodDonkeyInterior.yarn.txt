title: EnterLevel
tags: 
colorID: 3
position: -1545,1363
---
<<if $act is 2 and $day is 1>>
    <<if $gregg_friendship_quest is 1>>
        <<show Gregg>>
        <<show SteveScriggins>>
        <<setCanInteract Gregg 0>>
        <<setCanInteract SteveScriggins 0>>
        <<walk Gregg GreggWalkIn>>
        <<walk Mae MaeWalkIn wait>>
        <<playerCanRun 0>>
        <<playerCanJump 0>>
        [[Intro]]
    <<endif>>
<<elseif $act is 4 and $day is 3>>
    [[LastDayScene]]
<<else>>
    <<set $wont_open_food_donkey_basement_door_1 to 0>>
<<endif>>
===
title: InitLevel
tags: 
colorID: 0
position: -1618,1131
---
<<hide Gregg>>
<<hide SteveScriggins>>

<<set $arcade_machine_1_done to 1>>
<<set $arcade_machine_2_done to 1>>
<<set $arcade_machine_3_done to 1>>
===
title: PickRobotMascot0
tags: 
colorID: 3
position: -867,1849
---
Gregg: Oh ja. Froschroboter. #line:cf2c6f
SteveScriggins: Endlich. Verdammt noch mal. #line:ad000b
Mae: Oh, halt die Klappe Steve. #line:3c7574
<<set $robot_head to 0>>
[[ArcadeMachinePicked]]
===
title: PickRobotMascot1
tags: 
colorID: 3
position: -600,1853
---
Gregg: Ja! Roboter-Schweinchen! #line:0f71f1
SteveScriggins: Endlich. Verdammt noch mal. #line:6d10f5
Mae: Ach halt die Klappe Steve. #line:55c353
<<set $robot_head to 1>>
[[ArcadeMachinePicked]]
===
title: PickRobotMascot2
tags: 
colorID: 3
position: -322,1856
---
Gregg: Cool cool. Mecha-bunn. #line:dbed46
SteveScriggins: Endlich. Verdammt noch mal. #line:5f8904
Mae: Oh, halt die Klappe Steve. #line:003392

<<set $robot_head to 2>>
[[ArcadeMachinePicked]]
===
title: Gregg
tags: 
colorID: 3
position: -653,933
---
<<if $gregg_friendship_quest_1 is 0>>
    <<stopDialogue Gregg>>
    [[CowHeadConvo]]
<<elseif $gregg_friendship_quest_1 is 1>>
    <<if $food_donkey_key_ring > 0>>
        <<stopDialogue Gregg>>
        [[DoorUnlock]]
    <<elseif $gregg_fq1_look is 0>>
        <<stopDialogue Gregg>>
        [[DoorLocked]]
    <<elseif $gregg_fq1_look is 1>>
        <<set $gregg_fq1_look to 2>>
        [[GreggLook1]]
    <<elseif $gregg_fq1_look is 2>>
        <<set $gregg_fq1_look to 3>>
        <<setCanInteract Gregg 0>>
        [[GreggLook2]]
    <<endif>>
<<elseif $gregg_friendship_quest_1 is 2>>
    <<stopDialogue Gregg>>
    Mae: Oh Gott. Diese Typen. #line:03fc35
    Mae: Ich war sowohl erschrocken als auch super begeistert von diesen Dingern #line:be955e
    Mae: wenn sie sie vor die Tuer gestellt haben. #line:f18682
    Mae: Sie bewegten sich! #line:74ff84
    Gregg: Sie sehen so winzig aus, wenn sie zerlegt sind. #line:7c8bc4
        ->Ich weiss nicht Mann, sie sind immer noch ziemlich gross. #line:c44eba
            Mae: Gott, diese Dinger waren wirklich gigantisch, als sie sie raus hatten. #line:9dfdc5
        ->Wir sind auch in Stuecken kleiner. #line:1f9746
            Gregg: Zu real. #line:ab0e9f
            Gregg: Ich hoffe, du wirst von einem Hubschrauber zerhackt. #line:c82c72
            Mae: Ich hoffe, du wirst von einem stumpfen Hackbeil zerhackt. #line:edc300
            SteveScriggins: Ihr zwei seid komisch. #line:face10
    Gregg: ok, wir klauen einen von denen #line:d45b76
    Mae: ist das ueberhaupt Diebstahl? #line:4f805d
    Gregg: es ist besser, wenn wir es als Diebstahl betrachten. #line:907dd1
    Mae: cool #line:70900d
    <<close>>
    <<wait .5>>
    Gregg: aaaagh. #line:dcc89d
        -> aaagh? #line:699a9e
        -> was? #line:e18767
    Gregg: aaaaaaaaaaaagh. #line:b35cc3
        -> bist du ok? #line:9ae45d
        -> Mann, was? #line:2b4fb7
    Gregg: {width=300}aaaaaaaaaaaaaaaaaaaaaaaaaaaagh!!! #line:3af4e2
        -> DUDE! WAS! #line:9c6b89
        -> HOER AUF ZU SCHREIEN!!! #line:2a09bd
    Gregg: Ich kann mich nicht entscheiden! Die sehen alle toll aus! #line:6939b7
    SteveScriggins: Du bekommst eins. Das ist die Abmachung. Beeilen Sie sich. #line:9f4197
    Mae: Oooh lass mich waehlen! Lass mich waehlen! :) #line:8b2667
    <<close>>
    <<run Gregg Gregg3 -1 wait>>
    Gregg: sicher. #line:4a3753
    Mae: :| #line:83366f
    <<set $gregg_friendship_quest_1 to 3>>
    <<set $robot_head_choice_enabled to 1>>
<<elseif $gregg_friendship_quest_1 is 3>>
    <<if $gregg_fq1_try_decide is 0>>
        <<set $gregg_fq1_try_decide += 1>>
        [[TryToDecide00]]
    <<elseif $gregg_fq1_try_decide is 1>>
        <<set $gregg_fq1_try_decide += 1>>
        [[TryToDecide01]]
    <<elseif $gregg_fq1_try_decide is 2>>
        [[TryToDecide02]]
    <<endif>>
<<endif>>

===
title: ArcadeMachinePicked
tags: 
colorID: 3
position: -607,2238
---
<<close>>
<<wait 1>>
SteveScriggins: Wisst ihr was? Ich habe nichts dabei. #line:539a13
SteveScriggins: Bis dann, meine Damen. #line:3d5f3d
<<close>>
<<walk SteveScriggins SteveScriggins1>>
Mae: Steeeeeeve!? #line:e0a430
SteveScriggins: Tot aufgefunden werden, Punks! #line:67a6f7
SteveScriggins: Hweh heh heh. #line:8db583
<<close>>
<<wait 2>>
<<hide SteveScriggins>>
Gregg: Er sagt Punks, als ob das etwas Schlechtes waere. #line:1e46a1
Mae: Das ist mir egal. #line:c2da95
Mae: Er ist Abschaum und ein Krimineller, und ich glaube, er war von Anfang an eine schlechte Nachricht. #line:9f6b9b
Gregg: Mm. Ja. #line:99b7af
Mae: Welchen Deal hast du mit ihm gemacht? #line:c3b499
Gregg: Er darf im Snack Falcon klauen, was er will, waehrend ich arbeite. #line:d25707
Mae: Siehst du? Ein Verbrecher auf der ganzen Linie. #line:c21299
Gregg: Manche Leute... #line:93ec64
<<close>>
<<wait 1>>
Mae: Sollen wir ein Fenster einschlagen, um das hier rauszuholen? #line:04f5b8
<<close>>
<<set $robot_crate to 1>>
<<irisOut 1 wait>>
<<scene CarryingRobotMascot>>
===
title: NotSure
tags: 
colorID: 3
position: -318,1575
---
Gregg: Sag mir Bescheid, wenn du bereit bist. #line:fa0d15
===
title: Intro
tags: 
colorID: 3
position: -1896,1678
---
Mae: Oh Gott. Was ist das fuer ein Geruch? #line:f6e088
SteveScriggins: Leim. #line:fad0f9
Mae: Oh nein. Ist das- #line:5d7fae
<<walk SteveScriggins SteveWalkIn>>
SteveScriggins: [wave]Sup ladies.[/wave] #line:abf7a2
Mae: =_= #line:9ee1e3
<<vo Mae dissapointed>>
Mae: ugh. :( #line:904858
Mae: Scheiss Steve Scriggins. #line:ec28b5
SteveScriggins: *FFFFFFFTTTTT!!* #line:fa65df
Mae: Hast du gerade tatsaechlich ein Furzgeraeusch gemacht? #line:ac2c40
SteveScriggins: Hweh heh heh. #line:654f35
SteveScriggins: Habt ihr Damen euch verlaufen oder so? #line:d8ac7e
Gregg: Ein Deal ist ein Deal, Steve. #line:83a07b
SteveScriggins: Das habe ich nicht vergessen. #line:551af9
Gregg: Wo ist es denn? #line:4af5c7
SteveScriggins: Im hinteren Teil. #line:13520c
Mae: Ich erinnere mich an dich, weisst du. #line:250d22
SteveScriggins: Ich wette, [wave]jeder[/wave] erinnert sich an dich. #line:9470de
    ->Der Lehrer verklagt dich schon? #line:53c672
        Mae: Oder hat er dich wenigstens auf einem Parkplatz verpruegelt? #line:74925f
        SteveScriggins: Welcher Lehrer? #line:b063c2
        Mae: Der, den du in der laengsten Nachtpause in den Schrank gesperrt hast. #line:1a29e0
        SteveScriggins: Pssh. Der Typ kann jetzt nicht mal mehr in kleine Raeume gehen. #line:6cb47f
        SteveScriggins: Er war schon immer ein Weichei, er brauchte nur jemanden, der es hervorbringt. #line:c1cad9
        Mae: ... O_O #line:72372f
        Gregg: ... O_O #line:adb578
    ->Was machst du hier drin? #line:27d830
        SteveScriggins: Deine Mutter. #line:3d5d01
        Mae: Toll Steve. #line:cfe2ef
        SteveScriggins: Auf allen Wegen. #line:1dd2cd
        Mae: Cool. #line:e105bb
        SteveScriggins: Ich bin dein neuer Vater. #line:d8d1c3
        Mae: Steve, du konntest dich wirklich nicht mit meiner Mutter vertragen. #line:976352
        SteveScriggins: Ist das eine Herausforderung? #line:35ff2a
Mae: o_o :| #line:a25e73
Gregg: o_o #line:332935
Gregg: ok gut #line:6e68be
Gregg: koennen wir mit dem Thema weitermachen? #line:41c1e9
<<close>>
<<run SteveScriggins SteveScriggins1 -1>>
<<run Gregg Gregg1>>
<<wait 3>>
<<setCanInteract Gregg 1>>
<<runDialogue Gregg GreggScrigginsBG1>>

===
title: GetKeyRing
tags: 
colorID: 3
position: 264,380
---
<<stopDialogue Gregg>>
Mae: Kein Wunder, dass der Laden untergegangen ist. #line:df2ec4
Mae: Die Leute lassen ihre Schluessel ueberall liegen! #line:ab556f
Mae: Macht nichts, wenn ich es tue. #line:f2ee16
<<fadeOutInternal 0>>
<<playOneShot event:/food_donkey/key_pickup Mae>>
<<wait 1>>
<<set $food_donkey_key_ring to 1>>
<<set $got_food_donkey_key_ring to 1>>
<<setPlayerInteractLocator Gregg MaeDoorConvo>>
<<warp Gregg GreggDoor2>>
<<flip Gregg 1>>
<<clearAltBodyRig Gregg>>
<<setAnimInt Gregg kickingDoor 1>>
<<setAnimInt SteveScriggins kickingDoor 1>>
<<lookRight SteveScriggins>>
<<warp Mae MaeFoundKey>>
<<flip Mae 1>>
<<freezePlayer>>
<<fadeInInternal 1 wait>>
-> Habe einige Schluessel gefunden! #line:67715d
    <<close>>
	<<wait 2>>
	Mae: Leute? #line:3477ef
-> Ich bin der Schluesselmeister! #line:42f95d
	Mae: Ich werde euch den Arsch aufschliessen! \o/ #line:0257c2
	SteveScriggins: Ja, das habe ich von dir gehoert! #line:b50905
Mae: _o_ #line:9aa625
<<setCanInteract Gregg 1>>
===
title: RobotMascot0
tags: 
colorID: 3
position: 118,1162
---
Mae: Alter Gruenling hier. #line:6dbf9c
Gregg: Ich habe immer von diesem Ding getraeumt. #line:5475d3
SteveScriggins: Sex-Traeume #line:8665a2
SteveScriggins: Wahrscheinlich. #line:9ea776
SteveScriggins: Das Beste, was man bekommen kann. #line:3de628
SteveScriggins: Wahrscheinlich. #line:6e4c27
Mae: ... #line:6c1434
Gregg: Haben Froesche ueberhaupt Sex? #line:906358
Mae: Nee, die haben so was wie Eier oder so. #line:49a952
SteveScriggins: ... #line:dcb6a8
SteveScriggins: Ihr zwei seid wirklich, wirklich dumm. #line:8e1c7a
<<set $robot_head_0_done to 1>>
<<set $num_checked_out_robot_heads += 1>>

<<if $num_checked_out_robot_heads is 2>>
    [[Checked2ndRobotMascot]]
<<endif>>
===
title: RobotMascot1
tags: 
colorID: 3
position: 408,1165
---
Mae: Ziemlich cooles Schwein. #line:9bb4d4
Gregg: Ja, ziemlich in Ordnung. #line:39c073
Gregg: Hatte er nicht mal Wurstsemmeln oder so in der Hand? #line:f12c6b
Mae: Ja! Das ist doch bescheuert! #line:52eb57
SteveScriggins: Er frisst seine eigene Art. #line:1c45c6
Mae: oder ihre eigene Art. #line:dc9312
SteveScriggins: Wie auch immer, PC-Polizei. #line:cb2684
Mae: PC selbst in den Verkehr Steve. #line:8c7d71
Gregg: ANYWAY. #line:41492b
<<close>>
<<wait .5>>
Mae: Kannibalenschwein, Mann. #line:4da150
Gregg: Kannibalenschwein. #line:b2bc85
<<set $robot_head_1_done to 1>>
<<set $num_checked_out_robot_heads += 1>>

<<if $num_checked_out_robot_heads is 2>>
    [[Checked2ndRobotMascot]]
<<endif>>
===
title: RobotMascot2
tags: 
colorID: 3
position: 655,1160
---
Mae: Oh Gott. Ich erinnere mich daran, dass diese weniger entsetzlich waren. #line:17cc92
Gregg: Damals waren sie gross. Konnte ihre Gesichter nicht sehen. #line:a4e94e
SteveScriggins: Ich muss mir die Gesichter der Damen ansehen. #line:e34b39
SteveScriggins: Hweh heh heh. #line:b5421a
<<lookAt Mae SteveScriggins>>
Mae: -_- #line:a87102
Gregg: Ich denke, das hier ist irgendwie niedlich. #line:34ea19
SteveScriggins: Ach, er findet es niedlich. #line:0d2101
Mae: Steve du bist eine Krankheit. #line:6d6a65
Mae: o_o #line:e5b1f0
<<set $robot_head_2_done to 1>>
<<set $num_checked_out_robot_heads += 1>>

<<if $num_checked_out_robot_heads is 2>>
    [[Checked2ndRobotMascot]]
<<endif>>
===
title: Checked2ndRobotMascot
tags: 
colorID: 3
position: 412,1539
---
SteveScriggins: GEEZ. Warum darf sie sich das aussuchen? #line:6a5cbc
SteveScriggins: Warum ist das so eine Sache? #line:0cc1d4
SteveScriggins: Das dauert ja ewig! #line:d79109
Mae: :( \o/ #line:3ff5dc
Mae: [shake=.05]SCRIGGINS! BITTE MEINEN GANZEN ASS![/shake] #line:e8e72d
SteveScriggins: WAS?! #line:497d4d
Mae: [shake=.05]GO LEGALY BRAIN DEAD[/shake] #line:7f2b56
Mae: [shake=.05]AUS OXYGENMANGEL[/shake] #line:a31f92
Mae: [shake=.05]DADURCH, dass ich an meinem ganzen Arsch erstickt bin![/shake] #line:6a859b
SteveScriggins: I... Ich... was? #line:2b4f1d
Gregg: Klassisch. #line:a59bd1
Mae: [shake=.05]DAS IST RICHTIG, STEVE! KLASSISCH![/shake] #line:58b520
SteveScriggins: [shake=.05]OH MEIN GOTT KOENNEN WIR DAS SOFORT FERTIG MACHEN[/shake] #line:56728d
Mae: :| _o_ #line:e04c44
===
title: DecisionPoint
tags: 
colorID: 3
position: -610,1588
---
Mae: Ok Kinder, wir nehmen den... #line:a60d14
[[Eigentlich bin ich mir noch nicht sicher...|NotSure]] #line:a56e50
<<if $robot_head_0_done is 1>>
    [[Froschkopf|PickRobotMascot0]] #line:ba2903
<<endif>>
<<if $robot_head_1_done is 1>>
    [[Schweinekopf|PickRobotMascot1]] #line:a002bf
<<endif>>
<<if $robot_head_2_done is 1>>
    [[Kaninchenkopf|PickRobotMascot2]] #line:4b2b97
<<endif>>
===
title: TryToDecide00
tags: 
colorID: 3
position: -957,1288
---
Gregg: Ugh... welcher? #line:66664a
Gregg: Ich bin schlecht in Entscheidungen, die wirklich etwas bedeuten. #line:69516a
Mae: Du hast die Smokings ausgesucht, die wir zum Abschlussball getragen haben. #line:75da50
Gregg: Ok ja... ich bin ziemlich gut darin. #line:73bfd9
Mae: Weisst du noch, wie Tim Whitley mit dem Riesentraktor zum Abschlussball gefahren ist? #line:4d04d5
Gregg: Auf jeden Fall. #line:bb0316
Gregg: Und... hast du gewaehlt? #line:3ca941
-> Mann gib mir Zeit. #line:e17c53
    Gregg: Ugh gut. #line:88a40e
    Mae: Mann, ist das eine ernste Angelegenheit? #line:c5db25
    Gregg: Das ist eine ernste Angelegenheit. #line:372a1a
    Mae: Ok, dann lass es uns wie eine ernste Angelegenheit behandeln. #line:834257
    Gregg: Ich bin aber hungrig. #line:dc3eed
    Mae: Uuuugh ich auch. #line:f704b7
    Gregg: Wir sollten spaeter essen. #line:23bfbd
    Mae: Ja, wahrscheinlich. #line:8ef300
    SteveScriggins: Ich MUSS noch woanders hin, weisst du! #line:622f69
-> Das habe ich wirklich. #line:bb5c3d
    Gregg: Oh cool #line:b1368f
    [[DecisionPoint]]
===
title: TryToDecide01
tags: 
colorID: 3
position: -621,1275
---
Gregg: Fuer welche entscheiden wir uns? #line:6a0429
-> [shake=.05]GIB MIR EINE SEK.[/shake] #line:d2c879
    Mae: \o/ #line:2806f2
    <<dilate Mae .5 .25>>
    Gregg: DUDE. CHILL. #line:21f7d5
    <<dilate Mae 1 .5>>
    Mae: _o_ #line:15fa0d
-> Aehm...ok. Ich glaube, ich hab's. #line:5d67bc
    Gregg: Sweeeeet. #line:a8057f
    [[DecisionPoint]]

===
title: TryToDecide02
tags: 
colorID: 3
position: -339,1279
---
Gregg: Irgendetwas? #line:5a0765
-> Noe! #line:c15a04
-> Yep! #line:c49d0a
    Gregg: YAY! #line:3d3e24
    [[DecisionPoint]]
===
title: GreggScrigginsBG1
tags: 
colorID: 3
position: -308,360
---
Gregg: {emote=hotdog} #line:0fd3c9
SteveScriggins: {emote=hotdog} #line:ee47fe
[[GreggScrigginsBG1]]
===
title: CowHeadConvo
tags: 
colorID: 3
position: -1149,1050
---
Gregg: Also funktioniert das manchmal noch? #line:94e75a
SteveScriggins: Eh. #line:c1f56a
SteveScriggins: Kennst du Beth Holstead? #line:07041a
Gregg: Ja. #line:7c6086
SteveScriggins: Sie war vor 6 Monaten hier und sie schwoert, dass es geredet hat. #line:f4826c
Gregg: Whoa. #line:9901fa
SteveScriggins: Musste nicht mal den Knopf druecken. #line:8a8c34
SteveScriggins: Aber ich wuesste nicht, wie viel ich darauf setzen wuerde. #line:84f2c5
SteveScriggins: Du kennst Beth. #line:ac3deb
Gregg: Irgendwie? #line:6800f9
SteveScriggins: Hier ist eine Menge Klebstoff drin. #line:c3cf18
Gregg: Ich denke schon. #line:1a7b84
<<set $gregg_friendship_quest_1 to 1>>
<<setInteractDir Gregg -1 0>>
<<walk SteveScriggins SteveScrigginsDoor1 -1>>
<<walk Gregg GreggDoor1>>
<<runDialogue Gregg GreggScrigginsBG2>>
===
title: GreggScrigginsBG2
tags: 
colorID: 3
position: -72,373
---
<<waitForMove SteveScriggins>>
SteveScriggins: {emote=cloud_rain} #line:a4aa76
<<close>>
<<wait 1>>
[[GreggScrigginsBG2]]
===
title: DoorLocked
tags: 
colorID: 3
position: -1144,801
---
<<flip SteveScriggins -1>>
SteveScriggins: [shake=.05]GODDAMMIT!!![/shake] #line:f42503
Mae: Was? #line:22597a
SteveScriggins: Jemand hat die verdammte Tuer abgeschlossen! #line:7c7633
SteveScriggins: Verdammt, ich wette, es war Beth. #line:5f6dd6
SteveScriggins: Oder Levy. #line:bc8c87
SteveScriggins: Oder der Zugmuell. #line:171a13
Gregg: Also... #line:433ee7
SteveScriggins: AAAAAAAAAAAARGH #line:08f274
SteveScriggins: Hoffentlich haben sie nichts gestohlen. #line:35fe96
Mae: ...aus dem verlassenen Lebensmittelladen... #line:78631e
Mae: ...mit den unverschlossenen Tueren... #line:08fe8e
Mae: Was ist ueberhaupt hinter dieser Tuer? #line:414a78
SteveScriggins: Koennte sie wahrscheinlich aufbrechen... #line:53e27a
Gregg: Oder ich koennte mein Dietrich-Zeug holen gehen. #line:5dc5cc
Mae: Ich wette, da ist ein Schluessel oder so. #line:a4ff55
SteveScriggins: Wie auch immer. #line:60fbb0
<<flip SteveScriggins 1>>
<<setAnimInt SteveScriggins kickingDoor 1>>
<<close>>
<<run Gregg GreggLook1>>
<<wait 3>>
<<stopMoving Gregg>>
<<warp Gregg GreggLook1>>
<<setAltBodyRig Gregg GreggRummageRig>>
<<playerCanRun 1>>
<<playerCanJump 1>>
<<set $gregg_fq1_look to 1>>
===
title: GreggLook1
tags: 
colorID: 3
position: -1108,506
---
Mae: Was machst du denn da? #line:7b4f3d
Gregg: Ich versuche, etwas zu finden, das hilft, die Tuer aufzubrechen. #line:1cdf49
Mae: Cool. Ich suche nach einem Schluessel, weil ich schlauer bin. #line:7e1e18
Gregg: Uh huh. Aber ich darf Sachen schlagen. #line:78d044
Mae: ... #line:d79326
Mae: Scheisse. #line:a242bf
===
title: GreggLook2
tags: 
colorID: 3
position: -817,445
---
Gregg: Wie viel Kleber haben sie hier gebraucht? #line:c14ced
Mae: Mann, man muss Sachen an Sachen kleben, um ein erfolgreiches Geschaeft zu fuehren. #line:d53ad2
Gregg: Nun, anscheinend nicht. #line:7aa431
Gregg: Was machst du da? #line:2f3d51
Mae: Ich sehe mir das Buero da oben an. #line:f5f106
Gregg: Cool. Pass auf, dass du dich nicht am Glas zu Tode schneidest. #line:d7a334
Mae: Dummer Tod. #line:2c43c9
Gregg: Dummer Tod. #line:bdc63d
===
title: DoorUnlock
tags: temp
colorID: 3
position: -228,847
---
<<setPlayerInteractLocator Gregg>>
Mae: Ihr Idioten hoert auf zu treten, bevor ihr euch die Oberschenkel bricht oder so. #line:6ac77f
Gregg: Ich bin kein Idiot. #line:9998a3
Mae: Ja sorry hey Idiot und Gregg hoer auf zu treten. #line:0d8b6d
Mae: Ich habe Schluessel. #line:7d4d5f
<<setAnimInt SteveScriggins kickingDoor 0>>
<<setAnimInt Gregg kickingDoor 0>>
<<walk Gregg GreggDoor1 1>>
<<flip SteveScriggins -1>>
<<lookNearest SteveScriggins>>
SteveScriggins: Woher hast du die? #line:21d50a
Mae: Aus dem Buero. Da oben. #line:85ac05
SteveScriggins: Kein Wunder, dass der Laden untergegangen ist. #line:dd9132
SteveScriggins: UEberall Schluessel liegen lassen! #line:61ec2e
Mae: Das war genau das, was ich dachte. #line:081083
<<walk SteveScriggins SteveScrigginsDoor2 1>>
<<walk Mae GreggDoor2>>
Mae: Ich bin veraergert, dass wir genau die gleichen Worte gedacht haben. #line:262410
SteveScriggins: Ich fand es besser. #line:cc94e2
<<lookRight Mae>>
Mae:{emote=food_donkey_key} #line:e5558a
<<fadeOutMusic 5>>
<<close>>
<<playOneShot event:/food_donkey/key_door Mae>>
<<wait 1.1>>
<<playOneShot event:/food_donkey/door_open Mae>>
<<wait 1>>
<<tell BasementDoor Unlock>>
<<wait 1>>
<<walk SteveScriggins SteveScriggins2>>
<<wait 1.5>>
<<setInteractDir Gregg 1 0>>
<<lookNearestMae>>
->Warum machen wir Verbrechen mit diesem Kerl? #line:5ee8c4
	Gregg: Wer raubt, darf nicht waehlerisch sein. #line:fe058b
	<<walk Gregg Gregg2 -1>>
	Mae: Das koennen sie wirklich. #line:3b160d
	SteveScriggins: Du weisst, dass ich dich hoeren kann, oder? #line:a09ff1
->Ugh. Warum musste es ausgerechnet Steve sein? #line:49924c
	Gregg: Er hatte einen Insider. #line:3b3c2e
	<<walk Gregg Gregg2 -1>>
	Mae: Was in? #line:e58cdf
	SteveScriggins: Ich habe die Eingangstuer aufgebrochen. #line:23f714
<<playMusic food_donkey_faces 3>>
<<set $gregg_friendship_quest_1 to 2>>
===
title: LastDayScene
tags: 
colorID: 6
position: -1420,1661
---
<<if $food_donkey_last_scene is 0>>
    <<set $food_donkey_last_scene to 1>>
    <<fadeIn 1>>
    <<warp Mae MaeMallardRats1>>
    <<run Mae MaeMallardRats2 wait>>
    <<lookLeft Mae>>
    Mae: :) \o/ [wave]Meine Babies!!![/wave] #line:30eeae
    <<lookUp Mae>>
    Mae: Meine kleinen Rattenbabys... #line:4b21af
    <<lookNearest Mae>>
    Mae: Nun, nicht mehr so klein. #line:45b100
    Mae: Du hast es geschafft! Du hast dich befreit! #line:a1ab40
    Mae: U_U Es ist ein [wave]Wunder![/wave] #line:cd174f
    <<lookUp Mae>>
    Mae: _o_ Ich werde diesen Moment einfach ein bisschen geniessen. o_o #line:51e243
    Mae: Kuemmere dich nicht um mich... #line:610c8d
    <<fadeOut 2 wait>>
<<else>>
    <<fadeOut .0001 wait>>
<<endif>>
<<warp Mae MaeSitUpTop>>
<<sit3>>
<<lookNearest Mae>>
<<setSortingLayerName PlayerBG5>>
Mae: :) #line:a73b60
<<wait .5>>
<<fadeIn 2 wait>>
<<waitForInteractOrJump>>
<<fadeOut 2 wait>>
<<scene Outskirts left>>
===
