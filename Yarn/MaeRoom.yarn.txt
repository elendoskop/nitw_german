title: Bed
tags: demo2
colorID: 0
position: 3493,1361
---
[[DoSleep]]
===
title: Internal
tags: demo
colorID: 0
position: 1835,1792
---
<<if $mae_room_internal is 0>>
	Mae: {align=top,furthestLocator}Wenn ich nicht aus diesem Raum rauskomme, werde ich ihn wahrscheinlich [speed=.7][color=999]mit mir zusammen niederbrennen.[/all] #line:b8186a
	<<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 1>>
	Mae: {align=top,furthestLocator}Ich wette, jemand hat das getan. Zum Beispiel aus [color=999]politischen Gruenden.[/all]-_- #line:7c0703
    Mae: o_o #line:d91611
	<<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 2>>
	Mae: {align=top,furthestLocator}Ich frage mich, was uebrig bleiben wuerde, wenn ich [speed=.7][color=999]den Raum mit mir darin niederbrennen wuerde?[/all]o_o #line:4e50e5
	<<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 3>>
	Mae: {align=top,furthestLocator,width=2}Knochen wahrscheinlich. Kupferrohre. [speed=.7][color=999]Ein schreiendes Phantom.[/all] #line:0e224c
	<<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 4>>
	Mae: {align=top,furthestLocator,width=2}"Ja, das ist ein guter Ort" #line:edff90
	Mae: {align=top,furthestLocator,width=3}"aber ich habe gehoert, dass es hier einmal ein Haus gab und ein Maedchen es niedergebrannt hat." #line:a322de
	Mae: "[speed=.5][color=999][shake=.03]Mit ihr darin.[/all]":( \o/ #line:7e7347
    Mae: :| _o_ #line:33f726
	<<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 5>>
	Mae: {align=top,furthestLocator,width=2}"Das erwaehnen wir gerne gegenueber potentiellen Kaeufern."[/all] #line:5e02cd
	<<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 6>>
	Mae: {align=top,furthestLocator,width=2}"Es gibt eine Menge Geschrei." #line:582c59
	Mae: "[speed=.7][color=999]Ich meine war."[/all] #line:c023b8
	<<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 7>>
	Mae: {align=top,furthestLocator,width=2}In der Tat, sie tat es in einer Nacht [speed=.7][color=999][shake=.04]GENAU WIE HEUTE...[/all]-_- #line:317cd1
    Mae: o_o #line:d76775
	<<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 8>>
	Mae: {align=top,furthestLocator,width=2}Und deshalb sollte ich [wave]rausgehen.[/all] #line:623215
	<<set $mae_room_internal += 1>>
<<elseif $mae_room_internal is 9>>
	Mae: {align=top,furthestLocator,emote=maeburn,emoteAlign=middle} #line:493746
<<endif>>
===
title: Mirror_A1D3
tags: 
colorID: 2
position: 2088,1793
---
<<fadeOut 1>>
<<wait 1>>
<<scene Mirror>>
===
title: BedChoice
tags: 
colorID: 0
position: 3789,1092
---
<<if $act is 2 and $day is 4 and $night is 1>>
    Mae: No sleep! Harfest!!! #line:b9a1ee
<<elseif $act is 1 and $day is 3 and $party_prep>>
    Mae: Nicht schlafen! Party Time! Ich muss mich fertig machen! #line:051e1a
<<elseif $night is 0>>
    [[Bed]]
<<elseif $act is 1 and $day is 0>>
    [[Bed]]
<<else>>
    Mae: Schlafenszeit? #line:6bed1b
    [[Nein|NoBed]] #line:af024e
    [[Ja|Bed]] #line:0633e1
<<endif>>
===
title: Bass
tags: 
colorID: 0
position: 4063,1090
---
<<if $bass_practice_times_today >= 2>>
    Mae: Thaaaat ist genug Bass fuer einen Tag. #line:212bc0
    <<set $bass_practice_done to 1>>
<<else>>
    <<if $bass_practice_times_today is 1>>
        Mae: Hmm... vielleicht noch einmal? #line:d85479
    <<else>>
        Mae: Koenntest du etwas Bass spielen? #line:165374
    <<endif>>
    Mae: Aber welches Lied? #line:02d34a
    -> nee... spaeter. #line:7cc20e
        Mae: vielleicht. #line:f426b9
    -> Weltraumdrache <<if $actday > 0100>> #line:50b676
        <<if $did_bass_practice_heavy_doze_intro is 0>>
            <<set $did_bass_practice_heavy_doze_intro to 1>>
            Mae: WANDERER IN SPAAAAAACE... #line:a4adfd
            Mae: NOMADIN DES VERBOTENEN RAUMS... #line:c33be3
        <<endif>>
        <<runBandPractice 3>>
    -> Irgendwo anders sterben <<if $actdaynight >= 01015>> #line:fb9c84
        <<runBandPractice 0>>
    -> Zyklen <<if $did_gregg_friendship_quest_3 or $did_bea_friendship_quest_4>> #line:2e4016
        <<if $did_bass_practice_cycles_intro is 0>>
            <<set $did_bass_practice_cycles_intro to 1>>
            Mae: Dieses Lied. Ihr wisst schon. Das Lied. #line:5e9324
        <<endif>>
        <<runBandPractice 4>>
    -> Merkwuerdiger Herbst <<if $actdaynight >= 02015>> #line:255175
        <<runBandPractice 1>>
    -> Tick Tock <<if $library_charity_bearity > 0>> #line:439b9a
        <<if $did_bass_practice_tick_tock_intro is 0>>
            <<set $did_bass_practice_tick_tock_intro to 1>>
            Mae: Ah ja, Tick Tock. #line:8d3750
            Mae: Dieses schreckliche Charity-Bearity-Lied ueber die Zeitmessung. #line:977ab9
            Mae: Ich habe den Text mit Gregg in der Highschool ein bisschen aufgepeppt. #line:331649
            Mae: Mal sehen, ob ich mich an alle Worte erinnern kann... #line:ef5622
        <<endif>>
        <<runBandPractice 5>>
    -> KÃ¼rbiskopf-Typ <<if $actdaynight >= 03015>> #line:33bfe7
        <<runBandPractice 2>>
    -> Geh und stirb, Engelsgesicht <<if $actday > 0103 and $did_get_dead_angel_face_intro is 0>> #line:f9255e
        <<set $did_get_dead_angel_face_intro to 1>>
        Mae: Whoa, nein. :( -_- #line:6aaaed
        Mae: Nein. #line:c95744
        Mae: Den werde ich auf keinen Fall spielen. #line:d77add
        Mae: Zu peinlich. #line:556605
        Mae: :| o_o #line:dcc73a
    //-> Bass Practice 4 <<if $actday > 0400>>
    //    <<runBandPractice 6>>
<<endif>>
===
title: NoBed
tags: 
colorID: 0
position: 3805,1659
---
Mae: Du kannst nicht im Bett leben, Mae. #line:f0ec03
===
title: EnterLevel
tags: 
colorID: 0
position: 2830,1303
---
<<if $night is 0>>
    <<if $mae_room_morning>>
        <<tell _AmbientSFXMorning Play>>
    <<else>>
        <<tell _AmbientSFXDay Play>>
    <<endif>>
<<else>>
    <<tell _AmbientSFXNight Play>>
<<endif>>
<<if $day > 0 and $night is 0 and $party_prep is 0 and $woke_up is 0>>
    <<fadeIn 3>>
    <<if $act is 4 and $day is 2>>
        // let the hospital ambience keep playing
    <<else>>
        <<if $act is 3>>
            <<playMusic waking_up_act3>>
        <<else>>
            <<playMusic waking_up>>
        <<endif>>
    <<endif>>
    <<wait 3>>
    <<waitForInteractOrJump>>
    <<fadeOutMusic .5>>
    <<if $act is 4 and $day is 2>>
        <<fadeOut .5 wait>>
        <<animationTrigger MaeBed Empty>>
    <<else>>
        <<animationTrigger MaeBed WakeUp>>
        <<wait 3.5>>
        <<fadeOut .5 wait>>
    <<endif>>
    <<show Laptop>>
    <<animationTrigger MaeBed Idle>>
    <<warp Mae PlayerStart>>
    <<snapToFloor>>
    <<setCameraMode FollowPlayer>>
    <<if $act is 4 and $day is 1>>
        <<hide Laptop>>
    <<elseif $act is 4 and $day is 2>>
        <<hide Laptop>>
    <<elseif $act > 1 or $day >= 2>>
        <<show Laptop>>
    <<else>>
        <<hide Laptop>>
    <<endif>>
    <<fadeIn .5>>
    <<set $woke_up to 1>>
<<else>>
    <<if $act > 1 or $day >= 2>>
        <<show Laptop>>
    <<else>>
        <<hide Laptop>>
    <<endif>>
<<endif>>

<<if $act is 1 and $day is 2>>
    <<if $enter_mae_room < 010200 and $night is 0>>
        [[A1D2Morning]]
    <<endif>>
<<elseif $act is 1 and $day is 3>>
    <<if $after_party>>
        <<animationTrigger MaeBed Sleeping>>
        <<hide Laptop>>
        <<hide Player>>
        [[A1D3AfterParty]]
    <<elseif $party_prep>>
        <<set $mirror_done to 0>>
        <<if $mae_room_party_prep is 0>>
            [[A1D3PartyPrep]]
        <<endif>>
    <<endif>>
<<elseif $act is 2 and $day is 1>>
    <<if $enter_mae_room < 020100 and $night is 0>>
        [[A2D1Morning]]
    <<endif>>
<<elseif $act is 2 and $day is 4>>
    <<if $harfest_prep is 1 and $harfest_costume is 0>>
        <<set $mirror_done to 0>>
    <<endif>>
<<endif>>

<<if $act is 4 and $day is 2>>
    <<if $night is 0>>
        <<set $death_walk to 1>>
        <<playIdleAnimationPlayer>>
        //<<set $death_prairie_effects to 1>>
        <<if $enter_mae_room < 040200>>
            [[A4D2Morning]]
        <<endif>>
    <<endif>>
<<elseif $act is 4 and $day is 3>>
    <<if $found_pentagrams >= 3>>
        <<unlockAchievement Thryy_Wyrd_Tyyns>>
    <<endif>>
<<endif>>

<<if $act is 3 and $day is 3>>
    <<unlockAchievement Astral_Secrets>>
<<endif>>

<<if $act is 2 and $day is 1>>
    <<unlockDoodle EverythingFeelsBad>>
    <<unlockDoodle NeverDrink>>
<<endif>>

<<if $act is 3 and $day is 1>>
    <<unlockDoodle Ibon1>>
    <<unlockDoodle Eide1>>
<<endif>>

<<if $act is 4 and $day is 3>>
    <<unlockPentagramDoodle>>
    <<unlockDoodle Ibon3>>
    <<unlockDoodle Shapes>>
<<endif>>
===
title: DoSleep
tags: 
colorID: 0
position: 3478,1701
---
<<close>>
<<fadeOut .5 wait>>
<<setCameraMode Stationary>>
<<camera position 0 0>>
<<hide Laptop>>
<<hidePlayer>>
<<fadeIn .5>>
<<animationTrigger MaeBed Sleep>>
<<wait 7>>

[[EndSleep]]
===
title: A1D2Morning
tags: 
colorID: 2
position: 2531,1376
---
Mae: Wow! Es ist buchstaeblich 36 Stunden her, dass ich online war. #line:527470
Mae: Das darf nicht sein. #line:d0c0fb
<<set $enter_mae_room to 010200>>
<<fadeOut 1 wait>>
<<scene Laptop>>
===
title: A1D3PartyPrep
tags: 
colorID: 2
position: 2829,1645
---
<<set $mae_room_party_prep to 1>>
Mae: OK ok ok. #line:34ba15
Mae: Es ist Zeit zu gehen. #line:1c6fe3
Mae: Igitt, ich sollte wohl aufpassen, dass ich nicht wie eine Hoehlenbestie aussehe. #line:94f4e1
===
title: A1D3AfterParty
tags: 
colorID: 2
position: 3197,1348
---
<<set $mirror_done to 1>>
<<show Bea>>
Bea: Da hast du's. #line:e6d2e0
Mae: Es tut mir leid, Bea. Ich bin ein Wrack. #line:3df7c4
Bea: Weisst du? Wie auch immer. #line:208e5a
Bea: Es ist nicht deine Schuld. #line:96ecaa
Bea: Du bist noch ein Kind. #line:a3b28a
Mae: Ich bin aelter als du! Zwei Monate! #line:c2c29d
Bea: Ja gut, #line:4b7d9b
Bea: Ich blieb hier und wurde aelter #line:91603e
Bea: waehrend du weggingst und derselbe geblieben bist. #line:9d4919
Mae: zzz #line:a7865a
Bea: *seufz* #line:503330
Bea: Gutnacht Mayday. #line:0ea5b1
<<close>>
[[EndSleep]]
===
title: Laptop
tags: 
colorID: 0
position: 2076,1507
---
<<fadeOut .5 wait>>
<<scene Laptop>>
===
title: TriggerLaptop
tags: 
colorID: 0
position: 1836,1512
---
<<set $trigger_laptop to 0>>

Mae: Mal sehen, ob dieser Angus-Fix funktioniert... #line:1a6732
Mae: Schlimmer kann es zumindest nicht mehr werden. #line:246c71
Mae: ich schaetze, es koennte brennen? #line:c6fe5a
Mae: jedenfalls. #line:3d5618
<<irisOut 1 wait>>
<<scene Laptop>>
===
title: EndSleep
tags: 
colorID: 0
position: 3161,1654
---
<<fadeOut 3 wait>>

<<if $act is 1 and $day is 3>>
    <<scene AstralAct1Day3>>
<<elseif $act is 2 and $day is 1>>
    <<scene AstralAct2Day1>>
<<elseif $act is 2 and $day is 2>>
    <<scene AstralAct2Day2>>
<<elseif $act is 2 and $day is 3>>
    <<scene AstralAct2Day3>>
<<elseif $act is 2 and $day is 4>>
    <<scene BlackoutWoods>>
<<elseif $act is 3 and $day is 1>>
    <<scene AstralAct3Day1>>
<<elseif $act is 3 and $day is 2>>
    <<scene AstralAct3Day2>>
<<else>>
    <<endDay>>
<<endif>>
===
title: Mirror_A2D4
tags: 
colorID: 3
position: 2079,2258
---
<<fadeOut 1>>
<<wait 1>>
<<scene Mirror>>
===
title: Mirror_A2D1
tags: demo2
colorID: 3
position: 2082,2027
---
Mae: Oh gut. #line:0ca0d4
Mae: Ich hatte recht. #line:cd1c65
Mae: Ich sehe aus wie die Hoelle. #line:d8f5db
Mae: Verbrennt es! #line:cfe544
<<set $mirror_done to 1>>
===
title: A2D1Morning
tags: demo2
colorID: 3
position: 2538,1642
---
<<set $enter_mae_room to 020100>>
<<set $mirror_done to 0>>
Mae: =_= #line:297513
Mae: Nun #line:71032c
Mae: das war ein seltsamer Fall. #line:31fabd
Mae: Ich fuehle mich furchtbar. #line:f6439d
Mae: :\ #line:df2910
Mae: Ich sehe wahrscheinlich furchtbar aus. #line:c09536
Mae: igitt. #line:044650
Mae: Ich werde diesen Raum niederbrennen. #line:cb2c74
Mae: o_o #line:80183e
Mae: Mit mir drin. #line:0f0a0d
Mae: Lebe meine restlichen Tage als schreiender Geist. #line:18afa3
Mae: Jep. #line:12291f
Mae: :| #line:d0dffe
<<unlockDoodle FireGhost>>
<<unlockDoodle EverythingFeelsBad>>
<<unlockDoodle NeverDrink>>
===
title: InitLevel
tags: 
colorID: 0
position: 2746,1034
---
<<set $mirror_done to 1>>

<<if $day > 0 and $night is 0 and $party_prep is 0 and $woke_up is 0>>
    <<hidePlayer>>
    <<setCameraMode Stationary>>
    <<camera position 0 0>>
    <<hide Laptop>>
    <<animationTrigger MaeBed WakeUpIdle>>
<<endif>>
===
title: A4D2Morning
tags: 
colorID: 0
position: 2603,1911
---
<<set $enter_mae_room to 040200>>
Mae: Urg. #line:32b978
<<close>>
<<fadeOut 1 wait>>
<<overrideLoadingScreenType Blank>>
<<scene MaeStreet>>
===
title: EndBassPractice
tags: 
colorID: 0
position: 4353,1097
---
<<if $quit_bass_practice>>
<<else>>
    <<if $bass_practice_right is 100>>
        Mae: Aehm, das war so ziemlich perfekt. #line:9ef632
    <<elseif $bass_practice_right > 90>>
        // player did pretty alright!!
        Mae: Nicht schlecht, wenn man bedenkt, dass ich nicht weiss, wie man dieses Lied spielt! #line:eaa421
    <<elseif $bass_practice_right < 50>>
        // player played less than 50% of the notes registered
        Mae: Ich weiss nicht einmal, wie man dieses Lied spielt. Warum mache ich das eigentlich. #line:2c6a9e
    <<elseif $bass_practice_right < 20>>
        // player probably mashed random buttons for the whole song
        Mae: Nun... #line:6da361
        Mae: Ja, das war nicht gut. #line:38d6d1
    <<elseif $bass_practice_right < 50>>
        // pretty baaaad
        Mae: Autsch. #line:bfac2b
    <<else>>
        // a catch-all for doing pretty meh
        Mae: Ich kenne dieses Lied wirklich nicht. #line:9b88f4
    <<endif>>
    
    // unlock doodles if player did well enough
    <<if $bass_practice_right > 90>>
        <<if $bass_practice_song_index is 3>>
            <<unlockDoodle SpaceDragon>>
        <<elseif $bass_practice_song_index is 4>>
            <<unlockDoodle Cycles>>
        <<elseif $bass_practice_song_index is 5>>
            <<unlockDoodle CharityBearity>>
        <<endif>>
    <<endif>>
    
    <<if $bass_practice_song_index is 0>>
        // die anywhere else
    <<elseif $bass_practice_song_index is 1>>
        // weird autumn
    <<elseif $bass_practice_song_index is 2>>
        // pumpkin head guy
    <<elseif $bass_practice_song_index is 3>>
        // Space Dragon
    <<elseif $bass_practice_song_index is 4>>
        // Cycles
    <<elseif $bass_practice_song_index is 5>>
        // Tick Tock
    <<endif>>
<<endif>>
===
