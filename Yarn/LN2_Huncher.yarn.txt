title: ApproachHuncherDoor
tags: 
colorID: 0
position: 313,163
---
<<set $huncher_intro to 1>>
<<musicFadeOut 10>>
<<speechBubbleSizeOverride 1.5>>
HuncherOutside: Wer ist das, der da auf meiner Veranda herumlaeuft? #line:30571f
Adina: Hallo? #line:d131d2
HuncherOutside: Oh Maedchen, jetzt ist alles vorbei. #line:1bdf69
<<close>>
<<speechBubbleSizeOverride 0>>
<<hide HuncherOutside 1>>
<<wait 1>>
<<playOneShot event:/lost_constellation/teleport Adina>>
<<fadeOut .5 1 1 1>>
<<wait .5>>
<<hide SceneLink:HuncherInterior>>
<<warp Adina WarpHuncherInterior>>
<<clearSnowballs>>
<<wait 1>>
<<fadeIn .5 1 1 1>>
<<spawn BAMF Adina>>
<<wait 1>>

===
title: InitLevel
tags: 
colorID: 0
position: -305,28
---
<<hide HuncherOutside>>
<<hide HuncherSisterGhost>>
<<set $skeleton_arm to 0>>
<<set $skeleton_arm_fire to 0>>
<<set $skeleton_arm_done to 1>>
<<if $kicked_out_of_huncher_house is 1>>
	<<hide SceneLink:HuncherExterior>>
<<endif>>
<<if $huncher_sister_convo > 0>>
	<<scene LN2_LostWoods>>
<<endif>>
===
title: ApproachHuncherInside
tags: 
colorID: 0
position: 340,607
---
<<tell HuncherMusic to Play>>
HuncherInside: Es gibt alte Leichen im Norden. #line:91fe77
HuncherInside: Wo der Boden nie auftaut. #line:c0b484
HuncherInside: Seit Jahrtausenden in Erde und Eis eingefroren. #line:5b9694
HuncherInside: In einigen von ihnen steckt eine Krankheit, gegen die wir nicht mehr stark sind. #line:49aed8
<<tell HuncherKid to PlayInstrument>>
HuncherInside: Eines Tages wird sich die Erde erwaermen, und das Eis wird schmelzen. #line:a7bcd8
HuncherInside: Und diese Krankheit wird die Arbeit an uns beenden #line:d73926
HuncherInside: die begann, als wir zum ersten Mal geboren wurden. #line:552b36
Adina: ... #line:a9e4c8
<<setTalkAnimType HuncherInside 2>>
HuncherInside: [shake=.02]KID! Wuerdest du bitte mit dem Hupen aufhoeren?[/shake] #line:f87047
<<setTalkAnimType HuncherInside 1>>
HuncherKid: Noe. #line:7c4070
HuncherInside: Gut, Junge, gut. #line:e9986b
HuncherInside: Ich werde dich dort lassen, wo sie dich niemals finden werden. #line:3afd4a
HuncherKid: Ha ha ha. #line:6b61ed
[[Tut mir leid, stoere ich bei etwas?|InterruptingSomething]] #line:33b190
[[Ich muss zum Gefrorenen See.|NeedFrozenLake]] #line:572e33
<<show SceneLink:HuncherInterior>>
<<set $huncher to 1>>

===
title: HuncherStop
tags: 
colorID: 0
position: -264,564
---
<<if $huncher < 2>>
	HuncherInside: Stop. #line:1906a1
	Adina: UGHK! #line:96baa6
	Adina: Ich kann mich nicht bewegen. Das tut weh. Halt! #line:2f8f4b
	HuncherInside: Komm nicht naeher. #line:12d4f4
	<<set $huncher to 2>>
<<elseif $huncher is 2>>
	HuncherInside: Halt. #line:bc99fc
	Adina: UGHK! #line:eb1abc
<<endif>>

===
title: AngryHuncherFix
tags: 
colorID: 0
position: 606,366
---
<<set $huncher_outside to 1>>
<<hide HuncherInside>>
<<hide SceneLink:HuncherExterior>>
<<show SceneLink:HuncherInterior>>
<<hide TriggerDialogue:ApproachHuncherInside>>
<<tell HuncherMusic to Play>>
<<trackAllSpeechBubblesToCharacters 1>>
<<if $huncher_fix is 0>>
	Adina: !!! #line:7f4ab1
<<elseif $huncher_fix is 1>>
	Adina: Da kommt sie... #line:e44836
<<elseif $huncher_fix is 2>>
	Adina: Ich brauche einen Platz zum Verstecken! #line:e46c25
<<elseif $huncher_fix is 3>>
	Adina: Zu den Brombeeren! #line:614e4e
<<else>>
	Adina: Fertig! #line:6a3d07
<<endif>>
<<tell WinchElevator to SetBottom>>
<<warp HuncherKid HuncherKidStand>>
<<animationTrigger HuncherKid Stand>>
<<tell HuncherKid to StopInstrument>>
<<flip HuncherKid 1>>
<<wait .5>>
<<close>>
<<wait .5>>
<<trackAllSpeechBubblesToCharacters 0>>
<<warp HuncherOutside WarpHuncherExterior>>
<<spawn BAMF HuncherBAMF>>
<<flip HuncherOutside 1>>
<<show HuncherOutside .5>>
<<wait .5>>
<<if $player_in_left_brambles is 0 and $player_in_right_brambles is 0>>
	[[PlayerCaught]]
<<else>>
	<<if $player_in_left_brambles > 0 or $player_in_right_brambles > 0>>
		<<walk HuncherOutside HuncherFix>>
		<<show SceneLink:HuncherExterior>>
		<<if $huncher_fix is 0>>
			HuncherOutside: Verdammte Eichhoernchen! #line:9cf09e
		<<elseif $huncher_fix is 1>>
			HuncherOutside: Die verdammten Eichhoernchen wollen keine Pause machen.  #line:65b972
		<<elseif $huncher_fix is 2>>
			HuncherOutside: Eichhoernchen? #line:c27229
		<<else>>
			HuncherOutside: Eichhoernchen. #line:7a922f
		<<endif>>
		<<close>>
		<<wait .5>>
		<<setAnimInt HuncherOutside waggleArms 1>>
		HuncherOutside: Nordwind, schwarzer Wind, Wolf im Dunkeln! #line:eff14f
		<<wait 3>>
		HuncherOutside: Igitt, dieses Wetter benimmt sich nie. #line:169112
		<<wait 2>>
		HuncherOutside: Ich muss jedes Mal das ganze verdammte Ding sagen. #line:6d24bb
		<<wait 2>>
		HuncherOutside: Blaues Feuer im Norden! Ich habe deinen geheimen Namen entdeckt! #line:b1b57e
		<<wait 2>>
		HuncherOutside: Ich habe ihn tief in der Erde vergraben, und darauf ein Haus gebaut! #line:7da3ca
		<<wait 2>>
		HuncherOutside: Ihr muesst diesem Haus gehorchen! #line:c72cad
		<<wait 2>>
		HuncherOutside: Nun, blas nach Westen! #line:247652
		<<tell SnowWeathervane to Reset>>
		<<wait .5>>
		<<wait 1.5>>
		<<setAnimInt HuncherOutside waggleArms 0>>
		HuncherOutside: Ok, das war's. #line:f2054a
		<<wait 2>>
		<<close>>
		<<animationTrigger HuncherOutside Idle>>
		<<if $player_in_house is 1>>
			<<wait 10>>
		<<endif>>
		<<hide SceneLink:HuncherExterior>>
        <<if $player_in_left_brambles is 0 and $player_in_right_brambles is 0 and $player_in_house is 0>>
        	[[PlayerCaught]]
        <<endif>>
		<<flip HuncherOutside -1>>
		<<walk HuncherOutside HuncherDoor wait>>
        <<if $player_in_left_brambles is 0 and $player_in_right_brambles is 0 and $player_in_house is 0>>
        	[[PlayerCaught]]
        <<endif>>
		<<set $huncher_outside to 0>>
		<<spawn BAMF HuncherBAMF>>
		<<hide HuncherOutside 0>>
		<<wait 1>>
		<<warp HuncherOutside WarpHuncherInterior>>
        <<flip HuncherOutside 1>>
        <<spawn BAMF HuncherBAMF>>
		<<show HuncherOutside 1>>
		<<if $player_in_house > 0>>
			[[PlayerCaught]]
		<<else>>
			[[CleanUpHuncher]]
		<<endif>>
	<<else>>
		[[PlayerCaught]]
	<<endif>>
<<endif>>

===
title: PlayerCaught
tags: 
colorID: 0
position: 896,134
---
<<stopGlobalDialogue>>
<<freezePlayer>>
<<setTalkAnimType HuncherOutside 2>>
<<if $player_in_house is 1>>
	<<set $player_caught_inside += 1>>
<<else>>
	<<set $player_caught_outside += 1>>
<<endif>>
<<setAnimInt HuncherOutside waggleArms 0>>
<<if $huncher_fix is 0>>
	HuncherOutside: [shake=.015]Runter mit dir![/all] #line:484c12
<<elseif $huncher_fix is 1>>
	<<if $player_in_house>>
		HuncherOutside: [shake=.015]Get out of my house![/shake]
	<<else>>
		HuncherOutside: [shake=.015]Bleib weg von meiner Veranda![/shake] #line:275983
	<<endif>>
<<elseif $huncher_fix is 2>>
	HuncherOutside: [shake=.02]Akzeptiere es! Du wirst heute Nacht in den Brombeeren erfrieren![/shake] #line:43928c
<<else>>
	HuncherOutside: [shake=.02]Geh weg![/shake] #line:f6f7d3
<<endif>>
<<setTalkAnimType HuncherOutside 1>>
<<freezePlayer>>
<<setAnimInt HuncherOutside waggleArms 1>>
<<set $kicked_out_of_huncher_house to 1>>
<<fadeOut .5 1 1 1>>
<<playOneShot event:/lost_constellation/teleport Adina>>
<<wait 1>>
<<warp Adina PlayerRejected>>
<<playerFace 1>>
<<wait 1>>
<<fadeIn .5 1 1 1>>
<<spawn BAMF Adina>>
<<freezePlayer>>
<<set $player_was_caught to 1>>
[[CleanUpHuncher]]

===
title: CleanUpHuncher
tags: 
colorID: 0
position: 903,586
---
<<musicFadeOut 8>>
<<setAnimInt HuncherOutside waggleArms 0>>
<<flip HuncherOutside 1>>
<<hide HuncherOutside>>
<<show HuncherInside>>
<<set $huncher_outside to 0>>
<<if $kicked_out_of_huncher_house is 0>>
	<<show SceneLink:HuncherExterior>>
<<else>>
	<<hide SceneLink:HuncherExterior>>
<<endif>>
<<set $huncher_fix += 1>>
<<tell WinchElevator to SetTop>>
<<tell SnowWeathervane to Reset>>
<<warp HuncherKid HuncherKidSit>>
<<animationTrigger HuncherKid Sit>>
<<tell HuncherKid to PlayInstrument>>
<<flip HuncherKid -1>>

<<if $player_was_caught is 0>>
	<<unfreezePlayer>>
<<else>>
	[[PlayerReactionToGettingCaught]]
<<endif>>



===
title: NeedSpecialKeyItem
tags: 
colorID: 0
position: 903,-96
---
<<if $player_caught_inside > 0>>
	Adina: Da war etwas in diesem Haus. Wenn ich nur schnell genug waere... #line:d1ef53
<<elseif $player_caught_inside is 0>>
	Adina: Dafuer brauche ich etwas Besonderes. #line:27c591
	Adina: Oh Gott, ich muss zurueck in dieses Haus. #line:b5c23a
	Adina: Das ist die schlimmste Longest Night aller Zeiten. #line:febd25
<<endif>>

===
title: Snowproblem
tags: 
colorID: 0
position: 314,-71
---
Snowproblem: ... #line:875a8e

===
title: NeedFrozenLake
tags: 
colorID: 0
position: 340,851
---
HuncherInside: Wenn du heute Nacht da draussen bist und frierst, #line:0c63bb
HuncherInside: wirst du nach meinem Ring suchen? #line:2dd1ce
HuncherInside: Wenn einmal etwas im Gebuesch liegt, kann ich es nie mehr sehen. #line:2063a0
HuncherKid: Ich habe deinen Ring letzte Woche gefunden. #line:b6c0b4
HuncherInside: Oh! Das hast du also. #line:00e22d
HuncherInside: Er ist mein zweitwertvollster Besitz. #line:1d822b
HuncherInside: Ich bewahre den wertvollsten in einem Schrein auf, so wertvoll ist er fuer mich. #line:1497d6
HuncherInside: Und so schmerzhaft. Wir muessen das Kostbare und das Schmerzhafte nah beieinander halten. #line:30c4ac
Adina: Ich habe das Gefuehl, wir kommen hier immer wieder vom Thema ab. #line:12b3c5
HuncherInside: *schnueffel schnueffel* #line:dc6c4e
HuncherInside: Ich kann den Segen des Waldgottes auf dir riechen. #line:772a6f
HuncherInside: Wenn du etwas wuesstest, wuerdest du dich waschen, bis es mit deiner Haut abfaellt. #line:e86089
HuncherInside: Wenn du gehst, gehst du hinunter in die Brombeeren wie alle anderen. #line:3d7fd0
HuncherInside: Wenn ich rausgehen muss, um das Wetter zu regeln #line:3a4888
HuncherInside: weil die Wetterfahne umgestossen wurde, schon wieder... #line:ef71c4
HuncherKid: Das ist ein wirklich dummes System, das ihr da habt. #line:e7729e
HuncherInside: ...wenn ich rauskomme, um das zu tun #line:7dde17
HuncherInside: Ich schaue auf die Senke hinaus, und nichts ruehrt sich. #line:8c4f20
HuncherInside: Und genau das wird auch mit dir geschehen. #line:d30758
Adina: Warum toetest du mich dann nicht einfach jetzt? #line:cdd88e
HuncherInside: Wegen des Geruchs. #line:b9ac6e

===
title: InterruptingSomething
tags: 
colorID: 0
position: 103,851
---
HuncherInside: Ich glaube, du kennst die Antwort. Du bist gerade aufgetaucht. #line:b60dc5
HuncherInside: Igitt, wir sind alle einfach aufgetaucht. #line:900186
HuncherInside: Das Problem ist, dass wir bleiben. #line:f4904e
HuncherInside: Aber darueber musst du dir keine Sorgen machen. #line:a4b55b
HuncherInside: Oder doch? #line:e74338
[[Ist das eine extrem vage Drohung?|VagueThreat]] #line:eb7b70
[[Eigentlich muss ich zum Gefrorenen See.|NeedFrozenLake]] #line:79076f
===
title: VagueThreat
tags: 
colorID: 0
position: 102,1083
---
HuncherKid: Sie? Vage Drohungen? #line:f422e9
HuncherInside: Oh, ich werde jetzt mal konkret, Kleiner. #line:ad5b5d
HuncherInside: Ich werde deine Figuren in einer einsamen Hoehle vergraben. #line:bc82a4
HuncherInside: Nicht einmal begraben! Ich habe die letzte nicht begraben. #line:2ea9eb
HuncherInside: Und SIE hat Hausarbeit gemacht! #line:629129
HuncherInside: Deine Familie wird dich nie finden. #line:e2e278
HuncherKid: Oh nein. Nicht das. #line:908ce2
[[Ist das deine normale Dynamik?|NormalDynamic]] #line:37e7a4
[[*AHEM.* Ich muss zum Gefrorenen See.|NeedFrozenLake]] #line:a20e89
===
title: NormalDynamic
tags: 
colorID: 0
position: 339,1083
---
HuncherInside: Das ist die Dynamik von allem. #line:c3d17b
HuncherInside: Bei ihr ist es nicht anders. #line:ef0f22
HuncherInside: Im Saegewerk haben sie dir nicht mal einen Namen gegeben, stimmt's, Kleiner? #line:f90ee6
HuncherInside: Nur Kind? #line:588ce5
HuncherKid: Entschuldigung, der volle Name war Sawmill Kid. #line:80f0d1
HuncherInside: Nun, jetzt bist du nur noch Kid. #line:6af004
Adina: Haben sie dir keinen Namen gegeben? #line:eac05e
HuncherKid: ... #line:ad4be3
HuncherInside: Du merkst nie, wie wenig und wie sehr du einen Namen brauchst #line:09a4c1
HuncherInside: bis sie dir deinen verweigern. #line:0acc41
Adina: Huh. #line:ed10fb
Adina: Es tut mir leid, dass ich uns immer wieder darauf zurueckfuehre, #line:5420c8
Adina: aber ich muss den Gefrorenen See erreichen. #line:0f8534
[[NeedFrozenLake]]
===
title: HuncherInside.1
tags: 
colorID: 0
position: 573,852
---
HuncherInside: Was bist du eigentlich? #line:54113b
Adina: Ich bin eine Astronomin. Ehemaliger Lehrling. #line:9b7671
Adinda: Der erste in meinem Observatorium, um genau zu sein. #line:e2bae1
HuncherInside: Oh Schatz, all diese Sterne in deinem Kopf. #line:30e496
HuncherInside: Sie werden heute Nacht wie Kerzen erloeschen. #line:cdae20
Adina: Was bist du? #line:5655f6
HuncherInside: Der Waldgott ist nicht wirklich ein Gott. #line:9dd048
HuncherInside: Ich bin doppelt so gross wie sie. #line:2bd351
HuncherInside: Ich bin doppelt so real. #line:120b58
HuncherInside: Ehrlich, wer auch immer du bist, sogar du bist doppelt so real wie sie. #line:199ef4
Adina: Adina. Adina Astra. #line:3206bb
HuncherInside: Ein laecherlicher Name. Du hast dir selbst einen Namen gegeben. Das merke ich. #line:9b9066
HuncherInside: ...nur der letzte Teil. #line:3ca1c3
HuncherInside: Nun, was wuerde ein kleines Maedchen dazu bringen, ihren Nachnamen zu aendern? #line:655867
Adina: Nichts, was du verstehen wuerdest. #line:1fe9aa
HuncherInside: Du hast doch keine Ahnung davon. Ich komme von irgendwoher. #line:488083
HuncherInside: Wie auch die Frau vor mir. #line:85cf5f
HuncherInside: Und wir fanden die Huette, und ich fuetterte den Ofen. #line:5a9d58
HuncherInside: Und wir haben ueberlebt, und dann habe ich ueberlebt. #line:a9bba5
HuncherInside: Und der Streit mit dem Waldgott dauerte Jahrhunderte lang an. #line:b78a1f
HuncherInside: Bis er endete. Vor kurzem erst. #line:456415
HuncherKid: Nun- #line:ba0c9c
HuncherInside: SCHEISSE, KID. #line:9d3ffe
HuncherInside: Ich schwoere, wenn ich hier nicht physisch anwesend waere, wuerde sie ueber alles reden. #line:d9f872

===
title: HuncherInside.2
tags: 
colorID: 0
position: 798,852
---
Adina: Bist du eine Hexe? #line:754ba9
HuncherInside: Was ist eine Hexe? Du meinst die Frauen in den Waeldern? #line:38bb9d
HuncherInside: Ein Mann in den Waeldern ist ein Einsiedler oder ein Waldarbeiter oder ein Jaeger. #line:4b1dc0
HuncherInside: Sie hatten keinen Namen fuer Frauen, die nicht dort waren, wo sie sein sollten. #line:a54127
HuncherInside: Also stahlen sie einen Namen, den sie fuerchteten und hassten #line:47446b
HuncherInside: und drueckten ihn uns auf. #line:9b1099
HuncherKid: Auf dich. Das Wort, das sie fuer Maedchen wie mich benutzen, ist "gekidnappt". #line:dc76f6
HuncherInside: *seufz* #line:fe6a74
HuncherInside: Das hier. Jeden Tag, ich schwoere. #line:24e6a7
HuncherInside: Will keine Hausarbeit machen. #line:e49db5
HuncherInside: Frechdachs. Frechdachs-Mund. #line:594a22
Adina: Warum schickst du sie nicht dahin zurueck, wo sie herkommt? #line:210c33
HuncherInside: Das ist die eine Frage, auf die ich keine Antwort habe. #line:4bef0a

===
title: HuncherInside.3
tags: 
colorID: 0
position: 1024,851
---
HuncherInside: Wir sind fertig mit reden. #line:31ac92
HuncherInside: Es tut mir leid, Adina Astra. #line:153458
HuncherInside: Ich weiss, du wolltest leben #line:3a36eb
HuncherInside: Aber ich habe dir diese Zukunft genommen #line:9a175c
HuncherInside: Und sie gehoert jetzt mir. #line:763b38
HuncherInside: Und du kannst sie nicht zuruecknehmen. #line:b1c622
<<set $kicked_out_of_huncher_house to 1>>
<<playOneShot event:/lost_constellation/teleport Adina>>
<<fadeOut .5 1 1 1>>
<<set $huncher_exit_enabled to 1>>
<<musicFadeOut 10>>
<<wait .5>>
<<warp Adina PlayerRejected>>
<<playerFace 1>>
<<wait 1>>
<<fadeIn .5 1 1 1>>
<<spawn BAMF Adina>>
<<hide SceneLink:HuncherExterior>>

===
title: HuncherSisterConvo
tags: 
colorID: 0
position: -7,426
---
<<musicFadeOut 1>>
<<clearSnowballs>>
<<show HuncherOutside .1>>
<<warp HuncherOutside HuncherSnowproblemsSpawn>>
<<wait .5>>
<<playerFace -1>>
Snowproblem: Was? Nein. Nein. #line:d5702d
<<playerFace -1>>
Snowproblem: *hust* *hust* *hust* #line:c7082b
<<playerFace -1>>
Snowproblem: NOOOOOOOOOOOO!!!! #line:bcfc7e
<<close>>
<<playOneShot event:/lost_constellation/ghost_appear HuncherSisterGhost>>
<<show HuncherSisterGhost 4>>
<<wait 4>>
<<spawn BAMF HuncherBAMF>>
<<wait 1>>
<<walk HuncherOutside HuncherNextToSnowman wait>>
HuncherOutside: What is this? #line:444f0e
HuncherOutside: Oh. Oh Gott. Nein. #line:05979d
HuncherOutside: Maedchen, was hast du getan? #line:cad6b4
HuncherSisterGhost: [wave][size=.85]Du... du hast es gesehen![/all] #line:64cb5f
HuncherSisterGhost:{width=8} [wave][size=.75]DU WAAAAAAAAAAAAAATCHED![/all] #line:83d561
HuncherOutside: Ich war's nicht! Es war dieser Ort! #line:b02ee9
HuncherSisterGhost: [wave][size=.85]Es war der Ofen. Es war das, was du geworden bist.[/all] #line:435556
HuncherOutside: Ich konnte mir nicht aussuchen, was mit mir passiert! #line:9c96a8
HuncherSisterGhost: [wave][size=.85]Mir war so kalt. Ich hatte solche Angst.[/all] #line:ca211e
HuncherOutside: Ich musste ueberleben! #line:9a049e
HuncherSisterGhost: [wave][size=.85]und das habe ich nicht.[/all] #line:fe3672
Adina: Was soll das? Wer sind Sie? #line:08c26f
HuncherOutside: Wage es nicht zu sprechen, du Verschwendung von einem Maedchen. #line:bb98ed
HuncherSisterGhost: [wave][size=.85]Ich war ihr Zwilling! Wir flohen in die Waelder, identisch in allen Dingen[/all] #line:94f283
HuncherSisterGhost: [wave][size=.85]bis auf eines[/all] #line:0b3835
HuncherSisterGhost: [wave][size=.85]Als sie meinen Koerper aus dem Wasser zog[/all] #line:f88f20
HuncherSisterGhost: [wave][size=.85]der einzige Teil von mir, den sie behalten hat[/all] #line:e48ee5
HuncherSisterGhost: [wave][size=.85]war die Taetowierung.[/all] #line:76fabf
HuncherOutside: Ich habe es ueber die Jahrhunderte immer bei mir behalten. #line:3d27fd
HuncherSisterGhost: [wave][size=.85]Ich glaubte an eine Idee von dir...[/all] #line:339c47
HuncherSisterGhost: [wave][size=.85]das ist schon so lange tot wie ich jetzt bin.[/all] #line:76a9bf
HuncherSisterGhost: [wave][size=.85]aber du hast ueberlebt.[/all] #line:58040e
HuncherOutside: Ich... I... #line:0c0e45
Adina: Oh mein Gott. Es tut mir so leid. Es tut mir so leid fuer das hier. #line:fedd77
HuncherOutside: Raus hier. #line:fc9179
Adina: Es tut mir leid!!! Ich wollte nie... #line:5422cd
<<setTalkAnimType HuncherOutside 2>>
HuncherOutside:{width=8, angryWobble=5} [shake=.05]GET OOOOOOOOOOOOOOOOUT![/shake] #line:4568ff
<<set $huncher_sister_convo to 1>>
<<set $day to 3>>
<<playOneShot event:/lost_constellation/teleport Adina>>
<<fadeOut .5 1 1 1>>
<<wait .5>>
<<wait .5>>
<<fadeColor 1 0 0 0>>
<<wait 1>>
<<showTalkingHeads>>
Mae:... #line:6f84b9
Granddad: Ja, Mae? #line:1d72c3
Mae: Worum geht es in dieser Geschichte? #line:cf28dd
Granddad: Worum soll es denn gehen? #line:08467a
Mae: Wenn ich mir aussuchen darf, wovon sie handelt, dann handelt sie von gar nichts. #line:c7cfd0
Granddad: Huh. Das ist eine ganz schoene Aussage. #line:12f046
Mae: Ich bin wirklich schlau. #line:b84f2a
Granddad: Nun, du hast dir den Plan ausgedacht, Banken auszurauben #line:f46c8e
Granddad: also bist du eindeutig an etwas dran. #line:458623
Mae: Ich will wissen, worum es hier geht. #line:da6a73
Granddad: Ich denke, das musst du selbst herausfinden. #line:e2790b
Mae: ... #line:173b38
Mae: Das gefaellt mir nicht. #line:2c480d
<<hideTalkingHeads>>
<<wait 1>>
<<scene LN2_LostWoods>>

===
title: HuncherKid.1
tags: 
colorID: 0
position: 1338,-115
---
Adina: Ich kann dich hier rausholen. #line:bb151e
HuncherKid: Du kannst dich nicht mal selbst hier rausholen. #line:884ffe
HuncherKid: Ich komme kaum aus dem Dachboden raus #line:1a7c96
HuncherKid: und weg von diesem extrem gruseligen Schrein. #line:bf4583
Adina: Hier ist es nicht sicher fuer dich. #line:22d5c0
HuncherKid: Wusstest du, dass ich das Gift gemacht habe und sie hat sich nicht mal bedankt? #line:432c9a
HuncherKid: Sie wird den ganzen Ruhm dafuer einheimsen! #line:7613a5
Adina: Was? #line:8041d4
HuncherKid: Ich weiss, nicht wahr? #line:2efeb6
HuncherKid: Einen Gott zu ermorden ist nichts, wofuer man jemand anderem die Lorbeeren ueberlassen will. #line:a1883c
Adina: Oh mein Gott. #line:91ac52
<<set $forest_god_poison to 1>>
Adina: Ich dachte, du waerst... #line:3acb63
HuncherKid: Ich werde ueberleben. #line:98482f
HuncherKid: Niemand, der wissen wuerde, dass ich weg bin, ist jemand, von dem ich moechte, dass er mich findet. #line:936be4
Adina: Ich muss gehen. #line:4652bf

===
title: EnterLevel
tags: 
colorID: 0
position: -12,173
---
<<animationTrigger HuncherKid Sit>>
<<if $snowproblem is 1 and $huncher_sister_convo is 0>>
	[[HuncherSisterConvo]]
<<endif>>
===
title: HuncherKid.2
tags: 
colorID: 0
position: 1585,-113
---
HuncherKid: Geh weg, wenn du kannst. #line:ec4aff
===
title: SnowmanBuildSpot
tags: 
colorID: 0
position: 632,-83
---
<<if $huncher_outside is 1>>
	Adina: Der Huncher ist unterwegs! #line:f98957
<<elseif $huncher is 0>>
	Adina: Hm. #line:cbf93d
	Adina: Ich sollte wohl zuerst das Haus ueberpruefen. #line:6839e6
<<elseif $huncher_fix > 0 and $hasSpecialKeyItem > 0>>
	Adina: Ok, Zeit zu bauen. #line:2fb294
<<else>>
	Adina: Ein Teil des seltsamen Schnees liegt weit oben in den Baeumen. #line:6ac5e9
	Adina: Wir brauchen einen guten starken Wind. #line:d3d09c
	Adina: Ha ha ha. Ich muss nur den Wind kontrollieren, das ist alles. Einfach. #line:7f23e5
<<endif>>

===
title: PlayerReactionToGettingCaught
tags: 
colorID: 0
position: 1353,553
---
<<set $player_was_caught to 0>>
<<wait 1>>
<<alwaysRequireButtonToAdvance 1>>
<<if $hasSpecialKeyItem is 0>>
	<<if $huncher_fix is 1>>
		Adina: Igitt. #line:faf841
		Adina: Ich werde heute Nacht nicht hier sterben. #line:91be24
		Adina: Ich bin einfach nicht. #line:3666a2
		Adina: Ich muss sehen, was in dieser Hoehle ist. #line:f5d228
		Adina: Ich muss in dieses Haus. #line:0349c2
		Adina: Ok, lass uns gehen. #line:a085f4
	<<elseif $huncher_fix is 2>>
		Adina: Mir ist so kalt. So kalt. #line:c5b211
		Adina: Nein. #line:1e24ac
		Adina: Ich bin eine Astronomin. #line:0215da
		Adina: Das ist mein Himmel, verdammt noch mal. #line:5fbf86
		Adina: Ich werde das ueberleben. #line:10dff0
	<<elseif $huncher_fix is 3>>
		Adina: Sie scheint von dieser Wettersache abgelenkt zu sein. #line:788729
		Adina: Vielleicht... hmm... #line:ee59c0
	<<endif>>
<<endif>>
<<alwaysRequireButtonToAdvance 0>>
<<unfreezePlayer>>
===
