title: EnterLevel
tags: 
colorID: 0
position: 292,288
---
<<if $bea_friendship_quest_3 is 0>>
    [[BeaFQ3Start]]
<<endif>>
===
title: InitLevel
tags: 
colorID: 0
position: 292,49
---
<<show Bea>>
<<show Dad>>

<<set $dad_ham_panther_costume to 1>>
===
title: CannedGoods
tags: 
colorID: 3
position: 393,1283
---
Mae: Hmm was soll ich nehmen... #line:0a9fe3
<<shuffleNextOptions>>
-> Succotash... Thuccotath. #line:f40611
    <<set $grocery_canned_goods to 1>>
-> Gruene Haehnchen #line:5c56a7
    <<set $grocery_canned_goods to 2>>
-> Huehnercreme-Suppe #line:2c6f37
    <<set $grocery_canned_goods to 3>>
Mae: Hm. hoert sich das gut an? #line:8972e1
-> Nein #line:3e1e2c
    <<set $grocery_canned_goods to 0>>
-> Ja #line:74390d
    Mae: Lecker. #line:5ef3f9
===
title: Boxes
tags: 
colorID: 3
position: 635,1286
---
Mae: ok ok was soll ich nehmen was soll ich nehmen #line:eb6f12
<<shuffleNextOptions>>
-> Mac n’ Cheese #line:fa76a0
    <<set $grocery_box to 1>>
-> Ueberbackene Kartoffeln #line:828558
    <<set $grocery_box to 2>>
-> Kartoffelpueree #line:aa1bef
    <<set $grocery_box to 3>>
-> Reis #line:8842c2
    <<set $grocery_box to 4>>
Mae: Hm. bin ich... sicher? #line:4a19d2
-> Nein #line:051137
    <<set $grocery_box to 0>>
-> Ja #line:7e3fff
    Mae: mmmjam. lecker. #line:020910
===
title: Cooler
tags: 
colorID: 3
position: 930,1283
---
Mae: Hmm hmm. #line:03323f
<<shuffleNextOptions>>
-> Kekse #line:e83960
    <<set $grocery_cooler to 1>>
-> Rahmspinat #line:7f16e1
    <<set $grocery_cooler to 2>>
Mae: bin ich sicher? #line:173e2f
-> Nein #line:956cb6
    <<set $grocery_cooler to 0>>
-> Ja #line:62cd7f
    Mae: ooh la la. #line:0b8aab
===
title: TriggerDadConversation
tags: 
colorID: 3
position: 940,269
---
<<stopFollowing Bea>>
<<walk Bea BeaDeli>>
<<walk Mae MaeDeli wait>>
[[DadDeli]]
===
title: DadDeli
tags: 
colorID: 3
position: 1248,244
---
Dad: Ahoi, meine Damen! #line:b0f39c
Mae: Hey Dad! #line:09a736
Dad: Hey Bea, lange nicht mehr gesehen! #line:a9a7d4
Bea: Wie geht es Ihnen, Mr. Borowski? #line:d7c51c
Dad: Kann mich nicht beklagen! #line:82cd69
Dad: Nur ich und das Fleisch. #line:e65545
-> Meatfriends. #line:6bf390
    Dad: Es hilft, mit deinen besten Fleischstuecken und Fischen befreundet zu sein. #line:d706c5
-> Nur du? #line:1f44c2
    Dad: Oh, nicht immer. Aber heute Abend, ja. #line:d3efcb
    Dad: Es gibt schlimmere Orte auf der Welt, wo man sein kann. #line:78360b
Bea: Stimmt. #line:e8ffdc
<<lookAt Mae Dad>>
Mae: Immer noch komisch, dich hier zu sehen. #line:830792
Dad: Besser als die Alternativen! #line:14bc89
Mae: Ich glaube, ich habe meinen alten Musiklehrer gesehen, als wir reinkamen. #line:a49e56
Bea: Oh ja. Das *WAR* Mr. Armistead. #line:5b9d35
Mae: Sieht so aus, als ob es sein Job ist, die Leute zu begruessen? #line:6d3895
Dad: Jep. Das nennt man einen Begruesser. #line:2c8bb0
Dad: Ich wusste doch, dass ich ihn von irgendwoher kenne. #line:3d57c4
Mae: Gibt es Alternativen? #line:ff5ba3
Dad: Funktioniert nicht. #line:ccd6c3
Mae: Oh.		 #line:087c8d
Dad: Und was macht ihr reizenden Damen hier? #line:ccba89
Bea: Wir holen uns nur etwas zum Abendessen. #line:b17b70
Dad: Wie geht es deinem Vater? #line:7796ad
Bea: Ganz gut. #line:71d878
Dad: Was kann ich euch bringen, meine Damen? #line:249e60
[[Meat]]
===
title: BeaFQ3Start
tags: 
colorID: 3
position: 552,283
---
<<setAltBodyRig Bea BeaGroceryRig>>
<<setCanInteract Bea 1>>
<<set $block_for_groceries to 1>>
Mae: Dieser Ort hat den Essensesel getoetet! #line:d80018
Bea: Ja, irgendwie schon. #line:126ebb
Mae: FOOOOOD! #line:f3d6c9
Bea: Weisst du, niemand, der hier arbeitet, hat etwas damit zu tun. #line:dd0a34
-> Stimmt. #line:75332c
-> EEEEEEEEESEL! #line:913892
    Bea: Du hast Ham Panther Corporate gezeigt, wer der Boss ist. #line:50cbfc
    Bea: Eigentlich, #line:7b7b9b
    Bea: wusstest du, dass unser Senator im Vorstand von Ham Panther sitzt oder so? #line:7fcbab
    Mae: Du weisst, dass du null Spass machst, oder? #line:947657
    Bea: Das weiss ich, ja. #line:9c73f1
Bea: Ok. #line:dc268f
Bea:  Ich muss schnell ein paar Sachen holen #line:0519cd
Mae: Ach, ich dachte, wir wuerden zusammen abhaengen. #line:8e830f
Bea: Uh. #line:634095
Bea: Wir haengen rum. #line:d3152a
Bea: Ich bin nur hier, um etwas zum Abendessen fuer Dad und mich zu holen. #line:b452f6
Bea: Ich habe keine Lust auf den grossen Einkauf heute Abend. #line:d1e3c7
Mae: Lass mich aussuchen, was wir haben! #line:1c115a
Bea: Oh, tut mir leid, kommst du zum Essen? #line:fb386d
Mae: Darf ich? #line:ed4c86
Bea: Um #line:5e93e9
Bea: Ich denke schon? #line:8c9c2b
Mae: Das fragst du? #line:11de4c
	Bea: Habe ich nicht. #line:eab27e
	Bea: Aber jetzt fuehlt es sich komisch an, es nicht zu tun... #line:5814f2
Mae: Ich werde aussuchen, was wir dann essen! #line:6371db
<<close>>
<<wait 1.5>>
Bea: Warte, meinst du das ernst? #line:3eb3e7
Mae: Ja! #line:a8770f
Bea: Weisst du was? Nur zu. Ich hasse einkaufen. #line:3cecad
Bea: Brauchst du... ein Hauptgericht und zwei Beilagen? #line:f82c64
-> Schick! #line:602e6e
    Bea: Eh. Wir sind traditionell. #line:c1b6e4
    Mae: Was fuer ein Hauptgericht? #line:93f378
-> Was zaehlt als Hauptgericht? #line:10f4f1
Bea: Wir koennen wieder in der Fleischabteilung anhalten, wenn du alles andere ausgesucht hast. #line:0cd607
Mae: Was ist mit dem Nachtisch? #line:e32ffb
Bea: Wir sind doch keine Kinder. #line:f6e596
Mae: Appetitanreger? #line:0302de
Bea: Wir sind knapp bei Kasse, Mae. #line:57c74a
Mae: ok ok ok du bleibst hier und ich bin gleich wieder da #line:323b95
Bea: Mach's einfach schnell und nicht zu schrecklich. ausserdem ist Dad empfindlich gegen Brokkoli. #line:9940ee
<<if $did_bea_friendship_quest_1 is 1>>
    Bea: Oh, ausserdem - kein Ladendiebstahl dieses Mal. #line:24d2aa
    Mae: Ah. #line:c726dd
    Bea: Ich kann mir ein paar Lebensmittel leisten, Mae. #line:d0c4a4
    Bea: Auch dieser Ort hat Kameras. #line:506520
    Mae: Gutes Argument. #line:383df7
<<endif>>
<<playMusic grocery_shopping 3>>
===
title: Bea
tags: 
colorID: 3
position: 710,-3
---
<<if $grocery_canned_goods is 0>>
    Bea: Wir brauchen eine Konservendose oder so. #line:70d893
<<elseif $grocery_box is 0>>
    Bea: Vielleicht auch etwas aus einer Dose? #line:69962d
<<elseif $grocery_cooler is 0>>
    Bea: Sieh dir die Kuehlbox an. Da sind Sachen drin. #line:356763
    Mae: cooles Zeug. #line:186792
<<elseif $grocery_meat is 0>>
    Mae: Mein Vater arbeitet im Feinkostladen. Wir koennten wegen des Fleisches dorthin gehen! #line:df97a8
    Bea: Na gut. #line:5b8552
    <<set $grocery_picking_interactions_done to 1>>
    <<follow Bea Mae>>
    <<setCanInteract Bea 0>>
    <<set $block_for_groceries to 0>>
<<endif>>
===
title: FoodChart
tags: notes question!
colorID: 1
position: -4,397
---
//question: Syntax looks inconsistent for these options.//
// Should there be a colon after $grocery_cooler and $grocery_meat?//
$grocery_canned_goods: #line:76a00b
1 - Sukkotasch #line:3751d9
2 - Gruene Bohnen #line:796c2e
3 - Huehnercreme-Suppe #line:85d290

$grocery_box: #line:e1f87d
1 - Makkaroni mit Kaese #line:f257b6
2 - Ueberbackene Kartoffeln #line:1d1e3a
3 - Stampfkartoffeln #line:2dc6b6
4 - Reis #line:121815

$grocery_cooler #line:d3048c
1 - Kekse #line:42defe
2 - Rahmspinat #line:9c49f3

$grocery_meat #line:3c4582
1 - Rinderhackfleisch #line:cb319e
2 - Huhn #line:4362e8
3 - Fisch #line:fa8f97
4 - Schweinekoteletts #line:566a8e
===
title: EndGroceries
tags: 
colorID: 3
position: 2232,394
---
<<if $grocery_meat is 1 and $grocery_bakey_shakey is 0 and $grocery_box is 1 and $grocery_canned_goods is 1>>
    //Ground Beef (no bakey shakey) + Mac n’ Cheese + Succotash = GOOD
    <<set $grocery_quality to 1>>
<<elseif $grocery_meat is 4 and $grocery_bakey_shakey is 1 and $grocery_box is 2 and $grocery_canned_goods is 2>>
    //Pork Chops (with Bakey Shakey) + Scalloped Potatoes + Green Beans = GOOD
    <<set $grocery_quality to 1>>
<<elseif $grocery_meat is 2 and $grocery_bakey_shakey is 0 and $grocery_canned_goods is 3 and $grocery_cooler is 1>>
    //Chicken (nobakey shakey) + Cream of Chicken Soup + Biscuits = GOOD
    <<set $grocery_quality to 1>>
<<elseif $grocery_meat is 3 and $grocery_bakey_shakey is 0 and $grocery_box is 1 and $grocery_cooler is 2>>
    //Fish (no bakey shakey) + Mac n’ Cheese + Creamed Spinach
    <<set $grocery_quality to 1>>
<<elseif $grocery_meat is 3 and $grocery_bakey_shakey is 1>>
    //Fish (bakey shakey)
    <<set $grocery_quality to -1>>
<<elseif $grocery_meat is 1 and $grocery_bakey_shakey is 2>>
    //Beef  (bakey shakey)
    <<set $grocery_quality to -1>>
<<elseif $grocery_meat is 1 and $grocery_box != 1>>
    //Beef + anything other than Mac n’ Cheese
    <<set $grocery_quality to -1>>
<<elseif $grocery_meat is 3 and $grocery_canned_goods is 3>>
    //Fish + Cream of Chicken
    <<set $grocery_quality to -1>>
<<elseif $grocery_meat is 2 and $grocery_bakey_shakey is 1 and $grocery_canned_goods is 3>>
    //Chicken (bakey shakey) + Cream of Chicken Soup
    <<set $grocery_quality to -1>>
<<elseif $grocery_meat is 4 and $grocery_canned_goods is 3>>
    //Pork Chops + Cream of Chicken Soup
    <<set $grocery_quality to -1>>
<<elseif $grocery_meat is 1 and $grocery_canned_goods is 3>>
    //Beef + Cream of Chicken Soup
    <<set $grocery_quality to -1>>
<<endif>>

<<fadeOutMusic 5>>
<<lookAt Mae Dad>>
Dad: So, meine Damen. Lasst uns sehen, was ihr alles habt! #line:efe52f
Dad: hmmm. hmmm. ja. ok. #line:bd6d5f
<<if $grocery_quality is 1>>
    Dad: Sieht nach einem tollen Abendessen aus, das du geplant hast! #line:cee0e9
<<elseif $grocery_quality is -1>>
    Dad: Nun, das ist... interessant. #line:c44a10
    Mae: Interessant? #line:c8fcd7
    Dad: Vielleicht bin ich einfach nicht auf dem Laufenden. #line:540b97
    Dad: viel Glueck! #line:39c13e
<<else>>
    Dad: Sieht voellig 100 Prozent ok aus. #line:c5754d
<<endif>>

Bea: Alles klar. Danke Herr Borowski! #line:4119a8
Dad: Sag deinem Vater einen schoenen Gruss von mir! #line:9a4d5d
Mae: Bis spaeter, Dad! #line:1fd6fb
Dad: Nacht, meine Damen! #line:f29948
<<close>>
<<irisOut 1 wait>>
<<set $dad_ham_panther_costume to 0>>
<<scene BeaCar>>
===
title: BakeyShakey
tags: 
colorID: 3
position: 2226,744
---
Mae: Hmm, soll ich mir einen Shakey Bakey holen? Das passt gut zu FLEISCH. #line:d51dba
-> Nein #line:730fac
-> Ja #line:24851a
    <<set $grocery_bakey_shakey to 1>>
===
title: Meat
tags: 
colorID: 3
position: 1562,149
---
<<shuffleNextOptions>>
-> Rinderhackfleisch #line:1894bb
    <<set $grocery_meat to 1>>
    Dad: Klassiker! #line:1a8749
-> Haehnchen #line:04a91c
    <<set $grocery_meat to 2>>
    Dad: Da kann man nichts falsch machen! #line:870377
-> Einen deiner besten Fische bitte! #line:9a54e6
    <<set $grocery_meat to 3>>
    Bea: Was immer gut ist. Abendessen fuer drei Personen. Nicht zu teuer. #line:01377d
    Dad: Geht klar! #line:048de0
-> Schweinekoteletts #line:8a650b
    <<set $grocery_meat to 4>>
    Dad: Wir schaffen das! Wir schaffen das! #line:e5e4a4
Mae: Hmm... soll ich bei diesen Fleischsorten den Abzug druecken? #line:2a0093
-> Nein #line:202380
    Mae: Warte, Dad... Ich habe meine Meinung geaendert. #line:e1f3ac
    [[Meat]]
-> Ja #line:c63c62
    [[AfterMeat]]
===
title: AfterMeat
tags: 
colorID: 3
position: 1896,293
---
Dad: Bea, du solltest mal zum Abendessen vorbeikommen! #line:2adcea
Mae: Im ''Ham Panther''? #line:947f21
Dad: Zu Hause, Mae. #line:853382
Dad: Ich werde uns etwas Schoenes grillen! #line:f4db0e
Dad: Wir koennen hier hinten so ziemlich alles zerhacken! #line:7648fb
Bea: Das waere sehr nett. Danke schoen. #line:132b9f
Mae: Ja! #line:458233
Mae: Ok lasst uns unser Fleisch holen! #line:5afa32
Dad: Komme sofort! #line:10b6bf
<<lookAt Mae Bea>>
Mae: In Ordnung! Abendessen beschlossen! #line:973b42
Bea: Ich schaue mir nicht mal an, was du hast. #line:ada333
Bea: So muede bin ich gerade. #line:e34641
Mae: Es wird grossartig werden! #line:3faaf0
Bea: Alles klar, lass uns weitergehen. #line:c05d42
[[EndGroceries]]
===
title: VerifyCannedGoods1
tags: 
colorID: 3
position: -98,655
---
Mae: Succotash... Thuccotath. #line:a0ded5
[[Confirm1]]
===
title: Confirm2
tags: 
colorID: 3
position: 762,1021
---
-> Nein #line:580a58
-> Ja #line:fd473e
    Mae: mmmjam. koestlich. #line:3b2d6d
    <<confirmGroceryChoice>>
===
title: Confirm3
tags: 
colorID: 3
position: 1740,1012
---
-> Nein #line:3cfa5b
-> Ja #line:5ff4e0
    Mae: ooh la la. #line:1e59e3
    <<confirmGroceryChoice>>
===
title: VerifyBakeyShakey
tags: 
colorID: 3
position: 1483,1554
---
Mae: Hmm, soll ich mir einen Shakey Bakey holen? Das passt gut zu FLEISCH. #line:83b538
-> Nein #line:379c
-> Ja #line:30dcb7
    Mae: mmmjam. lecker. #line:b8fddc
    <<confirmGroceryChoice>>
===
title: VerifyCannedGoods2
tags: 
colorID: 3
position: 123,656
---
Mae: Gruene Boehnchen #line:e0084e
[[Confirm1]]
===
title: VerifyCannedGoods3
tags: 
colorID: 3
position: 361,656
---
Mae: Huehnercreme-Suppe #line:cf6716
[[Confirm1]]
===
title: Confirm1
tags: 
colorID: 3
position: 76,936
---
-> Nein #line:eeadb2
-> Ja #line:b8df7f
    Mae: Lecker. #line:e8fc45
    <<confirmGroceryChoice>>
===
title: VerifyBox1
tags: 
colorID: 3
position: 590,654
---
Mae: Der gute alte Mac n' Cheese #line:9a497c
[[Confirm2]]
===
title: VerifyBox2
tags: 
colorID: 3
position: 814,653
---
Mae: Mae: Kartoffeln! #line:a8d956
[[Confirm2]]
===
title: VerifyBox3
tags: 
colorID: 3
position: 1036,649
---
Mae: Kartoffelpueree #line:8dcf34
[[Confirm2]]
===
title: VerifyBox4
tags: 
colorID: 3
position: 1277,647
---
Mae: Reis ohne Scheiss #line:57cd92
[[Confirm2]]
===
title: VerifyCooler1
tags: 
colorID: 3
position: 1590,666
---
Mae: Hmm... Kekse #line:9a9863
[[Confirm3]]
===
title: VerifyCooler2
tags: 
colorID: 3
position: 1886,672
---
Mae: Rahmspinat...? #line:ac9a86
[[Confirm3]]
===
