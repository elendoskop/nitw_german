title: Monument.1
tags:
colorID: 0
position: 27126,-6230
---
Mae:{locator=monument}Ich glaube, ich habe einen Gross-Grossonkel oder so etwas hier drauf. #line:58b61d
Mae:{locator=monument}Ja, da haben wir's. Anselm Borowski. #line:a3079f
<<wait .25>>
Mae:{locator=monument}"Anselm?" #line:38a5cc
Mae:{locator=monument}Selbst im Leben war er tragisch. #line:00c67e
===
title: Monument.2
tags:
colorID: 0
position: 27366,-6209
---
Mae:{locator=monument}Ein Opossum obendrauf, natuerlich. Immer, immer ein Opossum. #line:9be2ef
===
title: Monument.3
tags:
colorID: 0
position: 27652,-6155
---
Mae:{locator=monument}Wir moegen unsere Kriegsdenkmaeler in Possum Springs. #line:09feae
Mae:{locator=monument}Es gibt 2 wirklich alte drueben in Olde Possum Springs. #line:90c430
Mae:{locator=monument}Drei in der Stadt selbst. #line:bb922b
Mae:{locator=monument}Und ich glaube, sie haben einen neuen auf dem Pill Hill aufgestellt. #line:4446dd
Mae:{locator=monument}Es gibt einen oben bei Possum Jump, aber der ist gruselig. #line:29938e
Mae:{locator=monument}Ich war nicht mehr dort oben seit der Party, an die wir nicht mehr denken wollen. #line:d8270b
<<end>>
===
title: VeryTopAct2
tags:
colorID: 3
position: 26072,-7221
---
Mae: Vor Jahren dachte ich, als ich 20 war, dass ich nie mehr so hoch hinaufklettern koennte. #line:60d1cf
Mae: Das zeigt, was du weisst, kleine Mae. #line:af0110
Mae: ... #line:3b5e17
<<unlockDoodle RoofcatIsCorrect>>
<<end>>
===
title: Windmill
tags: demo2
colorID: 0
position: 27868,-6471
---
Mae: Ich glaube, das sollen Wetterstationen sein? #line:d433ba
Mae: Pionierpfadfinder haben sie gemacht, wenn sie nicht gerade Football spielten #line:23c9f1
Mae: oder ueber [wave]MANHOOD[/wave] lernten. #line:70c096
Mae: Das sind mit Sicherheit die maennlichsten Miniaturwindmuehlen. #line:1107b1
<<set $windmill_done to 1>>
===
title: BlockedEast
tags:
colorID: 0
position: 27195,-6480
---
<<if $act is 0 or $demo2>>
    [[BlockedEast_Demo2]]
<<else>>
    Mae: Mein Gott! #line:adb5ee
    Mae: Was fuer ein Wetter habe ich verpasst? #line:058d85
    Mae: Das ist verrueckt. #line:d006bb
    Mae: Na ja, ich wollte sowieso nicht in die Kirche gehen. #line:232c49
    <<unlockDoodle FallingApart>>
<<endif>>
===
title: InitLevel
tags:
colorID: 0
position: 28717,-8564
---
<<setCanInteract Donna 0>>

<<if $act is 0 and $day is 0>>
	<<show Donna>>
	<<show Gregg>>
    Gregg: =_= #line:2c6d68
	<<trigger Gregg Sit ShowGnome>>
	<<warp Gregg GreggRoof Sit>>
    <<setPlayerInteractLocator Gregg GreggRoofTalk>>
    <<setCanInteract Donna 1>>
    <<hide Lori>>
    <<hide TZEmployee1>>
    <<hide TZEmployee2>>
    <<show Bruce>>
<<else>>
    <<hide Gregg>>
    <<hide Bruce>>
<<endif>>

<<if $act is 1 and $day is 2>>
    <<show PoleWorker>>
<<else>>
    <<hide PoleWorker>>
<<endif>>

<<if $act is 1 and $day is 3>>
    <<show PoleWorker>>
<<else>>
    <<hide PoleWorker>>
<<endif>>

<<if $act is 2 and $day is 1 and $demo2>>
    //<<sit Bea>>
<<endif>>

<<if $act is 2 and $day is 4>>
    <<show Molly>>
<<else>>
    <<hide Molly>>
<<endif>>

<<if $act is 2 and $day is 4 and $towne_centre_east_city_council_a2d4_done is 0>>
    <<show Council1>>
    <<show Council2>>
    <<show Council3>>
    <<show Council4>>

    <<setSpriteLayer Council1 CityCouncil1FG>>
    <<setSpriteLayer Council2 CityCouncil2FG>>
    <<setSpriteLayer Council3 CityCouncil3FG>>
    <<setSpriteLayer Council4 CityCouncil4FG>>
<<else>>
    <<hide Council1>>
    <<hide Council2>>
    <<hide Council3>>
    <<hide Council4>>
<<endif>>

<<set $taxi_stop to 1>>

<<if $demo2>>
    <<show Bea>>
<<else>>
    <<hide Bea>>
<<endif>>

<<if $act is 3 and $day >= 2 and $did_birdland_quest is 0>>
    <<show Saleem>>
<<else>>
    <<hide Saleem>>
<<endif>>

<<if $act is 4 and $day is 3 and $janitor_done is 0>>
    <<show Janitor>>
<<else>>
    <<hide Janitor>>
<<endif>>

<<hide Donna>>
<<hide TZEmployee2>>
<<hide TZEmployee1>>

<<if $act is 1 and $day is 1>>
    <<show Donna>>
    <<show TZEmployee2>>
<<elseif $act is 1 and $day is 2>>
    <<show TZEmployee2>>
<<elseif $act is 1 and $day is 3>>
    <<show Donna>>
    <<show TZEmployee1>>
    <<show TZEmployee2>>
<<elseif $act is 2 and $day is 1>>
    <<show Donna>>
    <<show TZEmployee2>>
<<elseif $act is 2 and $day is 2>>
    <<show TZEmployee1>>
<<elseif $act is 2 and $day is 3>>
    // no one outside
<<elseif $act is 2 and $day is 4>>
    <<show TZEmployee1>>
    <<show TZEmployee2>>
<<elseif $act is 3 and $day is 1>>
    <<show Donna>>
    <<show TZEmployee1>>
    <<show TZEmployee2>>
<<elseif $act is 3 and $day is 2>>
    <<show TZEmployee1>>
    <<show TZEmployee2>>
<<elseif $act is 3 and $day is 3>>
    <<show TZEmployee2>>
<<elseif $act is 3 and $day is 4>>
    // no one outside
<<elseif $act is 4 and $day is 3>>
    <<show Donna>>
    <<show TZEmployee1>>
    <<show TZEmployee2>>
<<endif>>
===
title: Travis
tags:
colorID: 3
position: 27227,-6772
---
<<if $act is 2 and $day is 1>>
	Travis: Du bist scheisse, Mae. #line:f484d3
	Mae: ... #line:db3f7c
	Travis: Ja, du hast mich gehoert. #line:f51788
	Mae: ...... #line:f86705
	Travis: Du bist scheisse. #line:15d320
	Mae: Du bist das SCHLECHTeste. #line:d67bdd
	<<set $travis to 1>>
<<elseif $act is 2 and $day is 2>>
	<<if $travis < 20>>
		Travis: Mein Auto wurde zu Schrott gefahren. #line:7ad657
		Mae: Vielleicht wenn du nicht so ein Idiot waerst... #line:46a076
		Travis: Warte was? Wo kommt das denn her? #line:9bf366
		<<set $travis to 20>>
	<<elseif $travis is 20>>
		Mae: Vielleicht war es... carma. #line:1d96cd
		Travis: Wie Karma? #line:f328e7
		Mae: N-nein... das tust du nicht. Oh. #line:53af8f
		<<set $travis to 21>>
	<<elseif $travis is 21>>
		Travis: Schoen. #line:af7283
	<<endif>>
<<endif>>

===
title: CautionSign
tags:
colorID: 0
position: 27818,-6778
---
Mae: VORSICHT: Nicht auf Strassenlaternen springen. #line:c44b09
Mae: Nicht auf Stromleitungen laufen. #line:8ec0c5
Mae: Springe nicht zwischen Daechern. #line:fc58ae
Mae: Du koenntest sterben. #line:84b1a7
Mae: Dankeschoen! #line:8f21e1
===
title: Gregg_A0D0
tags: demo
colorID: 0
position: 28594,-6813
---
<<camera lookOffset 0 0>>
<<camera lookZoom -1>>
[[GreggRoof]]
===
title: GreggRoof
tags: demo
colorID: 0
position: 28955,-6475
---
Mae: Also... aehm... was ist passiert? #line:cf3f5f
Gregg: Habe mich in den Aufzug geschlichen #line:a602db
Gregg: aber als ich hier rauskam, schloss sich das Fenster hinter mir. #line:6c7039
Mae: Warum wolltest du ueberhaupt hier raufkommen, Kumpel? #line:32f63a
Mae: Du bist nicht gerade dafuer gemacht, auf Daechern herumzuspringen. #line:d0c5a3
Gregg: Ich schaetze, ich bin heute einfach nicht in Stimmung #line:f6b77b
Gregg: also dachte ich, ich werfe etwas vom Dach. #line:4e889d
<<wait .25>>
Gregg: Ach ja, ich habe auch einen Gartenzwerg gestohlen. #line:ffa80d
Mae: Hast du gerade einen deiner freien Tage? #line:7d3522
Gregg: Ich denke schon. #line:cfb37f
<<close>>
<<wait .5>>
Mae: Ich habe vorhin daran gedacht, was passieren wuerde #line:f93fd2
Mae: wenn ich mich in meinem Schlafzimmer lebendig verbrennen wuerde. #line:ad2144
Gregg: ok. :| o_o #line:9d04d5
Mae: Ich habe gedacht, dass ich hier fuer immer spuken wuerde. #line:fb1532
Mae: Nur um alle zu veraergern. #line:53a293
Gregg: Oh Mann, ich wuerde allen in den Arsch beissen. #line:b10785
Gregg: Sie wuerden sagen, was zum Teufel, wer hat mir gerade in den Arsch gebissen? #line:62f831
Gregg: [shake=.04]RAWR ASSBITE!!!!![/shake] D: #line:dc657f
Gregg: :| #line:639e94
Mae: Ich habe darueber nachgedacht, fuer immer hier zu sein. #line:aaa099
Mae: Oder fuer immer irgendwo zu sein. #line:22d2ac
Mae: Wie ein Dracula. #line:2a4aa0
Gregg: Wenn ich an einem Ort fuer immer spuken wuerde, weiss ich nicht, ob es Possum Springs waere. #line:210c97
Gregg: Diese Stadt ist scheisse. Diese Stadt ist ein Dracula. #line:c35e37
Gregg: Ich sag dir was: #line:f4d0c9
Gregg: Du gehst runter, faehrst mit dem Aufzug wieder hoch und laesst mich wieder rein. #line:9dfb5e
Gregg: Dann koennen wir Beas Auto ausleihen und zu Donut Wolf fahren. #line:511df5
Mae: Ich weiss nicht... Bea und ich sind nicht wirklich... #line:d5e3ff
Gregg: Nein, ist schon okay, Angus und ich leihen es uns staendig. #line:28b487
[[GreggRoofGnomeDecision]]

===
title: GreggRoofGnomeDecision
tags: demo
colorID: 0
position: 29314,-6420
---
Mae: Toller Kerl. Wolltest du diesen Zwerg werfen? #line:9c86c6
Gregg: Ich weiss es nicht. Was denkst du denn? #line:20404a
[[Nein, Alter, der Zwerg feiert heute Abend mit uns, |GreggRoofNoToss]] #line:3f0569
[[Offensichtlich. Das ist nicht mal eine Frage.|GreggRoofToss]] #line:290447

===
title: GreggRoofToss
tags: demo
colorID: 0
position: 29643,-6469
---
<<close>>
<<musicFadeOut 4>>
<<mood Gregg Neutral>>
<<trigger Gregg ThrowGnome>>
<<wait 3.9>>
<<trigger Gregg HideGnome>>
<<playSound CarSounds GnomeHit>>
<<shake 0.05 0.05 2>>
<<wait .5>>
<<playSound CarSounds Alarm loop>>
<<wait 1.5>>
Gregg: Ja, das war's. Ich fuehle mich besser. #line:766ba4
Mae: Guter Kerl. #line:00c75b
Gregg: Koennen wir jetzt von diesem Dach runtergehen? #line:7d296e
Mae: Ich werde dich das nie vergessen lassen. #line:bffd2f
Gregg: Hey Arschgesicht, willst du Donut Wolf oder nicht? -_- #line:c18e98
Mae: {width=4}[shake=.06]AWWOOOOOOOOOOOO!!!![/shake] \o/ #line:0a79d8
Gregg: [shake=.04]RIDE THE CHARIOT![/shake] D: \o/ #line:fe600d
Mae: [shake=.04]Zur Donut-Hoelle!!!![/shake] \o/ :D #line:a9de67
<<fadeOutSounds CarSounds 4>>
<<set $gnome_tossed to 1>>
<<musicFadeOut 2>>
<<fadeOut 2 wait>>
<<sectionTitle LaterAtTheDonutWolf PawsModeDonutWolf01>>

===
title: GreggRoofNoToss
tags: demo
colorID: 0
position: 29464,-6126
---
Gregg: Du hast Glueck gehabt, Gnom. #line:162b13
Mae: Hoer auf zu reden. Donut Wolf. #line:5946bb
Gregg: Jetzt geh schon! #line:ab22d7
Mae: {width=4}[shake=.06]AWWOOOOOOOOOOOO!!!![/shake] \o/ :) #line:4d60a3
<<set $gnome_tossed to 0>>
<<musicFadeOut 2>>
<<fadeOut 2 wait>>
<<sectionTitle LaterAtTheDonutWolf PawsModeDonutWolf01>>
===
title: TZEmployeeAuto
tags:
colorID: 0
position: 28086,-6227
---
<<set $tz_employees_done to 1>>
<<if $act is 1 and $day is 1>>
    [[TZAutoA1D1]]
<<elseif $act is 1 and $day is 2>>
    // doing nothing
<<elseif $act is 1 and $day is 3>>
    [[TZAutoA1D3]]
<<elseif $act is 2 and $day is 1>>
    [[TZAutoA2D1]]
<<elseif $act is 2 and $day is 2>>
    // doing nothing
<<elseif $act is 2 and $day is 3>>
    // no one outside
<<elseif $act is 2 and $day is 4>>
    [[TZEmployeeEmoticonLoop2]]
<<elseif $act is 3 and $day is 1>>
    [[TZAutoA3D1]]
<<elseif $act is 3 and $day is 2>>
    [[TZEmployeeEmoticonLoop2]]
<<elseif $act is 3 and $day is 4>>
    // no one outside
<<elseif $act is 4 and $day is 3>>
    [[TZAutoA4D3]]
<<endif>>
===
title: TZEmployeeEmoticonLoop
tags: demo2
colorID: 0
position: 28539,-6275
---
TZEmployee2:{emote=coffee} #line:eed221
Donna:{emote=Zigarette} #line:8091f7
[[TZEmployeeEmoticonLoop]]
===
title: PoleWorker_00_00
tags:
colorID: 2
position: 27843,-7167
---
Mae: Was machst du da oben? #line:f813b5
PoleWorker: die Strassenlaterne wurde bei dem grossen Sturm vor ein paar Tagen weggerissen #line:000cfb
PoleWorker: hilft nicht, dass die Idioten immer draufspringen #line:fe6afe
Mae: Das ist eine Schande. #line:6152d7
PoleWorker: erzaehl's den anderen Kindern #line:b25b45
Mae: Erwachsene. #line:98ba19
PoleWorker: Oh ho ho, das glaube ich nicht. #line:da110d
PoleWorker: Erwachsene haben sowieso Arbeit und was Besseres zu tun. #line:6c4f35
Mae: Ah. #line:a4f4be
PoleWorker: Wie auch immer, sagt den anderen Kindern, sie sollen sich zurueckhalten. #line:84f644
PoleWorker: Sie werden sich selbst oder jemand anderen umbringen! #line:a91201
===
title: PoleWorker
tags:
colorID: 2
position: 28007,-7476
---
<<if $pole_worker_sequence is 0>>
    <<if $pole_worker is 0>>
        <<set $pole_worker += 1>>
        [[PoleWorker_00_00]]
    <<elseif $pole_worker is 1>>
        <<set $pole_worker += 1>>
        <<set $pole_worker_done to 1>>
        [[PoleWorker_00_01]]
    <<endif>>
<<endif>>
===
title: PoleWorker_00_01
tags:
colorID: 2
position: 28223,-7187
---
Mae: Weisst du, wann sie die Stufen reparieren? #line:3e2570
Mae: Ich muss dringend in die Kirche gehen. #line:32ca45
PoleWorker: Morgen, denke ich. #line:a59986
Mae: Die Vampire koennen nicht auf heiligen Boden gehen. #line:78a49e
PoleWorker: Ich habe hier noch keine Vampire gesehen. #line:d6f386
PoleWorker: 'cept maybe the government #line:e0ae5c
PoleWorker: Sie saugen uns aus! #line:af7334
Mae: Du hast meine Vampirgeschichte uebernommen. #line:ba3a68
Mae: Ich gehe jetzt. #line:841cfd
PoleWorker: Nimm dich vor den Blutsaugern in Acht, Junge. #line:269930
Mae: ADULT!!!! #line:fa4dd6
===
title: SeeGregg
tags:
colorID: 0
position: 29095,-6966
---
<<camera extraLookOffset 0 2>>
<<alwaysLookAt Mae Gregg>>
<<wait .25>>
Mae: o/ #line:d67925
<<lookAt Gregg Mae>>
Mae: Hey Gregg! #line:f10a55
Mae: _o_ #line:a16998
Gregg: {medianLocator,align=middle}Hey Maedae #line:d1eba7
Mae: Was machst du hier oben, Kumpel? #line:653e87
Gregg: {medianLocator,align=middle}Habe mich ausgesperrt. #line:00c232
Mae: Ok, ich komme hoch. Bleib einfach dran. #line:0ca798
<<addToJournal GreggStuckOnRoof>>
<<unlockDoodle Gnome 2>>
<<camera extraLookOffset 0 0>>
===
title: Bruce
tags:
colorID: 0
position: 28794,-7126
---
<<if $bruce is 0>>
    Mae: Aehm... hallo. Geht es dir gut? #line:d103e5
    Bruce: ... #line:75e9bf
    Mae: Bist du von hier? #line:75fd93
    Bruce: Nein. #line:05fea8
    Mae: Dann kommst du aus dem Zug? #line:1d6966
    Bruce: Mir wurde schlecht, ich musste anhalten. Jeder weiss, dass man in Possum Springs nicht mehr anhaelt. #line:590a40
    Mae: Wegen unserer strengen Vagabundierpolitik? #line:d5d703
    Bruce: Manche halten den Atem an, wenn wir durch die Stadt rollen. #line:2a4989
    Mae: Aha? Und was machst du hier oben? #line:3b33ee
    Bruce: Schlafen. Ich halte mich von der Strasse fern. #line:6de505
    Mae: Kann nicht sehr warm sein. #line:a64b50
    Bruce: Im westlichen Teil der Stadt ist es viel kaelter. Am besten hoch und ostwaerts halten. #line:4ef33a
    Mae: Ok, dann werde ich mal gehen... #line:0a1c6f
    Bruce: In der Abenddaemmerung muss ich es zu den Gleisen schaffen und irgendetwas von hier weghuepfen. #line:5bd29d
    Bruce: Aber die Wahrheit ist, ich habe zu viel Angst, dieses Dach zu verlassen. #line:f83329
    Mae: Angst wovor? #line:16eb2d
    Bruce: "Steig nicht in Possum Springs aus." Ich wusste das. #line:836928
    Bruce: Ich bin hier ausgestiegen. Ich habe es vermasselt. #line:d7af93
    Mae: Wovor hast du Angst? #line:58c686
    Bruce: ... #line:3139c8
    Mae: Hey hoer mal, Alter, ich meine, wenn du hier oben rumhaengen willst, ist mir das egal. #line:3f378d
    Mae: Aber klau bloss nichts oder fress das Haustier von jemandem oder so. #line:78985f
    Mae: Ich meine, hey, wir alle haben harte Zeiten, und #line:fbafa2
    Bruce: Ich will nicht mehr mit dir reden. #line:93263a
    Bruce: Bitte geh weg. #line:6ab524
    <<set $bruce to 1>>
<<elseif $bruce is 1>>
    Bruce: ... #line:59f8c7
<<endif>>
===
title: Pee
tags:
colorID: 0
position: 28510,-7352
---
Mae: Ich dachte, er haette aufgehoert, diese Pinkel-Sache zu machen. #line:fdc5de
Mae: Irgendetwas muss ziemlich falsch sein. #line:2993d6
===
title: Teen1_Demo
tags: demo
colorID: 0
position: 27507,-7042
---
<<if $towne_centre_east_teen_1 is 0>>
	Mae: 'sup! #line:0d60a8
	<<set $towne_centre_east_teen_1 += 1>>
    <<set $towne_centre_east_teen_1_walk_away to 1>>
<<elseif $towne_centre_east_teen_1 is 1>>
	Mae: Ihr seid doch noch in der Highschool, oder? #line:bfe93f
	Teen3: Ja, wahrscheinlich. #line:e06f9e
	Mae: Mann, ich und Gregg haben den Laden mal richtig in Brand gesteckt :) #line:337b91
	Teen1: Ihr habt es aber nicht niedergebrannt. #line:d94eda
	Mae: :| #line:5fa79c
    Mae: Nein, aber ich meine... #line:d3f0d9
	Teen2: Also die Geschichte hier ist, dass du die Schule nicht niedergebrannt hast. #line:0f0c84
	Teen1: Das ist leicht zu machen. #line:33146d
	Teen3: Schaut uns an. Wir brennen auch nicht die Schule nieder. #line:3826c8
	Teen2: Aber man sieht uns nicht damit prahlen. #line:1560de
	Mae: Ich gebe nicht an! :( #line:4c8f45
	Teen1: Ach so, ok. Was wolltest du denn? #line:a2cd9b
	Mae: Nichts! Oje! #line:aca9fc
    Mae: :| #line:a57a6d
	<<unlockDoodle Teens 1>>
	<<addToJournal TeensAreTheWorst>>
	<<set $towne_centre_east_teen_1 += 1>>
<<elseif $towne_centre_east_teen_1 is 2>>
	Teen1: {emote=pentagramm} #line:418e2f
	<<set $towne_centre_east_teen_1 += 1>>
<<elseif $towne_centre_east_teen_1 is 3>>
	Teen1: {emote=Kreuzknochen} #line:13ef18
	<<set $towne_centre_east_teen_1 += 1>>
<<elseif $towne_centre_east_teen_1 is 4>>
	Teen1: {emote=Herzensbrecher} #line:3529c2
	<<set $towne_centre_east_teen_1 to 2>>
<<endif>>
===
title: Teen1
tags:
colorID: 0
position: 27515,-7345
---
<<if $act is 0 and $day is 0>>
    [[Teen1_Demo]]
<<endif>>
===
title: Teen1WalkAway
tags:
colorID: 0
position: 27117,-7105
---
Teen1: Yo, du kennst Gregg, richtig? #line:1c7cc0
Mae: Er ist mein bester Freund. #line:9e7b39
Teen2: [wave]Aw.[/wave] #line:de45fb
Teen3: Kumpel, halt die Klappe. #line:da1f27
Teen1: Ja ich glaube er steckt auf dem Dach fest. #line:1e0070
Mae: Warte was? #line:fa3c14
Teen2: Ja, ich glaube, er ist da hoch gegangen und hat sich ausgesperrt. #line:0c7801
Teen3: Er sieht so traurig aus. #line:cf886a
Teen1: Ich wollte meine Mutter anrufen, um ihr zu sagen, dass sie die Feuerwehr anrufen soll, aber... #line:cd700b
Mae: Warum ist er auf dem Dach? #line:586b85
Teen2: Pssh. Ich weiss es nicht, und ich bin nicht sein [color=EC4D67][wave]bester Freund[/all]. #line:7a3a1c
Teen3: Du bist so ein Arsch. #line:e2d758
<<addToJournal TodoGetGreggOffRoof>>
<<unlockDoodle FindGregg>>
<<set $towne_centre_east_teen_1_walk_away to 2>>
===
title: SteveScriggins
tags: demo
colorID: 0
position: 29798,-7336
---
<<if $steve_scriggins is 0>>
    SteveScriggins: {width=2} Huh. Wusste nicht, dass du [color=999999]zurueck bist.[/all] #line:9b4983
    [[Mensch, weiss es denn jeder?{width=1.5}|ScrigginsDoesEveryoneKnow]] #line:c3709f
    [[Kenne ich dich?{width=.7}|ScrigginsDoIKnowYou]] #line:5b4461
<<elseif $steve_scriggins is 1>>
    [[ScrigginsLoop]]
<<endif>>
===
title: ScrigginsLoop
tags: demo
colorID: 0
position: 30208,-7332
---
SteveScriggins: Got any copper? #line:ac432c
===
title: ScrigginsDoesEveryoneKnow
tags: demo
colorID: 0
position: 29706,-7040
---
SteveScriggins: Es ist keine Schande aufzugeben, Aufgeber. #line:1ce8e0 #(ツ)_/¯
Mae: Oh Gott, du bist Steve Scriggins. #line:7b4aaf
Mae: Ich wusste nicht, dass du noch in der Stadt bist. #line:8d7567
SteveScriggins: Ja, ich strippe Kupfer. #line:72cfbf
Mae: Ok #line:75a460
SteveScriggins: Wir haben jedes Wochenende ein grosses Lagerfeuer auf dem Feld hinter dem Haus meines Bruders #line:aa74d3
SteveScriggins: um den ganzen Bauschrott zu verbrennen, der nicht aus Kupfer ist. #line:87e1c3
Mae: Aehm #line:e926a4
SteveScriggins: Wegen dem ganzen Zeug, das da drin ist, brennt das Feuer 10 mal heisser. #line:e3d6c0
Mae: ... #line:4b4260
SteveScriggins: Du kannst nicht in seine Naehe kommen, ohne zu sterben! #line:c24eb4
SteveScriggins: Du solltest auch mal kommen! #line:0c3741
Mae: Nein. #line:8e6c2c
SteveScriggins: ok. #line:928d90
<<set $steve_scriggins to 1>>
<<addToJournal ScrigginsCopper>>
===
title: ScrigginsDoIKnowYou
tags: demo
colorID: 0
position: 30032,-7039
---
SteveScriggins: Steve Scriggins #line:d31df2
Mae: Ich... ich bin mir nicht sicher...	 #line:ba2173
SteveScriggins: Du erinnerst dich vielleicht an mein Werk, ich habe diesen [color=999999]Lehrer eingesperrt...[/color] #line:7f3722
Mae: ...in den Vorratsschrank... #line:358b61
SteveScriggins: IN THE [color=999999]SUPPLY CLOSET[/color] #line:3d7c06
Mae: ...vor der Weihnachtspause. Er war da drin fuer #line:5f4aa4
SteveScriggins: [wave]ZWEI WOCHEN![/wave] #line:4b4dda
Mae: Er hat spanische Arbeitsbuecher gegessen und Klebstoff getrunken, um zu ueberleben :\ #line:334180
SteveScriggins: [shake=.05]YEAH HE DID![/shake] #line:a1e8cf
Mae: :| #line:e6338a
<<addToJournal ScrigginsDumb>>
<<unlockDoodle Scriggins 1>>
<<set $steve_scriggins to 1>>
[[Warum hast du das getan?|ScrigginsWhyDidYouDoThat]] #line:270390
[[Ist er nicht daran gestorben?|ScrigginsDidntHeDie]] #line:09c670
===
title: ScrigginsWhyDidYouDoThat
tags: demo
colorID: 0
position: 29874,-6734
---
SteveScriggins: ¯\_(ツ)_/¯ #line:923790
SteveScriggins: Ich bin eine [color=999999][wave]Bedrohung![/all] #line:7b8553
===
title: ScrigginsDidntHeDie
tags: demo
colorID: 0
position: 30187,-6703
---
SteveScriggins: Noe, er ist in eine offene Kanalisation gelaufen! #line:94d268
Mae: Oh mein Gott, wirklich? #line:503d14
SteveScriggins: Ausgezeichneter Tod! #line:9cdd44
Mae: Wow, ja. Ausgezeichneter Tod. #line:51f03e
SteveScriggins: Inspirierend. Ich hoffe, ich sterbe auch so oder in einer Schiesserei mit den Cops. #line:f14ae3
Mae: Das sind beides sehr erreichbare Ziele. #line:f68a6e
SteveScriggins: ...[wave]EXCELLENT[/wave] [color=999999]Tod...[/color] #line:8fc11c
===
title: BlockedWest
tags:
colorID: 0
position: 26943,-6475
---
<<if $blocked_west is 0>>
    Mae: Ich habe letzte Woche dort drueben ein seltsames Opossum gesehen. Ich habe immer noch solche Angst vor dem Tod. #line:2a2fbe
    Mae: Ich koennte Angus bei der Arbeit besuchen oder in den Snack Falcon gehen, aber ich weiss nicht. #line:b418c8
    Mae: Da drueben sind sie. #line:a11f59
    Mae: Bei dem Opossum. #line:bf8562
    <<set $blocked_west += 1>>
<<else>>
    Mae: Gibt es etwas Deprimierenderes als die leerstehenden Ruinen der Partyscheune? #line:7207ed
    Mae: Ich bin zu deprimiert, um noch weiter zu gehen. #line:169976
<<endif>>
===
title: TriggerSeenEnough_A2D4
tags:
colorID: 3
position: 29042,-7354
---
Mae: Ach!!! alles ist so toll und harfig!!! #line:f51f56
Mae: was im Grunde nur ein weniger gruseliges Halloween ist? #line:f64867
Mae: wie auch immer! #line:d2be0f
Mae: ich will nur noch nach Hause und mich fuer heute Abend fertig machen!!!! #line:37ed5f
Mae: ich koennte das einfach tun... #line:0040bf
Mae: oder ich koennte ein bisschen herumlaufen... #line:411c3d
Mae: die harfige Luft einatmen... #line:3ae27b
Mae: nun, ich werde nach Hause kommen, wenn ich es tue. #line:854d93
<<set $harfest_prep to 1>>
===
title: TriggerCityCouncil_A2D4
tags:
colorID: 3
position: 29165,-7724
---
<<if $towne_centre_east_city_council_a2d4 is 0>>
    <<set $towne_centre_east_city_council_a2d4 to 1>>
    Council2: Das Riesenrad sieht wie ein Alptraum aus. #line:c018a9
    Council3: Danke, Colleen. Das ist sehr hilfreich. #line:63a036
    Council1: Was ist daran falsch? #line:65be21
    Council2: Ich traue riesigen tragenden Maschinen nicht, die man an einem Tag zusammenbauen und wieder auseinandernehmen kann. #line:85f499
    Council1: Es ist der Hit-Rekord! Wir hatten schon immer den Hit-Rekord auf dem Harfest! #line:fbf9bd
    Council4: Ja. Seit 1984. #line:60f551
    Council3: Vielleicht ist das ein Hinweis darauf, dass wir es ausmustern sollten. #line:e9a75f
    Council4: Haltet die Klappe ANDREW. #line:d961e0
    Council2: Ok, lasst uns alle einen Schritt zuruecktreten und uns abkuehlen. #line:a741dc
    Council1: WIR. SIND. GEHEN. TO. HABEN. THE. HIT. REKORD. BEI. HAR. FEST. TO. NIGHT!!! #line:4bcc2e
    Council2: Also gut. Ich werde alle Klagen zu euch schicken. #line:6bddef
    Council1: BRING ES COLLEEN. #line:183d43
<<endif>>
[[CityCouncilLoop_A2D4]]
===
title: CityCouncilLoop_A2D4
tags:
colorID: 3
position: 29498,-7740
---
Council1:{emote=spiral} #line:0c5c70
Council2:{emote=wolkenblitz} #line:54543c
Council3:{emote=Gewehr} #line:9399ab
Council4:{emote=Kasse} #line:ae651b
===
title: Window_Angry_A2D1
tags:
colorID: 3
position: 30609,-6253
---
note: diese werden abgespielt, ohne dass ihr vorruecken oder reagieren muesst #line:705e76
WindowAngry: HEY! GET DOWN OFFA THERE!!! #line:1aec5d
WindowAngry: Hey!!!! #line:97e1bd
===
title: Window_Angry_A2D3
tags:
colorID: 3
position: 30614,-6007
---
note: diese werden abgespielt, ohne dass Sie vorruecken oder antworten muessen #line:02cc74
WindowAngry: HEY! RUNTER VON DORT! #line:a8ee33
WindowAngry: HOeRST DU MICH???!! #line:0608aa
===
title: Window_Angry_A3D1
tags:
colorID: 4
position: 30618,-5752
---
note: diese werden abgespielt, ohne dass Sie vorruecken oder reagieren muessen #line:de17c9
WindowAngry: HEY! I'M GONNA CALL THE COPS!!! #line:7605ff
WindowAngry: HOeRST DU MIR ZU?!?!? #line:cb7419
===
title: Window_Angry_A3D3
tags:
colorID: 4
position: 30614,-5513
---
note: diese werden abgespielt, ohne dass Sie vorruecken oder antworten muessen #line:48b12e
WindowAngry: Du wirst dir das Genick brechen!!! #line:562e84
WindowAngry: Und ich werde keinen Finger ruehren, um zu helfen!!! #line:3cf919
WindowAngry: Hey!!!! #line:cf2abf
===
title: StairLanding
tags:
colorID: 3
position: 32249,-7362
---
Mae: Hm. Sieht aus, als koennte mich die Strassenlaterne halten... #line:ae462a
===
title: Monument_Act1.1
tags:
colorID: 2
position: 27076,-5851
---
Mae:{locator=monument}Ich glaube, ich habe einen Gross-Grossonkel oder so etwas hier drauf. #line:26c2d0
Mae:{locator=monument}Ja, da haben wir's. Anselm Borowski. #line:c8aa32
<<wait .25>>
Mae:{locator=monument}"Anselm?" #line:575515
Mae:{locator=monument}Selbst im Leben war er tragisch. #line:f637c0
===
title: Monument_Act1.2
tags:
colorID: 2
position: 27374,-5850
---
Mae:{locator=monument}Wir moegen unsere Kriegsdenkmaeler in Possum Springs. #line:d4b9bf
Mae:{locator=monument}Es gibt 2 wirklich alte drueben in Olde Possum Springs. #line:ec9cde
Mae:{locator=monument}Drei in der Stadt selbst. #line:371f22
Mae:{locator=monument}Und ich glaube, sie haben eine neue auf dem Pill Hill aufgestellt. #line:cbfba5
Mae:{locator=monument}Es gibt einen oben bei Possum Jump, aber der ist gruselig. #line:f55126
Mae:{locator=monument}Ich war seit der Party, an die wir nicht mehr denken wollen, nicht mehr dort oben. #line:95951d
<<end>>
===
title: Monument_Act2.2
tags: demo2
colorID: 3
position: 27375,-5580
---
<<if $did_gregg_friendship_quest_2>>
    Mae:{locator=monument}Ich meine, Gregg hat immer noch diesen Helm aus dem ersten Weltkrieg... #line:a619bc
    Mae:{locator=monument}Mit dem Einschussloch darin... #line:50cdde
<<else>>
    Mae:{locator=monument}Wie 10 Jungs aus meiner High School Klasse gingen zur Armee. #line:89cfa9
    Mae:{locator=monument}Muss mir das College irgendwie leisten. #line:bfc7b8
<<endif>>
<<end>>
===
title: Monument_Act2.1
tags: demo2
colorID: 3
position: 27067,-5579
---
Mae:{locator=monument}Ich war noch nie im Krieg. #line:561e36
Mae:{locator=monument}Nicht, dass der Anwerber es nicht versucht haette, aber. #line:a9820b
Mae:{locator=monument}Hart. #line:97b518

===
title: Monument_Act3.1
tags:
colorID: 4
position: 27068,-5298
---
Mae:{locator=monument}Krieg ist ein Scheissdreck. #line:0cd6eb
Mae:{locator=monument}Hoerst du mich, Statue? #line:55532a
Mae:{locator=monument}Ich schaetze, du wurdest eingezogen. #line:53a772
Mae:{locator=monument}lach. #line:59caaa
Mae:{locator=monument}Du bist gestorben, bevor du die Statue geniessen konntest, die sie von dir gemacht haben. #line:0a3d05
Mae:{locator=monument}Es ist Muell. #line:a0cb1a
===
title: Monument_Act3.2
tags:
colorID: 4
position: 27376,-5286
---
Mae:{locator=monument}sie koennen mich nicht zwingen, in den Krieg zu ziehen. #line:62aebc
Mae:{locator=monument}Ich werde in den Untergrund gehen. #line:2458f4
Mae:{locator=monument}Ich werde einer Gruppe beitreten, die gegen den Krieg ist und so. #line:c78ee8
Mae:{locator=monument}wir werden Kriegszeug in die Luft jagen und andere... Kriegszeug sabotieren. #line:e55462
Mae:{locator=monument}Ich werde fuer Jahrzehnte im Schatten leben. #line:a93a7f
Mae:{locator=monument}keine Adresse. #line:381607
Mae:{locator=monument}Stets auf der Flucht. #line:dc5f5f
Mae:{locator=monument}lebte in einem Wohnwagen in der Wueste. #line:ba0ffe
Mae:{locator=monument}das wird geil sein. #line:85886d
<<end>>
===
title: ReminderEastStairs
tags:
colorID: 3
position: 30551,-7965
---
Mae: Wow! Der Bau ist endlich fertig! #line:efb822
Mae: Die Treppe ist offen fuer Geschaefte! #line:ba8080
Mae: Ich frage mich, was da oben los ist? #line:b3488e
<<set $reminder_east_stairs_done to 1>>
===
title: TZAutoA1D1
tags: demo2
colorID: 0
position: 27870,-5671
---
Donna: Was? #line:eb1a51
TZEmployee2: Und wohin genau hast du vor zu gehen? #line:f5d94a
Donna: Ich weiss es nicht. Ham Panther? #line:79c5b7
TZEmployee2: Aber Schatz, du wirst dort nur ein Minimum bekommen. #line:1e6139
TZEmployee2: Und ich habe gehoert, die sind moerderisch... #line:c31a79
Donna: *Dieser* Ort ist moerderisch. #line:4e6c26
Donna: Ich glaube nicht, dass ich noch einen Anruf verkraften kann. #line:06228a
Donna: Ich wache manchmal auf, weil ich dieses *BING* Geraeusch hoere. #line:b88667
TZEmployee2: Heutzutage nehme ich einfach ein Schlafmittel. Das wirkt wahre Wunder. #line:d82dd4
Donna: Wir sollten uns nicht betaeuben muessen, um unseren Schlaf von diesem Job zurueckzubekommen! #line:9dab4c
TZEmployee2: Nun, Ham Panther wird dir keinen Bonus geben. #line:5f1001
TZEmployee2: Ham Panther hat nicht einmal eine Bestenliste. #line:337022
Donna: Aber es sind keine Aufrufe. #line:aa9f78
Donna: Es sind nicht die Verkaeufe. #line:409727
Donna: Ich wuerde nicht auf dieses *BING* warten. #line:558160
TZEmployee2: Ich weiss nicht, Schatz... #line:73b8de
TZEmployee2: Vielleicht hast du ja heute eine gute Schicht und kommst in die Bestenliste #line:97aa38
TZEmployee2: Und wir werden sehen, wie du dich dann fuehlst? #line:772dc2
Donna: Ich denke schon. Aber es wird nichts aendern. #line:7c1979
[[TZEmployeeEmoticonLoop]]
===
title: Bea_A2D1
tags: demo2
colorID: 3
position: 28966,-7979
---
<<if $bea is 0>>
    <<set $bea += 1>>
    [[Bea_A2D1_00]]
<<elseif $bea is 1>>
    <<set $bea += 1>>
    <<set $bea_done to 1>>
    [[Bea_A2D1_01]]
<<endif>>

===
title: Bea_A2D1_00
tags: demo2
colorID: 3
position: 29218,-8061
---
Mae: Hey Bea. #line:4d49e8
Bea: Yyyyo. #line:a932fc
Mae: Tut mir leid wegen gestern Abend. #line:7f76f5
Mae: Mir wird von Erkaeltungsmedikamenten schwummrig. #line:775889
Mae: Also ich weiss nicht wirklich, was ich erwartet habe, was passieren wuerde. #line:ae4daa
Bea: Tut mir leid, was ich gesagt habe. #line:f21747
Mae: Weisst du, ich erinnere mich nicht wirklich an viel. #line:3f81dc
Mae: Na ja, was soll's. #line:87517a
Bea: Ein Hoch auf den Alkohol. #line:79f993
Mae: Neues Stadtmotto. #line:794735

===
title: Bea_A2D1_01
tags: demo2
colorID: 3
position: 29491,-8052
---
Mae: Bandprobe spaeter? #line:c5f5bc
Bea: Ha ha. Yep. #line:6763a1
Mae: Ich bin erstaunt, dass es immer noch so ist. #line:db253f
Mae: Wir waren kaum eine Band in der Highschool. #line:f09f3b
Bea: Eh. Es ist lustig. Man kann mit Angus rumhaengen. #line:5f492f
Mae: Wie bist du ueberhaupt dazu gekommen, das zu tun? #line:7e3b21
Bea: Als Casey die Stadt verlassen hat, hat Angus mir erzaehlt, dass Gregg sehr traurig war. #line:b4bc92
Bea: Also habe ich mich freiwillig als neuer Schlagzeuger gemeldet. #line:b2ab24
Mae: Nun, "Schlagzeuger". #line:961a43
Mae: Das ist ein Laptop, Bea. #line:6f5e46
Bea: Das ist er, Mae. Das ist es wirklich. #line:b64069
===
title: NextToCat.1
tags: demo2
colorID: 0
position: 28040,-8201
---
Mae: Hey Katze. #line:60b9f0
Mae: Wie kommst du hier hoch? #line:a3b5e9
Mae: Du hast dir so viel Muehe gegeben, nur um zu schlafen? #line:380fbd
Mae: Ich habe selten mehr mit einem Tier zu tun gehabt. #line:ae3645
===
title: NextToCat.2
tags: demo2
colorID: 0
position: 28028,-7990
---
Mae: Hier, am Ende der Welt: #line:7523b8
Mae: eine schlafende Katze. #line:f059cf
===
title: NextToCat.3
tags: demo2
colorID: 0
position: 28004,-7779
---
Mae: Hey Katze. #line:c6211d
Mae: Als ich klein war, haette ich nie gedacht, dass ich mal hier hoch kommen wuerde. #line:d07089
Mae: Und du bist etwa halb so gross wie ich. #line:a54c4e
Mae: Du hast gewonnen. #line:070d48
<<set $next_to_cat_done to 1>>
<<unlockDoodle RoofcatIsCorrect>>
===
title: TopOfPartyBarn.1
tags: demo2
colorID: 0
position: 28301,-8179
---
Mae: Es braucht eine Menge Parteien, um eine Scheune offen zu halten. #line:f22571
Mae: Ich weiss nicht, wie sie ueberhaupt so lange ueberlebt hat, wie sie es tat. #line:4899f5
===
title: TopOfPartyBarn.2
tags: demo2
colorID: 0
position: 28299,-7952
---
Mae: Ich glaube nicht, dass Gregg die Erlaubnis bekommen hat, hier Bandproben abzuhalten. #line:da04bc
Mae: Wir werden so was von rausgeschmissen werden. #line:5c3822
Mae: Dann wird die Scheune wieder einmal still sein. #line:ee821c
<<set $top_of_party_barn_done to 1>>
===
title: BlockedEast_Demo2
tags: demo2
colorID: 0
position: 27458,-6445
---
Mae: Ich werde diese Treppe heute nicht hinaufsteigen. Ich muss nicht in die Kirche gehen. #line:6ba35b
Mae: Keine Goetter, keine Meister, keine verdammten Treppen heute bitte. #line:7e80c0
===
title: Saleem
tags: temp
colorID: 4
position: 31351,-7747
---
<<if $saleem_sequence is 0>>
	Mae: Hey! #line:0ac6cd
	Saleem: Hallo. #line:acae46
    Mae: Ich bin Mae! #line:0a3720
    Saleem: Hallo Mae, ich bin Saleem. #line:f8fbb2
	Mae: Bist du ein Strassenmusiker? Bist du Strassenmusiker? #line:25095e
	Saleem: Was ist Strassenmusik? #line:a333bc
	Mae: Ich weiss es nicht. #line:c0e82d
	Mae: Ich glaube, das ist, wenn du draussen spielst und die Leute dir Geld geben. #line:1624b0
	Saleem: Wirst du mir Geld geben? #line:7f9a7c
	Mae: Nein. #line:a6fccc
	Saleem: dann bin ich nicht der Strassenmusiker. #line:7745f8
	Mae: Cool. #line:a462d1
	<<set $saleem_sequence += 1>>
<<elseif $saleem_sequence is 1>>
	Mae: Immer noch hier oben? #line:259b36
	Saleem: Ich meine, ich bin gegangen und habe gearbeitet und so weiter, seit wir uns getroffen haben. #line:024ca0
	Mae: Hm #line:5cb2b8
	Mae: ist das eine Fiedel? #line:582055
    Saleem: Das ist eine Geige. #line:f931bf
    Mae: das ist doch das Gleiche, oder? #line:88c49e
    Saleem: es ist eine Geige, ich versichere es dir. #line:225e8c
	<<set $saleem_sequence += 1>>
<<elseif $saleem_sequence is 2>>
	Mae: Hat sich jemand beschwert? #line:71a96f
	Saleem: Worueber? #line:3e7428
	Mae: Du spielst. #line:b56369
	Saleem: Ich glaube nicht, dass es jemanden interessiert, ehrlich. #line:da4a5c
    Saleem: oder sie moegen es. #line:aad1cb
    Saleem: Mir ist es so oder so recht. #line:e6b741
	<<set $saleem_sequence += 1>>
<<endif>>

<<if $saleem_sadie_sequence is 0 and $sadie_sequence is 1>>
	Mae: Ich habe gerade jemand anderen getroffen, der alleine spielt #line:416e6d
	Saleem: Wirklich? #line:1f6fb3
	Mae: Ja. Auf der anderen Seite der Stadt, in dem verlassenen Gebaeude beim Clik Clak. #line:1f3f0d
    Mae: Sie hatten eines dieser grossen Saxophone. #line:19d860
	Saleem: Hm.  #line:1aa9c0
    Mae: Ich dachte nur, es sei ein interessanter Zufall. #line:671471
    Saleem: Kleine Stadt. #line:476700
	<<set $saleem_sadie_sequence += 1>>
<<elseif $saleem_sadie_sequence is 1 and $sadie_sequence is 2>>
	Mae: Erinnerst du dich an die Frau, die ich am anderen Ende der Stadt erwaehnt habe? #line:117b24
	Mae: Ich glaube, sie sucht Leute, mit denen sie spielen kann. #line:420897
	Saleem: wirklich? #line:8ace4f
	Mae: Ja. Ich meine ja nur. #line:f96b75
	Mae: Ich sagte ihr, ich wuerde es erwaehnen. #line:4696bd
	Saleem: Ich werde es im Hinterkopf behalten. #line:13db39
    Mae: Ich sage ja nicht, dass du mit ihr spielen *musst* oder so... #line:3049cc
    Saleem: Ich hab's verstanden. #line:b073b5
    Mae: Ich will das nicht seltsam machen, #line:98e27c
    Saleem: Es wird viel weniger seltsam sein, wenn du aufhoerst, es seltsam zu machen! #line:f41855
	<<set $will_complete_birdland_quest to 1>>
	<<set $saleem_sadie_sequence += 1>>
<<elseif $sadie_saleem_sequence is 1 and $saleem_sequence is 2>>
    Mae: Da ist also diese Frau am anderen Ende der Stadt. #line:21309d
    Mae: Sadie. #line:bbb1ef
    Saleem: ok? #line:ad12fb
    Mae: wie oben in dem verlassenen Gebaeude beim Clik Clak? #line:232c17
    Saleem: oooooook? #line:bfb92c
    Mae: jedenfalls spielt sie die ganze Zeit selbst Saxophon #line:a6edde
    Mae: vielleicht koennt ihr beide zusammen spielen? #line:db634f
    Mae: da du ja im Grunde dasselbe tust. #line:843f0a
    Saleem: oh. hm. #line:c0de3f
    Mae: Ich sagte ihr, ich wuerde es erwaehnen. #line:fcc3ce
	Saleem: Ich werde es im Hinterkopf behalten. #line:cd5995
    Mae: Ich sage ja nicht, dass du mit ihr spielen *musst* oder so... #line:48d710
    Saleem: Ich hab's verstanden. #line:74d466
    Mae: Ich will das nicht seltsam machen, #line:556364
    Saleem: Es wird viel weniger seltsam sein, wenn du aufhoerst, es seltsam zu machen! #line:d263ea
	<<set $will_complete_birdland_quest to 1>>
	<<set $sadie_saleem_sequence += 1>>
<<endif>>

<<set $saleem_done to 1>>
===
title: AngryWindowDude
tags:
colorID: 0
position: 27524,-7911
---
<<set $angry_window_dude to 1>>
<<animationTrigger WindowAngry GetAngry>>
<<wait .5>>
WindowAngry: HEY! Runter von dort! #line:5f6555
===
title: Janitor_A4D3
tags:
colorID: 6
position: 32770,-7571
---
Mae: Hey. Du bist es. #line:124470
<<wait 2>>
Janitor: jep jep. #line:e39af1
Janitor: Zumindest im Moment. #line:83c409
Mae: Weisst du, ich habe deinen Namen nie verstanden. #line:a1a920
Janitor: Ich bin der Hausmeister. #line:fc4c4f
Janitor: Ich raeume auf, mache Reparaturen. #line:64f0bc
Janitor: Was auch immer getan werden muss. #line:3fe85b
Mae: Oh. Ok. #line:2fdef8
Janitor: Wie ist es denn so, wieder zu Hause zu sein? #line:92e7ac
Janitor: Alles so, wie du es in Erinnerung hast? #line:e3929f
    ->Kinda. #line:777e51
    ->Nein. #line:634742
Mae: Ich weiss es nicht. #line:118998
Mae: Ich erinnere mich nicht einmal an heute. #line:e55685
Janitor: Klingt, als haettest du etwas durchgemacht. #line:587ad2
Mae: Woher wusstest du das? #line:71ead7
Janitor: Nennt es Erfahrung. #line:d8c6f3
Janitor: Ich schwoere, an manchen Morgen fuehle ich mich so alt wie diese Baeume. #line:4a5bd3
Mae: Weisst du, etwas Grosses ist passiert #line:9432cc
Mae: und ich habe das Gefuehl, ich haette heute aufwachen sollen #line:1e986c
Mae: und irgendwie etwas gelernt haben. #line:fdbecf
Mae: Aber ich weiss nicht, ob ich wirklich etwas gelernt habe. #line:c38b2a
Janitor: Weeeell, #line:81b541
Janitor: meiner Erfahrung nach lehren einen die grossen Dinge nichts #line:7881d4
Janitor: aber sie machen etwas aus dir #line:43dbbe
Janitor: und manchmal muss man eine Weile warten und sehen, was dabei herauskommt. #line:959955
Mae: huh. #line:59cb81
<<wait 2>>
Janitor: Nun, ich habe einen ehrlichen Arbeitstag hinter mir. #line:6a50a2
Janitor: Zeit zu gehen! #line:7a0d6b
Janitor: Smelters Spiel heute Abend! #line:c8a414
Mae: Man sieht sich. #line:cd3431
Janitor: Nicht, wenn ich dich zuerst sehe, Mae. #line:356aeb
Mae: ha ha #line:27078b
Janitor: *heeze* haw haw haw! #line:e73167
//janitor walks offscreen to the left//
<<close>>
<<walk Janitor JanitorOffLeft wait>>
Mae: warte! Woher kennst du meinen Namen? #line:0196c8
<<wait 2>>
//giant astral-god-looking bird flys up and into the sky in the bg//
//Mae: oh my god
//Mae: he was-
//Mae: finally some part of this makes sense.
//<<close>>
//<<walk Janitor JanitorBackOn wait>>
//janitor walks back onscreen interrupting//
//Janitor: HO-LEE JA-MOLEY
//Janitor: you see that big bird?
//Janitor: That’s a Great Nor’Easter King Crane!
//Janitor: Must be late to the migratin’.
//Janitor: Haw! Wait til the fellers at Miller’s hear about this!
//Mae: Ha ha ha ha.
//Mae: oh my god. This week.
//<<walk Janitor JanitorOffLeft wait>>
//Janitor: You said it.
<<hide Janitor>>
//janitor walks offscreen to the left//
<<set $janitor_done to 1>>
<<unlockDoodle Janitor1>>
===
title: FrontOfPartyBarnA2D2.1
tags:
colorID: 0
position: 30669,-7105
---
Mae: es sieht so traurig und leer aus, wenn wir nicht darin spielen. #line:4caedb
<<end>>
===
title: FrontOfPartyBarnA3D2.1
tags:
colorID: 0
position: 30664,-6860
---
Mae: ich kann nicht glauben, dass sie die Sachen der Band einfach da drin lassen #line:2cafac
Mae: was ist, wenn es jemand stiehlt? #line:f7395c
Mae: was ist wenn das Gebaeude einstuerzt? #line:f7cc05
Mae: ich sollte anfangen, Greggs Bass mit nach Hause zu nehmen. #line:bc3fe1
Mae: nur fuer den Fall... #line:7bc994
<<end>>
===
title: VeryTopAct3
tags:
colorID: 4
position: 26291,-7214
---
Mae: Hm. Ich habe hier oben manchmal andere Leute gesehen. #line:7d3403
Mae: gibt's das nicht mehr? #line:90446b
Mae: bin ich einfach alt und nicht mehr auf dem Laufenden? #line:dc268d
Mae: wie auch immer, es ist grossartig. #line:2f6f8d
Mae: ich werde ein Zelt aufstellen und hier oben zelten. #line:781c0c
<<wait 2>>
Mae: ... #line:72b571
Mae: wuerde definitiv wegrollen und sterben. #line:db87b9
<<unlockDoodle RoofcatIsCorrect>>
<<end>>
===
title: VeryTopAct4
tags:
colorID: 6
position: 26508,-7203
---
Mae: Wird nicht in der Lage sein, so schnell zu klettern. #line:a448fc
Mae: sobald es eisig wird. #line:5b6ebe
Mae: geniesse es, solange das Wetter haelt, mae. #line:70101d
<<unlockDoodle RoofcatIsCorrect>>
<<end>>
===
title: Molly_A2D4
tags:
colorID: 3
position: 28735,-8244
---
Mae: hey tante mall-cop. #line:2a7893
Molly: Einkaufszentrumspolizisten tragen keine Waffen, Mae. #line:c13098
Mae: Oje! #line:b3cb7d
Mae: ich schaetze, du brauchst die Waffe fuer... was machst du da? #line:61a686
Molly: die Strasse absperren. #line:4eb946
Mae: oh ja. das wuerde ich nicht ohne Waffe machen wollen. #line:61e5af
Molly: weisst du, wie oft ich schon auf jemanden geschossen habe? #line:0b4235
Mae: Nein. Sag es mir jetzt. #line:5dcd61
Molly: einmal.  #line:66b2fa
Mae: hast du sie getoetet? #line:0b253a
Molly: nein. #line:d597b8
Mae: du sagst also, du brauchst es nicht wirklich? #line:b5d18d
Mae: du solltest eine Streitaxt tragen. #line:ca4398
Mae: *Das* waere beeindruckend. #line:165e6d
Molly: ich werde es mir ansehen. #line:421366
<<set $molly_done to 1>>
===
title: TZAutoA1D3
tags:
colorID: 0
position: 28138,-5666
---
Donna: ...nun, auf das Brett zu steigen ist ein kalter Trost. #line:3c3fb7
Donna: ich bin einfach immer gereizt. #line:2a5830
TZEmployee1: gehst du vielleicht zu jemandem um darueber zu reden? #line:e3bfbc
TZEmployee2: Dr. Hank? #line:0bcbf7
Donna: ugh #line:453b2b
Donna: Dr. Hank macht alles und nichts. #line:e18d97
TZEmployee2: er hat letztes Jahr mein Bein repariert! #line:528fcd
TZEmployee1: er hat meine Babies gegen Windpocken und Streptokokken behandelt! #line:d7f2ea
Donna: ich weiss #line:a2ced8
Donna: ich habe nur... #line:808acc
Donna: was auch immer #line:3346be
[[TZEmployeeEmoticonLoop]]
===
title: TZAutoA2D1
tags:
colorID: 0
position: 28401,-5670
---
TZEmployee2: du wirst rausrennen und das in deiner *Pause* machen? #line:a58eb4
Donna: Pssst! #line:491246
Donna: [wave][size=.8]ich glaube ich werde Nancy sagen, dass ich krank bin[/all] #line:cbbfb2
Donna: [wave][size=.8]ich kann auf Kommando kotzen[/all] #line:22e3e4
TZEmployee2: [wave][size=.8]so kann man es auch machen[/all] #line:7e43c7
Donna: [wave][size=.8]ich habe Wechselkleidung im Auto[/all] #line:c607a6
TZEmployee2: [wave][size=.8]fuer den Fall, dass du auf deine normale Kleidung kotzen musst[/all] #line:1e9eac
Donna: [wave][size=.8]nein, ich will nur gut aussehen fuer das Vorstellungsgespraech[/all] #line:5893f6
TZEmployee2: [wave][size=.8]gute Idee![/all] #line:a2bb9e
Donna: [wave][size=.8]Ich meine, es ist Ham Panther. Das ist keine Kunstgalerie in Paris.[/all] #line:743a52
Donna: [wave][size=.8]aber ich denke jedes Bein ist gut![/all] #line:733c0d
TZEmployee2: [wave][size=.8]ich bin aufgeregt![/all] #line:858ae1
Donna: [wave][size=.8]Ich auch![/all] #line:b2dd82
Donna: [wave][size=.8]ich muss den ganzen Kaffee trinken, damit ich in 20 Minuten was zu kotzen habe[/all] #line:eac92b
[[TZEmployeeEmoticonLoop]]
===
title: TZAutoA3D1
tags:
colorID: 0
position: 28665,-5666
---
Donna: also heute ist mein letzter Tag! #line:216d76
TZEmployee2: oh je, wir werden dich vermissen! #line:996bfb
TZEmployee1: es ging so schnell! #line:91190b
TZEmployee2: lass uns wissen, wie es ist...? #line:b11aa6
Donna: Das werde ich! Ich werde dich besuchen kommen! #line:a631db
Donna: Ich kann euch zwei doch nicht allein lassen #line:c76275
TZEmployee2: oh je... was werden wir nur ohne dich tun? #line:c600d2
TZEmployee1: wollt ihr alle ins Kino gehen oder so? #line:c5c81f
Donna: Ja! Lasst mich erst mal einziehen #line:ed5c27
Donna: und wir koennen einen Ausflug machen! #line:3bdc54
Donna: und du wirst mich immer sehen, wenn du Lebensmittel einkaufst! #line:485d40
TZEmployee2: das ist wahr! #line:f7c7fc
Donna: Ich gehe nicht weit weg! #line:987d05
TZEmployee1: gar nicht so weit. #line:163393
[[TZEmployeeEmoticonLoop]]
===
title: TZAutoA4D3
tags:
colorID: 0
position: 28930,-5673
---
Donna: ...also werde ich mich aufteilen, in Teilzeit? #line:8e8286
TZEmployee1: wie koennen sie die Stunden nicht haben? #line:d7e8c5
TZEmployee2: es ist ein riesiger Laden! Wie koennen die keine Oeffnungszeiten haben? #line:e93dad
Donna: sie haben ein Computerprogramm #line:10fca4
Donna: es ist mit einer Art Grossrechner oder so verbunden #line:e11bb9
Donna: es spuckt den optimalen Zeitplan fuer alle Angestellten aus #line:1b7287
Donna: der Manager kann nicht viel dagegen tun #line:bc56fb
Donna: Computer spielt nicht mit Vorlieben #line:c22fee
TZEmployee2: ja, mit einem Computer kann man wohl nicht streiten. #line:920dae
TZEmployee1: hat Nancy dir die Hoelle heiss gemacht? #line:43cf15
Donna: Nein. Sie hat aber gelacht. #line:8ce172
Donna: ich habe auch gelacht. #line:0f9012
TZEmployee2: werden wir den Film immer noch machen? #line:9732e0
Donna: Ja. Lass uns einen Abend finden, an dem wir alle frei haben. #line:71afa1
TZEmployee1: ich habe dienstags frei! #line:73a2ce
TZEmployee2: ich habe Sonntags frei! #line:cdd913
Donna: ich habe nur donnerstags frei. #line:c903a9
<<wait 4>>
TZEmployee2: wir koennen alle Wuensche einreichen? #line:a4cd4f
[[TZEmployeeEmoticonLoop]]
===
title: TZEmployeeEmoticonLoop2
tags:
colorID: 0
position: 28339,-6468
---
TZEmployee2:{emote=coffee} #line:30e23a
TZEmployee1:{emote=Zigarette} #line:2102cc
[[TZEmployeeEmoticonLoop2]]
===


