title: EnterLevel_A1D1
tags:
colorID: 0
position: 115,-46
---
<<play HangingAtTheDinerMusic>>
Mae: Wann gehen wir denn mal aus? :) #line:e64ba3
Bea: *SCHNAUB* #line:170c91
Bea: Mae. Wir gehen nicht aus. #line:757099
Mae: Oh. :| #line:969d8f
Bea: Wir haben Jobs, Mae. #line:72ba4b
Angus: Ich arbeite auch im Video Outpost! :) #line:2ec9dd
<<mood Angus Neutral>>
Bea: Ich bin bei der 'Ol Pickaxe'. #line:b884b3
[[Ich dachte, du gehst zur Schule?|GoingToSchool]] #line:674beb
[[Ist das nicht der Laden deines Vaters?|DadStore]] #line:052809

===
title: 02_A1D1
tags:
colorID: 0
position: 157,1099
---
Mae: [speed=.25]...[/all] :\ #line:507b6a
<<close>>
<<wait 3>>
Angus: Tja! Wir gehen besser nach Hause, Mann. #line:1c4c2e
Gregg: Oh ja, Angus hat ein Date mit Schwertleuten online :) #line:f25929
Mae: Ah, Glueck gehabt :| #line:f58040
[[End_A1D1]]

===
title: GoingToSchool
tags:
colorID: 0
position: -96,210
---
Bea: Heh. #line:ac1d95
Bea: Ja, nun. Das Leben passiert. #line:5f2115
[[01_A1D1]]
===
title: DadStore
tags:
colorID: 0
position: 362,216
---
Bea: Sicher. #line:6bd277
[[01_A1D1]]
===
title: 01_A1D1
tags:
colorID: 0
position: 132,513
---
Gregg: Ich bin der Herr der Snack-Falken! \o/ D: #line:176c9c
Gregg: :| _o_ #line:2b6530
<<close>>
<<wait 2>>
Mae: Wo ist Casey? #line:a8647b
Angus: ... #line:1ce4c3
<<playMusic diner_conversation>>
Gregg: Oh ja, du weisst es nicht, hm? #line:2bdc0c
Gregg: Casey ist weg. #line:92efbd
Mae: "Weg"? Wie verschwunden? #line:1d6953
Gregg: Auf einen Zug aufgesprungen, wette ich. #line:12819e
Mae: Oh, wow. Er hat immer davon gesprochen, das zu tun. #line:63648f
Gregg: Ja, eines Tages war er einfach weg. #line:25ced7
Angus: Hat einen sauberen Schlussstrich gezogen. Hat nicht gemailt oder so. #line:157e2b
Mae: Nun, gut fuer ihn, denke ich. #line:8d9e90
Gregg: Seine Eltern haben Vermisstenplakate aufgehaengt #line:f326a8
Gregg: aber jeder wusste, was passiert ist. #line:69100f
Mae: Oje. Casey. Ich... hm. #line:f8a976
Bea: Mmhmm. #line:f2875e
Bea: Und was genau tust du, Mae? #line:1de68b
Mae: Ich war auf dem College? #line:1a87b5
Bea: Und warum genau bist du nicht mehr dort? -_- #line:c82812
[[Mann, scheiss auf die Schule!|ScrewSchool]] #line:fe08ac
[[Hat nicht geklappt.|DidntWorkOut]] #line:16136e

===
title: ScrewSchool
tags:
colorID: 0
position: -50,827
---
Bea: Atemberaubend... o_o #line:fa48f4
[[02_A1D1]]
===
title: DidntWorkOut
tags:
colorID: 0
position: 322,815
---
Bea: Huh. Stell dir das vor. o_o #line:03d1f3
[[02_A1D1]]
===
title: End_A1D1
tags:
colorID: 0
position: 177,1420
---
<<set $diner to 1>>
<<fadeOut 1>>
<<wait 1>>
<<scene TowneCentreWest>>

===
title: InitLevel
tags:
colorID: 0
position: 139,-374
---
<<if $act is 1 and $day is 1>>
<<elseif $act is 2 and $day is 1>>
    <<rotate Gregg 0>>
    <<hide Angus>>
    <<hide Bea>>
<<else>>
    <<hide Gregg>>
    <<hide Angus>>
    <<hide Bea>>
<<endif>>
===
title: EnterLevel_A2D1
tags:
colorID: 3
position: 816,-15
---
<<if $did_food_flicker>>
    [[AfterFoodFlicker]]
<<else>>
    [[BeforeFoodFlicker]]
<<endif>>
===
title: AfterFoodFlicker
tags: temp
colorID: 3
position: 925,312
---
Gregg: Ich glaube, ich koennte krank sein. #line:8f5b2c
Mae: Wahre Freundschaft: Sich von einem Freund krank machen zu lassen. #line:4cd424
Gregg: Wahre Brueder. #line:46d2fa
Mae: Wahre Manns. #line:c3bba5
Mae: Ich habe das Gefuehl, das ist das erste Mal, dass wir einfach zusammen abhaengen. #line:e418be
Gregg: Wie fuehlt es sich an? #line:a85293
    ->Ich meine, ich bin sehr wund und rieche nach Klebstoff. #line:649fea
        Gregg: Ja, ich werde eine Stunde lang duschen, wenn ich nach Hause komme. #line:d64e1c
    ->Wirklich gut. #line:1f7681
        Gregg: Dasselbe, Alter. #line:27b66e
Mae: Wie ist es denn so gelaufen? #line:e8f9ab
Gregg: Aeh... gut! #line:bd1c48
Mae: Seltsam, dich mit einem normalen Job zu sehen #line:76529a
Mae: den du magst #line:5bd0ae
Mae: geh zu. #line:9a8419
Gregg: Ja, ich arbeite an dem Plan. #line:de8ce4
    ->Der Plan? #line:535be6
    ->Ja, ich habe auch Plaene. #line:f9a33e
        Gregg: Was zum Beispiel? #line:62eefb
        Mae: Diesen Leimgeruch abwaschen. #line:333900
        Mae: Von welchem Plan hast du gesprochen? #line:4ceb74
Gregg: Bright Harbor. #line:194bb2
<<set $knows_bright_harbor to 1>>
Mae: Bright... harbor? #line:2ae623
Gregg: Ja. Wir werden nach Bright Harbor ziehen? #line:742542
    ->Wann? #line:9c17f9
    ->Seit wann? #line:57d1f4
        Gregg: Aeh... seit einem Jahr oder so? #line:29c4d0
        Gregg: Deshalb arbeiten wir auch 7 Tage die Woche. #line:d4729d
        Mae: Aber... wann zieht ihr denn um? #line:daf88b
Gregg: Ich glaube im Fruehling? #line:e3f582
Mae: Das ist noch... ein paar Monate hin. #line:611868
Gregg: Ja! #line:55bf08
Mae: Oh. #line:1d23a8
Gregg: Du solltest uns besuchen kommen, wenn wir uns eingelebt haben! #line:27933d
    ->Warum Bright Harbor? #line:19094e
    ->Ist Bright Harbor nicht sehr teuer? #line:d0109e
        Gregg: Nicht, wenn man in einer schrecklichen Nachbarschaft lebt! #line:e50912
Gregg: Oh Mae es ist SO cool. #line:cf3417
Gregg: Wir haben es im Sommer besucht #line:395094
Gregg: Da gibt es Plattenlaeden und schraege Bars #line:6a2f0a
Gregg: und alle sind cool und wie... frei? #line:fec4a4
Mae: Oh. #line:065855
Gregg: Dort habe ich mein Tattoo bekommen! #line:3f300e
Mae: Was ist das? #line:65e821
Gregg: Ja! #line:0a2cfd
//emoticon of sheep tattoo//
Gregg:{emote=sheep_tattoo} #line:abf3c7
<<close>>
Mae: Ist das ein... Schaf? #line:8256dc
Gregg: Ja. #line:96b6af
Mae: Warum ein Schaf? #line:836272
Gregg: ... #line:3bfad1
Gregg: Der Tattoo-Typ war so richtig heiss. #line:74cc2c
Gregg: Und es hat nicht mal weh getan! #line:04656e
Mae: Du hast ein Tattoo. #line:b32473
Mae: Du ziehst nach Bright Harbor. #line:c70ff4
Gregg: Ja! #line:b6e67e
Mae: Oh. #line:760f7a
// We need a variable to say I know about Bright Harbor and the tattoo so we don't repeat this later.
// ^ we can use $did_gregg_friendship_quest_1 to check this
Gregg: Was ist los? #line:21ee01
Mae: Nichts! #line:d2adb7
Gregg: Was glaubst du, wie lange du in der Stadt bleibst? #line:27a3ac
    -> Aehm. Ich weiss es nicht. #line:8c46c9
        Gregg: Ich werde dir helfen, etwas auszusuchen. #line:902d4d
    -> Nicht lange. Ich werde auch umziehen. #line:7e8dc3
        Gregg: Ooh wohin? #line:447df6
            ->Nord. #line:f2589a
                Gregg: Kaelte und Schneestuerme und heisser Kakao! #line:933aa2
                Gregg: Ooh, wir koennen im Sommer zu Besuch kommen! #line:9ff8a0
            ->Sued. #line:c69e05
                Gregg: Hitze und Wirbelstuerme und suesser Tee! #line:4ebf5c
                Gregg: Ooh, wir koennen im Winter zu Besuch kommen! #line:42c760
Mae: Ja. #line:ca737b
Gregg: Es war wirklich toll, heute Abend mit dir abzuhaengen. #line:f983d1
Gregg: Ich war in letzter Zeit wirklich langweilig. #line:8427cf
Gregg: Ich habe schon lange kein Verbrechen mehr begangen... #line:c052a9
Mae: Und warum? #line:f8bff0
Gregg: Angus. Der Plan. Ich kann nicht nach Bright Harbor gehen, wenn ich erwischt oder gefeuert werde. #line:34745b
Mae: Du bist jetzt ganz erwachsen. #line:46931e
Gregg: Ich bin bonafide. #line:da1ba3
Mae: Ich werde gehen, denke ich. #line:c4fe01
Gregg: Gute Entscheidung. Wir muessen die Verklebung loesen. #line:69f845
Mae: Ich glaube, wir haben das Diner vollgestunken. #line:f02432
Gregg: Eh. Waere ja nicht das erste Mal. #line:d57b18
Mae: =_= :) #line:930e20
Mae: Ich habe dich vermisst. #line:51102a
Gregg: :) #line:9efdb9
Gregg: Du auch, Bro. #line:6f2956
<<unlockDoodle GreggFQ1Crimes>>
<<unlockRobotHeadDoodle>>
<<close>>
<<set $gregg_friendship_quest to 0>>
<<set $did_gregg_friendship_quest_1 to 1>>
<<set $completed_gregg_friendship_quests += 1>>
<<set $night to 1>>
<<irisOut 1 wait>>
<<scene MaeHouse left>>
===
title: BeforeFoodFlicker
tags:
colorID: 3
position: 1212,244
---
Mae: Schade, dass du dir nicht das Rueckgrat gebrochen hast. #line:5cb2fa
Gregg: schade, dass deine Arme nicht abgerissen wurden. #line:a29a95
Mae: zu schade, dass du nicht entsetzlich entstellt wurdest. #line:5613b4
Gregg: zu schade, dass du nicht gestuerzt bist und dir die Zaehne an einer Treppe gebrochen hast. #line:70acbe
Mae: zu schade, dass du nicht unter der Kiste eingeklemmt wurdest #line:a2f9d3
Gregg: zu- #linen:ae229c
Mae: nicht fertig #line:125b5a
Mae: und dann bei lebendigem Leib in einem Feuer verbrannt. #line:a7906b
Gregg: schade, dass du nicht in einen Lueftungsschacht gekrochen bist #line:ad09cb
Gregg: um dem Feuer zu entkommen #line:eba147
Gregg: und gekocht zu werden. #line:6e9757
Mae: aaah. gute Zeiten. #line:c1da5f
Mae: Ich rieche immer noch gluuuuuuuuuuuuu. #line:4cd774
Gregg: Denk an etwas anderes. #line:9f4a1b
Mae: Ich kann nicht. Ich glaube, ich bin high von dem Kleber. #line:76f25a
Gregg: Denk an Pierogis. #line:0bfb26
Gregg: Gab es Pierogis, als du in der Schule warst? #line:33977f
Mae: Diese eine Kirche in der Stadt hat sie an jedem Feiertag verkauft. #line:53ef4f
Gregg: Das Fest der Ewigen Pierogi. #line:b8eef6
Mae: Yo Mann. #line:4276ff
Gregg: Was? #line:f0a45d
Mae: Mach deinen Mund auf. #line:4412c4
Gregg: Warum? #line:ba6483
Mae: Vertrau mir. #line:929b98
Gregg: Wahre Freundschaft ist das. #line:705360
<<irisOut 1 wait>>
<<scene FoodFlicker>>
===
