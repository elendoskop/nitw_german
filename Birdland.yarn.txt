title: Scriggins_A3D1_old
tags:
colorID: 0
position: -31,702
---
Mae: Oh Gott, was machst du denn hier? #line:f8d187
SteveScriggins: Eine Menge Kupfer in diesen Waenden. #line:623796
Mae: Was ist das ueberhaupt fuer ein Ort? #line:7143a7
SteveScriggins: Damals, als sie in Possum Springs das erste Mal Internet hatten #line:e01985
SteveScriggins: kam es von hier. #line:2ce4c0
Mae: Whoa. #line:5969ad
SteveScriggins: Nur noch Schrott. #line:f5ad41
===
title: Scriggins_A3D2_old
tags:
colorID: 0
position: 196,758
---
Mae: Und was bekommst du dafuer? #line:ae01ba
SteveScriggins: Bezahlt nach Gewicht. #line:c5f2a6
SteveScriggins: Ich habe einen Kerl, der es kauft. #line:ff2747
Mae: "Du hast einen Kerl?" #line:9f4d0a
===
title: Scriggins_A3D4_old
tags:
colorID: 0
position: 649,1066
---
Mae: Hey Schwachkopf. #line:18472f
SteveScriggins: Ich bin heute wirklich bedrohlich. #line:31166f
Mae: Ok, ich gehe jetzt wirklich. #line:4ed0a2
===
title: Scriggins_A3D3_old
tags:
colorID: 0
position: 394,909
---
Mae: Ich dachte, dein Bruder macht Abbrucharbeiten? #line:80bc95
SteveScriggins: Ja, er hat sein eigenes Team. #line:9206f7
Mae: Warum tust du das dann? #line:55d727
SteveScriggins: Er hat seinen Job, ich habe meinen. #line:2f8668
Mae: Das ist dein Job? #line:5a4891
SteveScriggins: Oh mein Gott, halt die Klappe. #line:500acf
===
title: Sadie_A3D3_old
tags:
colorID: 0
position: 844,689
---
Mae: Hier war mal was los, hm? #line:cc54db
Sadie: Ja, definitiv. Eine totale Sache. #line:a18520
Mae: Wow. #line:68cbe9
Sadie: Diese Band von Astral Mode, sie waren die Hausband #line:aae5cc
Sadie: vor einer sehr langen Zeit. #line:c14f07
Mae: Huh. #line:7cf63a
Sadie: Am Ende sind sie alle ein bisschen durchgedreht. #line:4436d6
Sadie: Der Laden wurde in den 80ern geschlossen. #line:ac4127
Sadie: Sie sprachen lange Zeit davon, es wieder zu eroeffnen #line:5139f0
Sadie: aber jetzt ist der Besitzer weg, wohin auch immer er gegangen ist #line:c79b14
Sadie: der ganze Laden steht einfach nur da #line:284a1b
Sadie: ist das groesste Vogelhaus in Possum Springs. #line:575e89
===
title: Sadie_A3D2_old
tags:
colorID: 0
position: 1075,628
---
Mae: Du spielst also (Instrument einfuegen)? #line:8dfd0a
Sadie: Ja, das tue ich. #line:23315a
Mae: Warum hier? #line:0affb6
Sadie: Ich komme hierher, seit ich 14 bin, ungefaehr in deinem Alter. #line:a41ab5
Mae: Ich bin 20!!!! #line:1e53bc
Sadie: Oh. Ok. #line:a168b5
Sadie: Dann (Lebenserfahrungen, die wie das Ende von allem schienen) #line:2e2a91
Sadie: und ich fing an, nach der Arbeit zurueck zu kommen. #line:2e7bce
Mae: Das ist deprimierend. #line:a7cd69
Sadie: Nein, ist es nicht. #line:068bb9
===
title: SteveScriggins
tags:
colorID: 4
position: -294,671
---
<<if $steve_scriggins_birdland_sequence is 0>>
    <<if $did_gregg_friendship_quest_1>>
    	Mae: Oh. Ich bin's, Steve. #line:3bd972
    	SteveScriggins: Was willst du denn? #line:09d567
    <<else>>
    	SteveScriggins: Was wollt ihr? #line:518685
    	Mae: Heiliger Bimbam. #line:3414e7
    	Mae: Steve? Steve Scriggins? #line:d02a86
    	SteveScriggins: Hey. Ich erinnere mich an dich. #line:4678df
    	Mae: Ja... aeh... genauso. #line:0ed40b
    <<endif>>
    Mae: Was machst du denn da? #line:1c3e52
    SteveScriggins: Ein Nebenjob. #line:741b46
    Mae: Was ist dein Hauptberuf? #line:d3f2e2
    SteveScriggins: Ich bin im Bautrupp von meinem Bruder. #line:a2ed6e
    Mae: Und was ist dein Nebenjob? #line:3e88ae
    SteveScriggins: Zeug auf dem Boden da drueben? #line:20bbe5
    SteveScriggins: das ist Kupfer. #line:574c67
    SteveScriggins: das Gebaeude ist voll davon #line:f96bac
    SteveScriggins: und niemand benutzt es #line:c65dda
    Mae: also bist du wie #line:1eca7e
    Mae: Rohre aus den Waenden reissen? #line:afee86
    SteveScriggins: auch Draehte. einige Draehte. #line:1532d7
    Mae: und...? #line:496c1a
    SteveScriggins: Mensch, du lernst ja schnell. #line:5cc7c9
    SteveScriggins: SIE VERKAUFEN ES. #line:fe5791
    Mae: das ist nicht wirklich ein Job. #line:d6dd2c
    SteveScriggins: zur Hoelle, das ist es nicht #line:47ee80
    SteveScriggins: muss es aus den Waenden rausholen #line:8a4a24
    SteveScriggins: muss manchmal die Rohre zersaegen #line:0de515
    Mae: ok. also, #line:43d0ec
    SteveScriggins: muss das Plastik von den Kabeln entfernen #line:dca1e0
    SteveScriggins: und sie nach Qualitaet in Stapel sortieren #line:73b33a
    SteveScriggins: denn nicht jeder wird etwas nehmen, das nicht Klasse A ist #line:b46e72
    Mae: nun, das ist alles sehr interessant #line:015bd6
    SteveScriggins: es gibt auch noch andere Sachen, die man an solchen Orten mitnehmen kann #line:2bccc2
    SteveScriggins: Tuerklinken und so #line:a83d65
    Mae: ich werde jetzt gehen, aber redet weiter #line:c5c922
    SteveScriggins: ... #line:65abad
    <<set $steve_scriggins_birdland_sequence += 1>>
<<elseif $steve_scriggins_birdland_sequence is 1>>
    Mae: Wie laeuft's mit deinem Nicht-Job? #line:5ef681
    SteveScriggins: Hast du ueberhaupt einen Job? #line:595f47
    SteveScriggins: wie jede Art von Job? #line:8d3a1f
    Mae: hoer zu, ich bin gerade zurueckgekommen, also #line:accbb3
    SteveScriggins: hast du *JEMALS* einen Job gehabt? #line:fdbdf6
        -> Ich habe eine Zeit lang Lebensmittel eingetuetet, als ich auf dem College war. #line:9d6d1c
            SteveScriggins: wie lange ist eine Weile? #line:537fce
            Mae: ... #line:545738
            Mae: 2 Monate? #line:b4711a
            SteveScriggins: *schnauf* #line:4f745c
            Mae: was? #line:027574
            SteveScriggins: was, war es zu schwer fuer dich? #line:b3b34e
            Mae: ugh. tschuess Steve. #line:aaaf08
        -> Eigentlich habe ich jetzt einen Job. Ich bin ein Manager. #line:34fb9d
            SteveScriggins: was managst du denn? #line:57f5e1
            Mae: Ich schaffe es, nicht zu kotzen, wenn ich dein Gesicht sehe! #line:829e36
            Mae: heh heh heh #line:7e3c0b
            SteveScriggins: lustig. #line:1948f7
            Mae: du bist da reingelaufen #line:5e614f
            SteveScriggins: sicher bin ich das. #line:a9c3f0
    <<set $steve_scriggins_birdland_sequence += 1>>
<<elseif $steve_scriggins_birdland_sequence is 2>>
    Mae: Hey Sturve. Sturve Skurgrns. #line:3d81e1
    SteveScriggins: mmmhmmm #line:32f83b
    Mae: Reisst du immer noch Scheisse aus den Waenden? #line:099804
    SteveScriggins: jep #line:a9cac3
    Mae: preeeeetty dumm #line:c24571
    SteveScriggins: Mensch Mann, halt die Klappe #line:02a7f2
    SteveScriggins: du hoerst *NIE* auf zu reden #line:32322b
    SteveScriggins: warum kommst du ueberhaupt hierher? #line:a88b18
    SteveScriggins: wir sind keine Freunde #line:bfacd4
    SteveScriggins: und warum redest du immer mit mir #line:d2970c
    SteveScriggins: als ob ich eine Art Arschloch waere? #line:7720ab
    Mae: Steve, du *IST* ein Arschloch. #line:964b48
    SteveScriggins: Was ist dein Problem mit mir? #line:5af2c3
    Mae: Damals in der Schule? Du warst so ein Tyrann! #line:f1a0b9
    Mae: Du hast den Leuten das Leben zur Hoelle gemacht! #line:9a752b
    Mae: und jetzt, Jahre spaeter, bist du was- #line:6cfe93
    Mae: Gebaeude fuer Schrott abreissen? #line:91905e
    Mae: wie, aktiv die Stadt zerstoeren? #line:0322d3
    SteveScriggins: ok, da kannst du gleich aufhoeren #line:1774e0
    SteveScriggins: du weisst, dass der einzige Grund, warum sich jemand in der Stadt an dich erinnert #line:abfefe
    SteveScriggins: ist, weil du fast ein Kind getoetet haettest, richtig? #line:27cc2b
    SteveScriggins: wie, sein Gesicht ist immer noch ein bisschen kaputt! #line:aad7c3
    SteveScriggins: ich habe nie *ALLES* so gemacht #line:8285f4
    SteveScriggins: und du und Gregg habt eine Menge Scheisse gebaut #line:937d65
    SteveScriggins: ueberall, nur so zum Spass #line:3ad5c7
    SteveScriggins: denkst du, ich wuerde das hier tun, wenn ich das Geld nicht braeuchte? #line:0b6fa0
        ->I- #line:901d9d
        ->uh #line:31f2d8
    SteveScriggins: du bist wie meine Nichte, ein grosses dummes Baby #line:1bbd58
    SteveScriggins: einfach herumlaufen und gegen Sachen stossen. #line:31d03b
    SteveScriggins: und ja ich weiss was die Leute ueber mich sagen #line:831fcb
    SteveScriggins: und ja, ich habe wahrscheinlich ein paar schlimme Sachen gemacht #line:1283cb
    SteveScriggins: aber wenigstens kann *ich* das zugeben. #line:49dc16
    SteveScriggins: du bist nicht besser als ich. #line:1c0532
    SteveScriggins: du bist nur mehr in dich selbst verliebt. #line:2829cc
    SteveScriggins: oder du bist noch duemmer. #line:2bfe46
    SteveScriggins: kann nicht sagen, was. #line:4fc743
    SteveScriggins: ist mir egal. #line:e36f5c
    Mae: Ok, ich denke es ist- #line:ea8642
    SteveScriggins: SHUT. UP. #line:fb1603
    SteveScriggins: Stell mich nicht auf die Probe, ok? #line:6b3af0
    SteveScriggins: komm nicht in die Naehe eines Mannes, wenn er so ist. #line:8c7085
    Mae: HA. #line:93d2f9
    Mae: oder was? #line:81c02b
    Mae: oder verdammt noch mal was, Steve? #line:3d506f
    SteveScriggins: ... #line:b92913
    Mae: ... #line:a782b0
    SteveScriggins: Raus hier, lass mich arbeiten. #line:c21294
    SteveScriggins: und kommen Sie nicht zurueck. #line:1039dc
    <<set $steve_scriggins_threat to 1>>
    <<set $steve_scriggins_birdland_sequence += 1>>
<<endif>>

<<set $steve_scriggins_done to 1>>
===
title: Sadie
tags: temp
colorID: 4
position: -296,427
---
<<if $did_birdland_quest and $act is 3 and $day is 4>>
	Mae: Hey! #line:eaf74e
	Sadie: Hey. #line:f10dd7
    <<startInteractingWithPlayer Saleem>>
	Saleem: Hallo. #line:7b1938
	Mae: Wollt ihr zusammen spielen? #line:01d416
	Sadie: Ja ha ha #line:b26250
    Saleem: Danke, dass du das moeglich gemacht hast, Junge. #line:81f7c4
    Mae: ha ha wow #line:92aa1d
    Mae: ... #line:b07246
    Mae: ... #line:761a9e
    Mae: huh. #line:6ef4fb
    Sadie: was? #line:b6d5d0
	Mae: Mir ist gerade klar geworden, dass... #line:728a54
	Mae: Ich habe etwas Hilfreiches getan. #line:003f53
	Mae: Als ob ich einen Plan gemacht und etwas Gutes getan haette. #line:8ccdbf
	Saleem: Ist das seltsam? #line:8ddc99
	Mae: Ich habe das Gefuehl, ich stolpere die ganze Zeit in Dinge hinein #line:4f7741
	Mae: Euch zwei vorzustellen ist wie... #line:649ce0
	Mae: Ich *wollte* zwei Fremden helfen. Und das habe ich getan. #line:4c6f32
	Mae: Und jetzt werdet ihr zusammen spielen #line:3eed91
	Mae: und euch verlieben... #line:c976ed
	Sadie: Was ist? Nein. #line:6e76e5
	Saleem: Ich bin in einer langjaehrigen monogamen Beziehung. #line:1d3750
	Sadie: Ich bin nicht auf dem Markt. #line:ae2102
    Sadie: Und mein Sohn nimmt den groessten Teil meiner Aufmerksamkeit in Anspruch. #line:189c91
	Mae: Oh. #line:1c5edc
	Mae: Habe ich es komisch gemacht? #line:40c40d
	Saleem: Du hast es merkwuerdig gemacht. #line:9b5ba5
	Sadie: Sehr seltsam. #line:1704c9
    Mae: verdammt #line:00c5c8
    <<unlockDoodle BirdlandComplete>>
    <<stopInteractingWithPlayer Saleem>>
    <<set $birdland_can_interact_posters to 1>>
    <<unlockAchievement Finish_Birdland_Quest>>
<<else>>
	<<if $sadie_sequence is 0>>
		Mae: Aehm. Hallo. #line:562cf6
		Sadie: Hallo. #line:81acb4
        Mae: Ich bin Mae. #line:63ecf0
        Sadie: Sadie. #line:ce0b09
        Mae: was machst du denn hier oben? #line:21b8fb
        Sadie: was machst *Du* hier oben? #line:30c60a
            ->erforschen #line:40c263
                Sadie: genauso wie ich es gefunden habe. #line:10afb5
            ->ich meine...brauche ich einen Grund? #line:080692
                Sadie: nee. #line:9779c5
        Sadie: wusstest du #line:9a0d6d
        Sadie: das ist die einzige Buehne in der Stadt. #line:957fae
            ->stehst du oft auf der Buehne? #line:7b8bff
                Sadie: noe. Zumindest nicht fuer eine lange, lange Zeit #line:747a60
            ->das ist nicht wahr! #line:d9aca4
                Mae: wir haben gerade ein Stueck im Baumarkt aufgefuehrt! #line:33b071
                Mae: wir haben die Theke als Buehne benutzt! #line:888baf
                Sadie: ok, das ist die einzige Buehne in der Stadt #line:6afb9d
                Sadie: das ist nur eine Buehne. #line:ffa2c6
        Mae: sieht aus, als haette hier schon lange niemand mehr gespielt #line:05f660
        Sadie: ja, ist schon ein paar Jahrzehnte her. #line:3248ea
        Mae: wirst du moegen #line:d8b38e
        Mae: hier etwas anziehen? #line:6cb9e0
        Sadie: oh, ich glaube nicht, dass irgendjemand hier etwas anziehen wird #line:68951f
        Sadie: Zumindest nicht in naechster Zeit. #line:f89ba7
        Sadie: kein Strom, wahrscheinlich strukturell unsolide. #line:a1c2f7
        Sadie: mit Ratten und Tauben verseucht. #line:cdbffd
        Mae: abgesehen von der Elektrizitaet ist das wie der groesste Teil der Stadt. #line:42ecd5
        Sadie: ha ha ha #line:627ca6
        Sadie: das ist es. das ist es. #line:e5753a
		<<set $sadie_sequence += 1>>
	<<elseif $sadie_sequence is 1>>
		Mae: Hallo nochmal. #line:719e43
		Sadie: Du bist es, Junge. #line:e69c1e
		Mae: Ich bin eigentlich 20. #line:7ba554
		Sadie: Ich bin 38. Du bist ein Kind. #line:afac7e
		Mae: Ok. #line:1feda7
		Mae: Wie hast du diesen Ort gefunden? #line:ed4846
        Sadie: Ich meine, ich wusste, dass das hier oben ist, seit ich ein Kind war #line:52e8e0
        Sadie: aber ich habe erst vor kurzem den Mut aufgebracht, hier einzubrechen #line:a47f4b
        Mae: warum ist das so? #line:de8657
        Sadie: ich werde auch nicht juenger #line:5c07b4
        Sadie: und dieses Haus koennte jeden Tag einstuerzen #line:4d1a35
        Sadie: man kommt an einen Punkt im Leben #line:4770ae
        Sadie: wo die Zahl der Erstlinge geringer wird #line:53ed2a
        Sadie: und die Zahl derer, die nie wiederkommen, wird hoeher #line:b4f56c
        Sadie: ich weiss nicht #line:a4e024
        Mae: ich weiss, was du meinst #line:33f73e
        Sadie: nein, das weisst du nicht. #line:f2eae5
		<<set $sadie_sequence += 1>>
	<<elseif $sadie_sequence is 2>>
		Mae: hast du einen Job oder so? #line:438aab
		Sadie: habe ich. #line:39a6a1
        Sadie: hatte aber ein paar Probleme. #line:e4ec1f
        Sadie: lebe jetzt bei meinen Eltern #line:93d32d
        Sadie: ich und mein Kind #line:d5c86f
        Sadie: also habe ich ein bisschen Freizeit. #line:9bb8df
        Mae: bei mir eigentlich auch. #line:110e68
        Mae: nur ohne Kind. #line:ce7cde
        Sadie: kleine Welt. #line:7d7967
		<<set $sadie_sequence += 1>>
	<<endif>>

	<<if $sadie_saleem_sequence is 0 and $saleem_sequence is 1>>
		Mae: Lustig. #line:14f2d3
		Sadie: Was? #line:fb57d1
		Mae: Ich habe gerade diesen Typen drueben bei den Kirchenstufen getroffen #line:d56f61
        Mae: er hing auch ganz alleine rum #line:7ff604
		Mae: und spielte Musik. #line:63060d
		Sadie: Kleine Welt. #line:fdd58d
		Mae: Ha heh. Na ja... #line:a589ec
		Mae: Wenigstens eine kleine Stadt. #line:2b3a7a
		Sadie: Auf jeden Fall. #line:25db82
		Mae: Man sieht sich. #line:b3714e
		<<set $sadie_saleem_sequence += 1>>
	<<elseif $sadie_saleem_sequence is 1 and $saleem_sequence is 2>>
		Mae: Du weisst schon, der Typ von gegenueber? #line:19da5a
		Sadie: welcher Typ? #line:cbdf81
		Mae: der Typ, von dem ich dir erzaehlt habe? #line:b9befd
		Sadie: ja #line:873219
		Mae: er scheint einsam zu sein #line:e0c791
        Mae: und du scheinst einsam zu sein #line:e40c63
        Sadie: aehm #line:8c7545
        Mae: ihr zwei koenntet zusammen spielen! #line:a39b11
        Sadie: aeh... #line:b8dc2a
        Mae: habe ich das komisch gemacht? #line:ead83c
        Sadie: nein, es ist in Ordnung. #line:84c663
        Sadie: was spielt er? #line:b7c473
        Mae: Geige? Fiedel? ist das dasselbe? #line:c3b4fa
		Sadie: huh. ok. #line:c4a468
        Sadie: Nun, wenn du ihn wieder siehst, sag ihm, er soll vorbeikommen. #line:aed811
		Sadie: es sei denn, er ist ein Widerling oder so #line:8a3551
		Mae: er scheint kein Widerling zu sein. #line:094633
		Sadie: nun, ich habe Pfefferspray. #line:7d9087
		<<set $will_complete_birdland_quest to 1>>
		<<set $sadie_saleem_sequence += 1>>
    <<elseif $saleem_sadie_sequence is 1 and $sadie_sequence is 2>>
        Mae: Ich habe gerade diesen Typen drueben bei den Kirchenstufen getroffen #line:51a20d
        Mae: er hing auch ganz alleine rum #line:9bd5d3
		Mae: und spielte Musik. #line:4ae33f
		Sadie: Kleine Welt. #line:6a96eb
		Mae: Ich habe ihm von dir erzaehlt. #line:e12f9f
        Mae: Ich spiele hier drueben. #line:a2743f
        Mae: Ganz allein. #line:d85572
		Sadie: Oh. OK. #line:57910f
		Mae: Vielleicht koennt ihr zusammen spielen???? #line:cf0fe9
        Sadie: Weisst du etwas ueber diesen Kerl? #line:5e9053
        Mae: Er ist kein Moerder. #line:f901a7
        <<wait 2>>
        Mae: Ich glaube nicht, dass er ein Moerder ist. #line:4f44a7
        Sadie: Ok gut... danke? #line:a1e6c2
        <<set $will_complete_birdland_quest to 1>>
		<<set $saleem_sadie_sequence += 1>>
	<<endif>>
<<endif>>

<<set $sadie_done to 1>>
===
title: InitLevel_A3D2
tags:
colorID: 4
position: -8,-25
---
<<hide Saleem>>
===
title: InitLevel_A3D3
tags:
colorID: 4
position: 292,-25
---
<<hide Saleem>>
===
title: InitLevel_A3D4
tags:
colorID: 4
position: 565,-21
---
<<if $did_birdland_quest>>
    <<show Saleem>>
    <<setCanInteract Saleem 0>>
<<else>>
    <<hide Saleem>>
<<endif>>
===
title: InteractPosters
tags:
colorID: 4
position: -12,459
---
//this interaction is enabled only after talking to them after their quest is completed//
<<startInteractingWithPlayer Sadie>>
<<startInteractingWithPlayer Saleem>>
Mae: Das war vorher nicht da. #line:28b2b0
Sadie: Ja! Ich habe sie hinter der Buehne gefunden. #line:8f19ea
Saleem: Unglaublich, dass sie noch intakt sind. #line:f16ed5
Saleem: Muessen jetzt 60... 70 Jahre alt sein? #line:1a1ce4
Mae: "Die Original Deep Hollow Hollerers" #line:270b64
Sadie: Versuch *das* dreimal schnell zu sagen #line:89cbae
    ->Deep Hollow Hollerers #line:b27488
    	Mae: Deep Hollow Hollellers #line:49532a
    ->Deep Hollow Hollerers Deep Hollow Hollerers #line:04248e
    	Mae: Deep Hollow Hollellers Deep Hollol Horellers #line:f81dce
    ->Deep Hollow Hollerers Deep Hollow Hollerers Deep Hollow Hollerers Deep Hollow Hollerers #line:afc6c9
		Mae: Deep Hollow Hollellers Deep Hollol Horellers Deep Hloler Hollerwers #line:58323d
Saleem: *aehem* #line:e3a4a2
Saleem: TIEF. HOLLOW. HOLLER-ERS. #line:f8b303
Sadie: D.H.H. #line:5d2e60
Mae: Wer sind die? #line:37a2d6
Saleem: Mensch Junge, wie lange lebst du schon hier? #line:10caf9
	->Mein ganzes Leben! #line:d11df2
		Saleem: Nun, dann hast du keine Entschuldigung. #line:f48f5a
	->Ich bin 20. #line:d8d0bc
		Sadie: Du siehst juenger aus. #line:27f964
		Sadie: Das wirst du spaeter im Leben zu schaetzen wissen. #line:d9986d
Saleem: Die Deep Hollor Horrelors - gottverdammt #line:8a05ae
Mae: HA HA. #line:688e0f
Saleem: Die D.H.H. waren *DIE* beste Tanzband in Deep Hollow County #line:af436f
Sadie: Das war ihr Club. #line:eaafe1
Sadie: Vier von ihnen. Geige, Saxophon, Tuba, Akkordeon. #line:174abe
Saleem: Polkas, Walzer, alte Melodien aus den Huegeln. #line:aedca6
Saleem: 20er bis 40er Jahre. #line:893682
Saleem: Hat diese Stadt durch harte Zeiten gebracht. #line:14fc79
Saleem: Sie waren auf dieser beruehmten Anthologie der Appalachenmusik aus den 60ern. #line:2de4f2
Sadie: Ja. Ein grosses Stueck lokaler Geschichte. #line:aa9969
->Was ist mit ihnen passiert? #line:699783
	Sadie: oh ja #line:5c13b3
	Sadie: seltsame Geschichte dort #line:28aa88
->Was ist mit diesem Ort passiert? #line:48f179
	Sadie: Nachdem sie draussen im Wald gefunden wurden #line:d24625
	Sadie: es ging lange Zeit von Besitzer zu Besitzer #line:aa8784
	Mae: "Gefunden"? "Draussen in den Waeldern?" #line:87ccac
	Sadie: oh ja. Uh. #line:0069a2
	Sadie: Seltsame Geschichte da. #line:56d1ab
Saleem: Sie wurden unheimlich. #line:796bf9
Sadie: oder irgendwie religioes? #line:a1db43
    ->gespenstisch? #line:5282a9
        Saleem: ja, sie sprachen davon, dass sie Reisen aus ihren Koerpern machen #line:1264ad
        Saleem: und sie haben fuer die Geister gespielt, die hier durch waren. #line:fd8afd
        Mae: huh. #line:a9c13b
        Saleem: die Leute dachten, sie waeren einfach nur komisch und poetisch, aber #line:7b2712
    ->religioes? #line:52c570
        Sadie: ja, sie fingen an darueber zu reden, wie #line:1fee38
        Sadie: sie haben fuer Goetter gespielt? #line:be44f0
        Sadie: aber wie Goetter, von denen niemand wusste? #line:8d7260
        Sadie: und dass sie Reisen aus ihren Koerpern heraus machten? #line:2030eb
Saleem: wer weiss. Sie wurden auch ein bisschen alt. #line:5ab54d
Mae: Das ist seltsam. #line:79ed5d
Sadie: Am Ende kamen sie nicht mehr hierher #line:4f1a42
Sadie: und fingen an, draussen in den Waeldern zu spielen #line:05143c
Sadie: auf diesen kleinen... Bandboxen, die sie gebaut haben #line:88e0c5
Saleem: Oder zumindest dachten die Leute das #line:6ad974
Saleem: Im November '49 fand man sie tot da draussen #line:3c5b83
Saleem: An der Kaelte gestorben! #line:7280f0
Saleem: Der Frost hat sie an ihren Instrumenten erfrieren lassen! #line:0397e1
Sadie: Mussten sie vom Boden schaelen. #line:7aee0b
Saleem: Einer der groessten WTF-Momente in der Geschichte von Possum Springs. #line:9aca4f
Mae: Heiliger Bimbam. #line:1b6e37
Sadie: Wie kann es sein, dass du diese Geschichte noch nicht kennst? #line:4d8464
Mae: Nein, es ist nur... #line:ee9aba
Mae: Ich... #line:9ab328
Mae: nichts. Die Welt ist gross und seltsam. #line:9c9f50
Saleem: Nachdem sie weg waren, waren die Tage dieses Ortes gezaehlt. #line:3483d7
Saleem: Und jetzt! zum ersten Mal seit Gott weiss wie lange! #line:655cb0
Saleem: Der Husker Bee Ballroom soll wieder Musik hoeren! #linen:664375
Sadie: Ha ha ha. #line:495145
Saleem: Und es ist ein volles Haus! Tauben wissen Qualitaet zu schaetzen! #line:d86719
Mae: Das macht mich gluecklich. #line:9dbdae
Sadie: Heb dir das Glueck auf, wenn es uns gut geht. #line:ae8d6b
Saleem: Ja. Lasst uns gut werden. #line:37bd29
Sadie: Dieser Ort koennte etwas Gutes gebrauchen. #line:378305
Saleem: Nun, das ist eine Sache mit der Musik- #line:15c89f
Saleem: sie fuellt den leeren Raum! #line:9cdec2
<<stopInteractingWithPlayer Saleem>>
<<stopInteractingWithPlayer Sadie>>
<<set $birdland_interact_posters_done to 1>>
===
title: EnterLevel
tags:
colorID: 0
position: 373,216
---
<<setSpriteLayer Saleem BG1>>
<<setSpriteLayer Sadie BG1>>
<<unlockDoodle Birdland>>
===
title: LottaBirds
tags:
colorID: 0
position: 804,223
---
Mae: Heilige Scheisse. Das sind eine Menge Voegel. #line:ed24e3
<<end>>
===
title: PeopleUsedToComeHere
tags:
colorID: 0
position: 1052,220
---
Mae: wow. #line:6d8982
Mae: die Leute mochten #line:33b594
Mae: komm her. #line:694849
<<end>>
===
title: Pentagram
tags: temp
colorID: 4
position: -347,141
---
<<if $pentagram_quest>>
    Mae: Whoa. Pentagramm. #line:56b789
    Mae: beweist nichts, seltsame Teenager. #line:a07291
    <<wait 2>>
    Mae: Trotzdem gruselig. #line:8a687e
    <<set $found_pentagrams += 1>>
    <<unlockPentagramDoodle>>
<<else>>
    Mae: Huh. Pentagramm. #line:0ee235
<<endif>>
<<set $birdland_pentagram_done to 1>>
===
